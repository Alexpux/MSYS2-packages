# Contributor (MSYS2): David Macek <david.macek.0@gmail.com>
# Maintainer (Arch Linux): Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor (Arch Linux): Allan McRae <allan@archlinux.org>
# Contributor (Arch Linux): bekks <eduard.warkentin@gmail.com>
# Contributor (Arch Linux): Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=upx
pkgver=3.93
pkgrel=1
pkgdesc='Ultimate executable compressor.'
arch=('i686' 'x86_64')
url='https://upx.github.io/'
license=('GPL')
depends=('ucl' 'zlib')
makedepends=('ucl-devel' 'zlib-devel')
source=("https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        'https://downloads.sourceforge.net/sevenzip/lzma465.tar.bz2')
sha256sums=('400828607a45f43fd9a0cc42c843e53a34aa46bb5541313d897fb4b0b683bafc'
            'c935fd04dd8e0e8c688a3078f3675d699679a90be81c12686837e0880aa0fa1e')

build() {
  cd "${pkgname}-${pkgver}-src"
  CPPFLAGS+=" -DUCL_NO_ASM"
  make UPX_LZMA_VERSION=0x465 UPX_LZMADIR="${srcdir}" all
}

package() {
  cd "${pkgname}-${pkgver}-src"

  install -Dm0755 src/upx.out "${pkgdir}/usr/bin/upx"
  install -Dm0644 doc/upx.1 "${pkgdir}/usr/share/man/man1/upx.1"
  install -Dm0644 LICENSE "${pkgdir}/usr/share/license/${pkgname}/LICENSE"
}
