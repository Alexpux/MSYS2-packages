# Contributor (MSYS2): David Macek <david.macek.0@gmail.com>
# Maintainer (Arch Linux): Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor (Arch Linux): Allan McRae <allan@archlinux.org>
# Contributor (Arch Linux): bekks <eduard.warkentin@gmail.com>
# Contributor (Arch Linux): Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=upx
pkgver=3.94
pkgrel=1
pkgdesc='Ultimate executable compressor.'
arch=('i686' 'x86_64')
url='https://upx.github.io/'
license=('GPL')
depends=('ucl' 'zlib')
makedepends=('ucl-devel' 'zlib-devel')
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        "lzma-sdk-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}-lzma-sdk/archive/v${pkgver}.tar.gz"
        'basename.patch')
sha256sums=('2ddd9d409620b6946319bfdab6cf9d570c6057a89cd5b0b4707fc5e13cc95929'
            '9e9cea6112bd93fbc71c61d1ff068cc324b2d7bb7789043928b68c5d3cc0672d'
            'c9a50f9983ade8d07c14edec2ff60f65185d8870e1a39f9e47d9f093acef4476')

prepare() {
  cp -rf upx-lzma-sdk-${pkgver}/* "${pkgname}-${pkgver}/src/lzma-sdk"
  cd "${pkgname}-${pkgver}"
  patch -p0 -i ${srcdir}/basename.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  CPPFLAGS+=" -DUCL_NO_ASM"
  #make UPX_LZMA_VERSION=0x465 UPX_LZMADIR="${srcdir}" all
  make all
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm0755 src/upx.out "${pkgdir}/usr/bin/upx"
  install -Dm0644 doc/upx.1 "${pkgdir}/usr/share/man/man1/upx.1"
  install -Dm0644 LICENSE "${pkgdir}/usr/share/license/${pkgname}/LICENSE"
}
