# Maintainer: João Guerra <joca.bt@gmail.com>

pkgbase="python-pygments"
pkgname=("python2-pygments" "python3-pygments")
pkgver=2.0.2
pkgrel=2
pkgdesc="A syntax highlighting engine written in Python"
arch=("i686" "x86_64")
url="http://pygments.org/"
license="BSD"
makedepends=("python2-setuptools" "python3-setuptools")
source=("https://pypi.python.org/packages/source/P/Pygments/Pygments-${pkgver}.tar.gz")
noextract=("Pygments-${pkgver}.tar.gz")
sha1sums=('fe2c8178a039b6820a7a86b2132a2626df99c7f8')

prepare () {
  # workaround for symlink issue
  tar -xf Pygments-${pkgver}.tar.gz || true
  cp Pygments-${pkgver}/scripts/debug_lexer.py Pygments-${pkgver}/scripts/find_error.py
}

package_python2-pygments () {
  depends=("python2")
  install="python2-pygments.install"

  cd "Pygments-${pkgver}"
  python2 setup.py install --root="${pkgdir}" -O1
  mv "${pkgdir}/usr/bin/pygmentize" "${pkgdir}/usr/bin/pygmentize2"
  install -Dm644 external/pygments.bashcomp "${pkgdir}/usr/share/bash-completion/completions/pygmentize2"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python3-pygments () {
  depends=("python3")

  cd "Pygments-${pkgver}"
  python3 setup.py install --root="${pkgdir}" -O1
  install -Dm644 external/pygments.bashcomp "${pkgdir}/usr/share/bash-completion/completions/pygmentize"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
