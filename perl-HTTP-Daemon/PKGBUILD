# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTTP-Daemon
pkgname=perl-${_realname}
pkgver=6.06
pkgrel=1
pkgdesc="A simple http server class"
arch=('any')
url="https://metacpan.org/release/HTTP-Daemon"
groups=('perl-modules')
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-HTTP-Date' 'perl-HTTP-Message' 'perl-LWP-MediaTypes')
checkdepends=('perl-Test-Needs')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/${_realname}-${pkgver}.tar.gz")
sha256sums=('fc03a161b54553f766457a4267e7066767f54ad01cacfe9a91d7caa2a0319bad')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
