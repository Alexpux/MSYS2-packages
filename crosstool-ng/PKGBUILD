# Maintainer: Martell Malone <martell malone at g mail dot com>

_realname=crosstool-ng
pkgname="${_realname}-git"
_ver_base=1.20
pkgver=1.20.3053.96f4ae4
pkgrel=1
pkgdesc="A cross-platform open-source toolchain system"
arch=('i686' 'x86_64')
url="http://www.crosstool-ng.org/"
license=("MIT")
makedepends=("git" "gcc" "autoconf" "gperf" "binutils" "bison" "flex" "wget" "tar" "patch" "make" "texinfo" "libtool" "automake" "xz")
provides=("${_realname}")
conflicts=("${_realname}")
depends=("ncurses-devel")
options=('staticlibs' 'strip')

source=("${_realname}"::"git+https://github.com/martell/crosstool-ng.git#branch=msys2"
        "0001-Modify-config-to-support-correct-Mingw64-Triplet-Nam.patch")

md5sums=('SKIP'
         '17e3292a692e6861566e4341ab0d8867')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_realname"
  git am "$srcdir/0001-Modify-config-to-support-correct-Mingw64-Triplet-Nam.patch"
}

build() {
  cd "$srcdir/$_realname"
  ./bootstrap
  ./configure --build=${CHOST} \
    --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_realname"
  make DESTDIR=${pkgdir} install
}
