# Maintainer: Martell Malone <martellmalone@gmail.com>

pkgname="crosstool-ng"
pkgver=1.21.0
pkgrel=1
pkgdesc="A cross-platform open-source toolchain system"
arch=('i686' 'x86_64')
url="http://www.crosstool-ng.org/"
license=("MIT")
makedepends=("autoconf" "automake-wrapper" "binutils" "bison"
             "flex" "gettext-devel" "git" "gcc" "gperf"
             "patch" "libtool" "make" "ncurses-devel"
             "tar" "texinfo" "wget" "xz")
depends=("ncurses" "libintl")
options=('staticlibs' 'strip')
source=("http://crosstool-ng.org/download/crosstool-ng/crosstool-ng-${pkgver}.tar.xz"{,.sig}
        "0001-Modify-config-to-support-correct-Mingw64-Triplet-Nam.patch"
        "0002-use-wide-versions-of-libs-for-nconf.patch"
        "0003-modify-kconfig-make-lintl.patch"
        "0004-kconfig-nconf-fix-compile-with-ncurses-reentrant-API.patch")
sha256sums=('fbadabcdbe1c81f8752f7b1c740120a1357cc18f73bca5b409888404dde31c12'
            'SKIP'
            'c55cf0f56a2ebbd81ff63993eb470f77a35c3cf001cfb47d95384e7e8f26834e'
            'de75159607310d30365609003768e903c67f4aeb1fbf48d91c39c6b38ded8e35'
            '43a37c96f0a8c245b380f666750b2287e7fe54ff888c95cde2f99f92194044a1'
            'd77d273c0aedb1b7c348d2e5c312de38687eccd3dffc88dcf513deec498a16be')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-Modify-config-to-support-correct-Mingw64-Triplet-Nam.patch"
  patch -p1 -i "${srcdir}/0002-use-wide-versions-of-libs-for-nconf.patch"
  patch -p1 -i "${srcdir}/0003-modify-kconfig-make-lintl.patch"
  patch -p1 -i "${srcdir}/0004-kconfig-nconf-fix-compile-with-ncurses-reentrant-API.patch"
  ./bootstrap
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
