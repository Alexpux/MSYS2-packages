# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=dos2unix
pkgver=7.3.2
pkgrel=2
pkgdesc='Text file format converter'
arch=('i686' 'x86_64')
url='http://waterlan.home.xs4all.nl/dos2unix.html'
license=('BSD')
depends=('libintl')
groups=('base-devel')
makedepends=('perl' 'gettext-devel')
conflicts=('hd2u')
source=("http://waterlan.home.xs4all.nl/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c7e8ee0bb3e001cc25a4a908d9a81ac52d124133d6a524a59f995bc90d438689')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
# Adding -g via CFLAGS_USER, because we don't want the extra debug messages.
# They are annoying for normal use.
# Headsup: in the next dos2unix release 7.3.3 we can use DEBUG=1 again.
  make DEBUG=0 CFLAGS_USER=-g STRIP=true
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm755 ${pkgdir}/usr/bin/dos2unix.exe ${pkgdir}/usr/bin/d2u.exe
  install -Dm755 ${pkgdir}/usr/bin/unix2dos.exe ${pkgdir}/usr/bin/u2d.exe
  install -D -m644 COPYING.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
