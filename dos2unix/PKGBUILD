# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=dos2unix
pkgver=7.3.4
pkgrel=1
pkgdesc='Text file format converter'
arch=('i686' 'x86_64')
url='https://waterlan.home.xs4all.nl/dos2unix.html'
license=('BSD')
depends=('libintl')
groups=('base-devel')
makedepends=('perl' 'gettext-devel')
conflicts=('hd2u')
source=("https://waterlan.home.xs4all.nl/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        dos2unix-7.3.4-msys2.patch )
sha256sums=('8ccda7bbc5a2f903dafd95900abb5bf5e77a769b572ef25150fde4056c5f30c5'
            '103d99896fd87d00871097b36ed80ba4e0fc9bf0f158e1d53a63c9f05a339bcb')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/dos2unix-7.3.4-msys2.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DEBUG=1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm755 ${pkgdir}/usr/bin/dos2unix.exe ${pkgdir}/usr/bin/d2u.exe
  install -Dm755 ${pkgdir}/usr/bin/unix2dos.exe ${pkgdir}/usr/bin/u2d.exe
  install -D -m644 COPYING.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
