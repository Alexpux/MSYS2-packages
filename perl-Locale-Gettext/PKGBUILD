# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=perl-Locale-Gettext
_realname=gettext
pkgver=1.05
pkgrel=4
groups=('perl-modules')
pkgdesc="Permits access from Perl to the gettext() family of functions"
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('gettext' 'perl')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/P/PV/PVANDRY/${_realname}-${pkgver}.tar.gz"
        'compatibility-with-POSIX-module.patch'
        '1.05-as-needed.patch'
        'doc-update.diff')
sha256sums=('27367f3dc1be79c9ed178732756e37e4cfce45f9e2a27ebf26e1f40d80124694'
            '080e3c8f26573ca7425e16190a9905eeefd80b3616fb23b2a92b45486decf247'
            '2ba2bdb1e10af8d8dcdc1ff9e148bd804777d41954a32664fd8ccdb29cb1a2cd'
            '8b1b404bd4e23904d182850856fff9c0fce4bf3240655a39d588e1b12542fe9b')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i ${srcdir}/compatibility-with-POSIX-module.patch
  patch -Np1 -i ${srcdir}/doc-update.diff
  patch -Np2 -i ${srcdir}/1.05-as-needed.patch
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # fix permissons
  find ${pkgdir}/usr -type f \( -name *.dll -o -name *.exe \) | xargs chmod 0755

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
