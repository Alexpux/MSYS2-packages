# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=libedit
pkgname=('libedit' 'libedit-devel')
pkgver=3.1
pkgrel=20150325
pkgdesc="Libedit is an autotool- and libtoolized port of the NetBSD Editline library."
groups=('libraries')
arch=('i686' 'x86_64')
license=('BSD')
url="http://www.thrysoee.dk/editline/"
depends=('msys2-runtime' 'ncurses' 'sh')
makedepends=('ncurses-devel')
options=('staticlibs')
source=(http://www.thrysoee.dk/editline/${pkgname}-${pkgrel}-${pkgver}.tar.gz
        libedit-20130712-1.src.patch
        libedit-3.0-20120311-msys2.patch)
sha256sums=('c88a5e4af83c5f40dda8455886ac98923a9c33125699742603a88a0253fcc8c5'
            'e973c43b6576fbafef69f05c3c95ee256bfb972d3622e8c87658e737415cc258'
            'd810791fc6da262ca712b1be34994ff1b6c80bde5fc4b587e22570f022c2a0c6')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgrel}-${pkgver}"

  patch -p2 -i ${srcdir}/libedit-20130712-1.src.patch
  patch -p1 -i ${srcdir}/libedit-3.0-20120311-msys2.patch
  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgrel}-${pkgver}"

  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --enable-widec \
    CPPFLAGS="$CPPFLAGS -I/usr/include/ncursesw" \
    LDFLAGS="$LDFLAGS -L/usr/lib/ncursesw"

  make

  make DESTDIR=${srcdir}/dest install
}

package_libedit() {
  groups=('libraries')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libedit-devel() {
  pkgdesc="libedit headers and libraries"
  groups=('development')
  depends=("libedit=${pkgver}" 'ncurses-devel')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
