# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=("libXau" "libXau-devel")
pkgver=1.0.8
pkgrel=1
pkgdesc="A Sample Authorization Protocol for X (libraries)"
arch=('i686' 'x86_64')
url="http://www.x.org/"
license=('MIT')
groups=()
makedepends=('gcc' 'util-macros' 'xproto')
depends=()
options=()
source=("http://xorg.freedesktop.org/releases/individual/lib/$pkgname-$pkgver.tar.bz2")
sha256sums=('fdd477320aeb5cdd67272838722d6b7d544887dfe7de46e1e7cc0c27c2bea4f2')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  autoreconf -ivf
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
  make DESTDIR="$srcdir/dest" install
  install -Dm644 ./COPYING $srcdir/dest/usr/share/licenses/$pkgname/COPYING
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package_libXau() {
  groups=('libraries')

  mkdir -p $pkgdir/usr
  cp -rf $srcdir/dest/usr/bin $pkgdir/usr/
  cp -rf $srcdir/dest/usr/share $pkgdir/usr/
}

package_libXau-devel() {
  pkgdesc="LibXau headers and libraries (development)"
  groups=('development')
  depends=("libXau=${pkgver}" 'util-macros' 'xproto')

  mkdir -p $pkgdir/usr
  cp -rf $srcdir/dest/usr/include $pkgdir/usr/
  cp -rf $srcdir/dest/usr/lib $pkgdir/usr/
}
