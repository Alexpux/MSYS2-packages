# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=flex
pkgver=2.6.3
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"
arch=('i686' 'x86_64')
url="https://github.com/westes/flex"
license=('custom')
groups=('base' 'base-devel')
depends=('m4' 'sh' 'libiconv' 'libintl')
makedepends=('libiconv-devel' 'gettext-devel')
options=('staticlibs')
install=flex.install
source=(https://github.com/westes/flex/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.lz
        flex-2.5.38-msys2.patch)
sha256sums=('907eeec6db1af86a26921888c2912e5d68ac5499ce5727d7b850a666ae209326'
            '6ffcb831faa9c29c44b717825744887ea5b3945605153831339e0b8a37cbe5c3')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/flex-2.5.38-msys2.patch

  ./autogen.sh
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    --enable-static \
    --disable-shared

  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # these tests used features removed in bison-2.6
  sed -i -e '/test-bison-yylloc/d' -e '/test-bison-yylval/d' tests/Makefile.in

  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  cp -f ${pkgdir}/usr/bin/flex.exe ${pkgdir}/usr/bin/flex++.exe
  cp -f ${pkgdir}/usr/bin/flex.exe ${pkgdir}/usr/bin/lex.exe

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgdir}/license.txt
}
