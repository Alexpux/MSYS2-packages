# Maintainer: Renato Silva <br.renatosilva@gmail.com>

pkgname='quilt'
pkgver='0.64'
pkgrel=1
pkgdesc='Manage large numbers of patches'
url='http://savannah.nongnu.org/projects/quilt'
groups=('base-devel')
license=('GPL2+')
arch=('any')

depends=(bash bzip2 diffstat diffutils findutils gawk gettext gzip patch perl)
source=("https://savannah.nongnu.org/download/quilt/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c4bfd3282214a288e8d3e921ae4d52e73e24c4fead72b5446752adee99a7affd')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
        --prefix='/usr' \
        --mandir='/usr/share/man' \
        --without-rpmbuild

    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make prefix="${pkgdir}/usr" mandir="${pkgdir}/usr/share/man" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

    # Bash completion
    mkdir "${pkgdir}/usr/share/bash-completion"
    mv "${pkgdir}/etc/bash_completion.d" "${pkgdir}/usr/share/bash-completion/completions"
}
