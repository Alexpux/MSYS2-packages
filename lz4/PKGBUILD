# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=('lz4' 'liblz4' 'liblz4-devel')
pkgver=1.9.0
pkgrel=1
pkgdesc='Very fast lossless compression algorithm'
arch=('i686' 'x86_64')
url='http://www.lz4.org/'
license=('LGPL')
source=(lz4-${pkgver}.tar.gz::"https://github.com/lz4/lz4/archive/v${pkgver}.tar.gz"
        001-mingw-msys-cygwin-build-install.patch)
sha256sums=('f8b6d5662fa534bd61227d313535721ae41a68c9d84058b7b7d86e143572dcfb'
            '3da9f11c7cae285af354a13ff3cf01fd96fb5fac3e8d0ef352c71fe7a4441876')
options=(!libtool)

prepare() {
  cd ${srcdir}/lz4-${pkgver}

  patch -bp1 -i ${srcdir}/001-mingw-msys-cygwin-build-install.patch
}

build() {
  cd ${srcdir}/lz4-${pkgver}
  make CC=/usr/bin/gcc CFLAGS="${CFLAGS}" all
  make DESTDIR=${srcdir}/dest PREFIX=/usr install
}

check() {
  cd ${srcdir}/lz4-${pkgver}
  make check || true
}

package_lz4() {
  groups=('libraries')
  depends=('gcc-libs' "lz4=${pkgver}")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  rm -f ${pkgdir}/usr/bin/*.dll
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_liblz4() {
  depends=('gcc-libs')

  mkdir -p ${pkgdir}/usr/bin
  cp -rf ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_liblz4-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("liblz4=${pkgver}")
  pkgdesc="lz4 headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
