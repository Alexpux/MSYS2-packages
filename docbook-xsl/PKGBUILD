# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=docbook-xsl
pkgver=1.78.1
pkgrel=3
pkgdesc='XML stylesheets for Docbook-xml transformations'
arch=('any')
license=('custom')
url='https://docbook.sourceforge.io/'
depends=('libxml2' 'libxslt' 'docbook-xml')
install="$pkgname.install"
source=("https://downloads.sourceforge.net/docbook/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('c98f7296ab5c8ccd2e0bc07634976a37f50847df2d8a59bdb1e157664700b467')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  _pkgroot=${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}

  install -dm755 ${_pkgroot}
  install -m644 VERSION VERSION.xsl ${_pkgroot}

  for fn in assembly common eclipse epub epub3 fo highlighting html htmlhelp javahelp lib \
      manpages params profiling roundtrip template website xhtml xhtml-1_1 xhtml5; do
    install -dm755 ${_pkgroot}/${fn}
    install -m644 ${fn}/*.{xml,xsl,dtd,ent} ${_pkgroot}/${fn} || true  # ignore missing files
  done

  install -dm755 ${pkgdir}/etc/xml
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
