# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=snowballstemmer
pkgbase=python-${_realname}
pkgname=("python2-${_realname}" "python3-${_realname}")
pkgver=1.2.1
pkgrel=1
pkgdesc="Snowball stemming library collection for Python (mingw-w64)"
arch=('any')
license=('BSD')
url='https://github.com/shibukawa/snowball_py'
makedepends=("python2-setuptools"
             "python3-setuptools")
options=('staticlibs')
source=("https://pypi.python.org/packages/source/s/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('919f26a68b2c17a7634da993d91339e288964f93c274f1343e3bbbe2096e1128')

prepare() {
  cd ${srcdir}
  cp -r ${_realname}-${pkgver} python2-build
  cp -r ${_realname}-${pkgver} python3-build
}

build() {
  cd "${srcdir}"
  for builddir in python{2,3}-build; do
    pushd ${builddir}
    /usr/bin/${builddir%-build} setup.py build
    popd
  done
}

package_python2-snowballstemmer() {
  depends=("python2")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    /usr/bin/python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1

  install -Dm644 LICENSE.rst "${pkgdir}/usr/share/licenses/python2-${_realname}/LICENSE.rst"
}

package_python3-snowballstemmer() {
  depends=("python3")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    /usr/bin/python3 setup.py install --prefix=/usr --root="${pkgdir}" -O1

  install -Dm644 LICENSE.rst "${pkgdir}/usr/share/licenses/python3-${_realname}/LICENSE.rst"
}

