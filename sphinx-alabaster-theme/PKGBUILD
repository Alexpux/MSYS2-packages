# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=sphinx-alabaster-theme
pkgbase=python-${_realname}
pkgname=("python2-${_realname}" "python3-${_realname}")
pkgver=0.7.8
pkgrel=1
pkgdesc="Modified Kr Sphinx doc theme"
arch=('any')
license=('BSD')
url='https://github.com/bitprophet/alabaster'
makedepends=("python2-setuptools"
             "python3-setuptools")
options=('staticlibs')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/bitprophet/alabaster/archive/${pkgver}.tar.gz")
sha256sums=('ebfea26e1a6d459432d995717316ab786ebc8bd43d859228e5bea31b3ebd2a32')

prepare() {
  cd ${srcdir}
  cp -r alabaster-${pkgver} python2-build
  cp -r alabaster-${pkgver} python3-build
}

build() {
  cd "${srcdir}"
  for builddir in python{2,3}-build; do
    pushd ${builddir}
    /usr/bin/${builddir%-build} setup.py build
    popd
  done
}

check() {
  cd "${srcdir}"
  for builddir in python{2,3}-build; do
    pushd ${builddir}
    /usr/bin/${builddir%-build} setup.py test
    #py.test
    popd
  done
}

package_python2-sphinx-alabaster-theme() {
  depends=("python2")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    /usr/bin/python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python2-${_realname}/LICENSE"
}

package_python3-sphinx-alabaster-theme() {
  depends=("python3")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    /usr/bin/python3 setup.py install --prefix=/usr --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python3-${_realname}/LICENSE"
}

