# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Archive-Zip
pkgname=perl-${_realname}
pkgver=1.46
pkgrel=1
pkgdesc="Provide a perl interface to ZIP archive files"
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/Archive-Zip/"
depends=('perl')
groups=('perl-modules')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/P/PH/PHRED/${_realname}-${pkgver}.tar.gz)
sha256sums=('6630ebeba753a7025126028eb290040ce44d86e8ced77017e31b75863348e524')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
