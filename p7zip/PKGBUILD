# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=p7zip
pkgver=9.38.1
pkgrel=1
pkgdesc='Command-line version of the 7zip compressed file archiver'
url='https://p7zip.sourceforge.io/'
license=('GPL' 'custom')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'bash')
makedepends=('nasm' 'dos2unix')
groups=('compression')
options=('emptydirs')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}_${pkgver}_src_all.tar.bz2"
        01_makefile_9.20.1.patch
        02_man_9.20.1.patch
        03_respect-umask-partial_9.20.1.patch
        10-const-ref-init.patch
        p7zip-9.20.1-1.src.patch)
sha256sums=('fd5019109c9a1bf34ad3257d37a6853eae8151ff50345f0a3ffba7d8c5fdb995'
            '9887537b9e041cb0eb24e9eaa0b59c99f3658e635030f3ea879573fcd8ba23b5'
            '1d7fae6eadb966ca3819421e357bea5d9bb245de241f08971cb78f01d2036964'
            '8427a23869532c5a28a1d76313033cdb4dd7eb5d914966b8950844ec968f0c57'
            '95b85ad15d2dba779de114727aee890db2bd69ac960fcea647712ca8e1d343fb'
            'da353b6d2fb9682e001b453e0f6fbb7d380623adfc93e58fb0c3aea55ade0985')

options=('!makeflags')

prepare() {
  cd "${srcdir}/${pkgname}_${pkgver}"
  
  patch -p1 -i ${srcdir}/01_makefile_9.20.1.patch
  dos2unix man1/7zr.1
  patch -p1 -i ${srcdir}/02_man_9.20.1.patch
  patch -p1 -i ${srcdir}/03_respect-umask-partial_9.20.1.patch
  patch -p1 -i ${srcdir}/10-const-ref-init.patch
  patch -p1 -i ${srcdir}/p7zip-9.20.1-1.src.patch
  
  [[ $CARCH = x86_64 ]] \
    && cp makefile.cygwin makefile.machine \
    || cp makefile.cygwin_asm makefile.machine

  rm GUI/kde4/p7zip_compress.desktop # FS#43766
}

build() {
  cd "${srcdir}/${pkgname}_${pkgver}"
  make all3 OPTFLAGS="${CXXFLAGS}"
}

check() {
  cd "${srcdir}/${pkgname}_${pkgver}"
  make test
  make test_7z
  make test_7zr
}

package() {
  cd "${srcdir}/${pkgname}_${pkgver}"
  
  #local sedexp1="s,^DEST_DIR=.*\$,DEST_DIR=${D},";
  #local sedexp2="s,^EXEEXT=.*\$,EXEEXT=.exe,";

  #mv -f install.sh install.sh.orig
  #cat install.sh.orig | sed -e "$sedexp1" -e "$sedexp2" > ./install.sh
  #chmod +x install.sh
  #./install.sh
  
  make install \
    DEST_DIR="${pkgdir}" \
    DEST_HOME="/usr" \
    DEST_MAN="/usr/share/man"

  # Licenses
  #install -d "${pkgdir}"/usr/share/licenses/p7zip
  #ln -s -t "${pkgdir}"/usr/share/licenses/p7zip \
  #  /usr/share/doc/p7zip/DOCS/License.txt \
  #  /usr/share/doc/p7zip/DOCS/unRarLicense.txt
  
  #cp contrib/gzip-like_CLI_wrapper_for_7z/README ${B}/p7zip-script.README
  #dodoc p7zip-script.README

  install -Dm755 contrib/gzip-like_CLI_wrapper_for_7z/p7zip ${pkgdir}/usr/bin/p7zip
  install -Dm644 contrib/gzip-like_CLI_wrapper_for_7z/man1/p7zip.1 ${pkgdir}/usr/share/man/man1/p7zip.1

  chmod -R a+r,u+w,a+X "${pkgdir}/usr"
}
