# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=sed
pkgver=4.4
pkgrel=1
pkgdesc="GNU stream editor"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/sed"
license=('GPL3')
groups=('base' 'base-devel')
depends=('libintl' 'sh')
makedepends=('gettext' 'gettext-devel')
install=sed.install
source=(https://ftp.gnu.org/pub/gnu/sed/${pkgname}-${pkgver}.tar.xz{,.sig}
        sed-4.4-1.src.patch
        sed-4.2.2-msys2.patch)
sha256sums=('cbd6ebc5aaf080ed60d0162d7f6aeae58211a1ee9ba9bb25623daa6cd942683b'
            'SKIP'
            '12525e7bf4a1b57913d4047a2313f9127f29468193c2b583cfdfa128b4141032'
            '0dfaa048d9edb8478e079b40bb61cc46f270cc85058d12def9382096dacdd71e')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/sed-4.2.2-msys2.patch

  autoreconf -fiv

  patch -p2 -i ${srcdir}/sed-4.4-1.src.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --build=${CHOST}
  make all html
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
