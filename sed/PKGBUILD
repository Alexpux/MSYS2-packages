# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=sed
pkgver=4.2.2
pkgrel=2
pkgdesc="GNU stream editor"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/sed"
license=('GPL3')
groups=('base' 'base-devel')
depends=('libintl' 'sh')
makedepends=('gettext' 'gettext-devel')
install=sed.install
source=(https://ftp.gnu.org/pub/gnu/sed/${pkgname}-${pkgver}.tar.gz{,.sig}
        sed-4.2.2-3.src.patch
        sed-4.2.2-msys2.patch)
sha256sums=('fea0a94d4b605894f3e2d5572e3f96e4413bcad3a085aae7367c2cf07908b2ff'
            'SKIP'
            '8e24c54995a5c7b9b00fc09940567c1ddd6cbd70a983c4c94ebf5d1965a20b58'
            '57f3c7addeb9663eacb467a289b57b10c4775eb5f7f44c73511581469bdc33ef')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p2 -i ${srcdir}/sed-4.2.2-3.src.patch
  patch -p1 -i ${srcdir}/sed-4.2.2-msys2.patch

  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --build=${CHOST}
  make all html
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  cd doc
  make DESTDIR=${pkgdir} install-html
}
