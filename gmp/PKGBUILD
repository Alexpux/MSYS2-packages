# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgbase=gmp
pkgname=('gmp' 'gmp-devel')
pkgver=6.1.0
pkgrel=4
pkgdesc="A free library for arbitrary precision arithmetic"
groups=('libraries')
arch=('i686' 'x86_64')
url="https://gmplib.org/"
license=('LGPL3')
install=gmp.install
source=(https://gmplib.org/download/gmp-${pkgver}/gmp-${pkgver}.tar.xz{,.sig}
        gmp.hga090b0c741d6.patch
        gmp-5.1.2-msys2.patch)
sha256sums=('68dadacce515b0f8a54f510edf07c1b636492bcdb8e8d54c56eb216225d16989'
            'SKIP'
            '3e25844051fe1bd3d3137f0435978024076a92ddbf084ec47732cf5c816c74dd'
            'c1ff80a8643a362f44be680a5430dca6f4ec16d84a4068d344a24b8d181e5436')
validpgpkeys=('5972C48224C1E7E62AA0275D1150EE33C1A000B0')   # Marco Bodrato

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/gmp.hga090b0c741d6.patch
  patch -p1 -i ${srcdir}/gmp-5.1.2-msys2.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --enable-cxx \
    --enable-fat \
    --enable-shared --disable-static
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package_gmp() {
  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/bin ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/share ${pkgdir}/usr/
}

package_gmp-devel() {
  groups=('development')
  options=(staticlibs)
  depends=("gmp=${pkgver}")
  pkgdesc="GMP headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/include ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/lib ${pkgdir}/usr/
}
