# Maintainer: Green0Photon <green0photon AT gmail DOT com>

pkgname=task
pkgver=2.5.1
pkgrel=1
pkgdesc="A command-line todo list manager"
arch=('i686' 'x86_64')
url="https://taskwarrior.org/"
license=("MIT")

depends=("gcc-libs"
         "libgnutls"
         "libutil-linux"
         "libhogweed" # for some reason, didn't install with libgnutls
)

makedepends=("git" # 'msys2-w32api-headers>=5.0.0.4624' 'msys2-w32api-runtime'
             "gcc"
             "make"
             "cmake"
             "libgnutls-devel"
             "libutil-linux-devel"
)

# AUR packages had an odd colon notation
# The comments were within the quotes so pacman knew about it
optdepends=("bash-completion" # for bash completion
            "python" # for python export addon
            "ruby" # for ruby export addon
            "perl" # for perl export addon
            "perl-json" # for perl export addon
            "rsync" # synchronisation via rsync
            "curl" # synchronisation via http(s)/ftp
            "openssh" # synchronisation via ssh
)

checkdepends=("python" # for tests
              "bash" # for tests
)

# This has symlink problems with files missing
#source=("http://taskwarrior.org/download/${pkgname}-${pkgver}.tar.gz")
#sha256sums=("d87bcee58106eb8a79b850e9abc153d98b79e00d50eade0d63917154984f2a15")

source=("${pkgname}-${pkgver}::git+https://git.tasktools.org/scm/tm/task.git#tag=v${pkgver}")
sha256sums=("SKIP")

#prepare() {
#  pacman -Syu --noconfirm # fix for appveyor old files
#}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
#  ls "/usr/include/sys"
#  pacman -Q
  msg "Starting cmake..."
  cmake -DCMAKE_BUILD_TYPE=release -DCMAKE_INSTALL_PREFIX=/usr .
  
  msg "Starting make..."
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=$pkgdir install

  # Note that we rename the bash completion script for bash-completion > 1.99, until upstream does so.
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/bash/task.sh" "$pkgdir/usr/share/bash-completion/completions/task"
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/fish/task.fish" "$pkgdir/usr/share/fish/completions/task.fish"
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/zsh/_task" "$pkgdir/usr/share/zsh/site-functions/_task"

  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/vim/ftdetect/task.vim" "$pkgdir/usr/share/vim/vimfiles/ftdetect/task.vim"
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/vim/syntax/taskdata.vim" "$pkgdir/usr/share/vim/vimfiles/syntax/taskdata.vim"
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/vim/syntax/taskedit.vim" "$pkgdir/usr/share/vim/vimfiles/syntax/taskedit.vim"
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/vim/syntax/taskrc.vim" "$pkgdir/usr/share/vim/vimfiles/syntax/taskrc.vim"

  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/task/LICENSE"
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}/test"
  
  msg "Starting make for testing..."
  make
  
  msg "Running tests (Please be patient)..."
  ./run_all || true
  msg "Results:"
  ./python problems || true
}
