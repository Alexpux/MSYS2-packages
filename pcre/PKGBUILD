# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=pcre
pkgname=('pcre' 'libpcre' 'libpcre16' 'libpcre32' 'libpcrecpp' 'libpcreposix' 'pcre-devel')
pkgver=8.40
pkgrel=2
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('i686' 'x86_64')
url="http://www.pcre.org/"
license=('BSD')
makedepends=('libreadline-devel' 'libbz2-devel' 'zlib-devel')
source=(https://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${pkgname}-${pkgver}.tar.bz2{,.sig}
        pcre-8.33-msys2-fix-ln.patch
        pcre-8.33-msys2.patch)
sha256sums=('00e27a29ead4267e3de8111fcaa59b132d0533cdfdbdddf4b0604279acbcf4f4'
            'SKIP'
            '59d420f6e48f527a464902913a101be27297fb26318d532f67ec0a245ea39db8'
            'da1665915f40d515c3179afcffee6e537fcc3da59fd771ae79347a80cb604f70')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/pcre-8.33-msys2-fix-ln.patch
  patch -p1 -i ${srcdir}/pcre-8.33-msys2.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --build=${CHOST} \
      --prefix=/usr \
      --enable-pcre16 \
      --enable-pcre32 \
      --enable-newline-is-anycrlf \
      --enable-unicode-properties \
      --enable-utf8 \
      --enable-pcregrep-libbz2 \
      --enable-pcregrep-libz \
      --enable-pcretest-libreadline \
      --disable-stack-for-recursion

  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make -j1 check
}

package_pcre() {
  depends=('libreadline' 'libbz2' 'zlib' 'libpcre' 'libpcre16' 'libpcre32' 'libpcrecpp' 'libpcreposix')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_libpcre() {
  groups=('libraries')
  depends=('gcc-libs')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*pcre-1.dll ${pkgdir}/usr/bin/
}

package_libpcre16() {
  groups=('libraries')
  depends=('gcc-libs')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*pcre16-0.dll ${pkgdir}/usr/bin/
}

package_libpcre32() {
  groups=('libraries')
  depends=('gcc-libs')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*pcre32-0.dll ${pkgdir}/usr/bin/
}

package_libpcrecpp() {
  groups=('libraries')
  depends=('libpcre' 'gcc-libs')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*pcrecpp-0.dll ${pkgdir}/usr/bin/
}

package_libpcreposix() {
  depends=('libpcre')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*pcreposix-0.dll ${pkgdir}/usr/bin/
}

package_pcre-devel() {
  pkgdesc="PCRE headers and libraries"
  options=('staticlibs')
  groups=('development')
  depends=("libpcre=$pkgver" "libpcre16=$pkgver" "libpcre32=$pkgver" "libpcreposix=$pkgver" "libpcrecpp=$pkgver")

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
