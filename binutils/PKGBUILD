# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=binutils
_basever=2.28
pkgver=${_basever}
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=('msys2-devel')
depends=('libiconv' 'libintl' 'zlib')
checkdepends=('dejagnu' 'bc')
makedepends=('libiconv-devel' 'gettext-devel' 'zlib-devel')
options=('staticlibs' '!distcc' '!ccache')
install=binutils.install
source=(#https://www.mirrorservice.org/sites/sourceware.org/pub/binutils/snapshots/binutils-${_basever}.tar.bz2
        https://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2
        0001-coff-linker-weak-nt-externals.patch
        0002-gas-aux-nt-weak-externals.patch
        0003-objcopy-weak-nt-externals2local.patch
        0050-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
        0100-binutils-2.28-msys2.patch
        )
sha256sums=('6297433ee120b11b4b0a1c8f3512d7d73501753142ab9e2daa13c5a3edd32a72'
            'ef52e2b615068bf8854aba8c8ed6facd98282571c684de304c13483e2e6e3570'
            '6448844e1a72d72b4398657b084a4bfee893f71d376a5ba0990ec1848b5d06fd'
            '14bae940a7c7409c9f4a35f29ae77407c7c36444bb7779a9b25ec61499a4bc71'
            'ed2a98e3ffa32c512436dfb9c3609872c43346a66eee68df2c7382a411cb693d'
            'b072622977e44e57192b3ee6471092d49e407e88a4f56c95833c1f4d0f1dfefb')

prepare() {
  cd "${srcdir}"/binutils-$_basever
  patch -p1 -i "${srcdir}"/0001-coff-linker-weak-nt-externals.patch
  patch -p1 -i "${srcdir}"/0002-gas-aux-nt-weak-externals.patch
  patch -p1 -i "${srcdir}"/0003-objcopy-weak-nt-externals2local.patch
  #patch -p1 -i "${srcdir}"/0004-fix-erroneous-relocations-applied.patch
  patch -p1 -i "${srcdir}"/0050-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
  patch -p1 -i "${srcdir}"/0100-binutils-2.28-msys2.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir "${srcdir}"/binutils-${CARCH}
}

build() {
  cd "${srcdir}"/binutils-${CARCH}

  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
  fi
  "${srcdir}"/binutils-${_basever}/configure \
    --prefix=/usr \
    --{build,host,target}=${CHOST} \
    --disable-werror \
    --disable-sim \
    $_conf \
    --without-libiconv-prefix \
    --without-libintl-prefix

  make
}

check() {
  # cd "${srcdir}"/binutils-${CARCH}

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  # make LDFLAGS="" -k check || true
  return 0
}

package() {
  cd "${srcdir}"/binutils-${CARCH}
  make DESTDIR=${pkgdir} install
  cp libiberty/libiberty.a ${pkgdir}/usr/lib

  # Remove unwanted files
  #rm ${pkgdir}/usr/share/info/{configure,standards}.info

  # Add some useful headers
  #install -m644 "${srcdir}"/binutils-${_basever}/include/libiberty.h ${pkgdir}/usr/include
  #install -m644 "${srcdir}"/binutils-${_basever}/include/demangle.h ${pkgdir}/usr/include
}
