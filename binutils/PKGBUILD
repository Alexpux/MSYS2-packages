# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=binutils
_basever=2.25.1
pkgver=${_basever}
pkgrel=2
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=('msys2-devel')
depends=('libiconv' 'libintl' 'zlib')
checkdepends=('dejagnu' 'bc')
makedepends=('libiconv-devel' 'gettext-devel' 'zlib-devel')
options=('staticlibs' '!distcc' '!ccache')
install=binutils.install
source=(#https://www.mirrorservice.org/sites/sourceware.org/pub/binutils/snapshots/binutils-${_basever}.tar.bz2
        https://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        0001-coff-linker-weak-nt-externals.patch
        0002-gas-aux-nt-weak-externals.patch
        0003-objcopy-weak-nt-externals2local.patch
        0004-fix-erroneous-relocations-applied.patch
        0050-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
        0100-binutils-2.26-msys2.patch
        binutils-2.24.51-msys2.patch
        )
sha256sums=('b5b14added7d78a8d1ca70b5cb75fef57ce2197264f4f5835326b0df22ac9f22'
            'SKIP'
            '2236889a2570b628d9b630aa60bc57c2abc07e072b5a4568bb5af86c9e1cea0b'
            '15717eefc02275611f8a2af112d2bd08e030fabd06c3d3f6070fd15dc56c54e7'
            '1db81bba67d4f254af567b4a23b4645c335b14e985dc59c930e124fe89dc2a25'
            '3ac2dccb420a2f86bb7341de9b2b1a43e2bb6065ce54b600b1bd63c7de3e01b9'
            'bd38317b28b894d5c0e0ba043fc2445af415f6e96bb9ac93c348748d21a43625'
            '0d99572e111e06446c4e4bc834d318a6bfa7946d4169b6169197317b1bcc5f09'
            '4661ea9608e0c00c95dff5a43e4db69917a16b2877f4fae2e4f4330a5ec592be')

prepare() {
  cd "${srcdir}"/binutils-$_basever
  patch -p1 -i "${srcdir}"/0001-coff-linker-weak-nt-externals.patch
  patch -p1 -i "${srcdir}"/0002-gas-aux-nt-weak-externals.patch
  patch -p1 -i "${srcdir}"/0003-objcopy-weak-nt-externals2local.patch
  #patch -p1 -i "${srcdir}"/0004-fix-erroneous-relocations-applied.patch
  #patch -p1 -i "${srcdir}"/0050-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
  #patch -p1 -i "${srcdir}"/0100-binutils-2.26-msys2.patch
  patch -p1 -i "${srcdir}"/binutils-2.24.51-msys2.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir "${srcdir}"/binutils-${CARCH}
}

build() {
  cd "${srcdir}"/binutils-${CARCH}

  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
  fi
  "${srcdir}"/binutils-${_basever}/configure \
    --prefix=/usr \
    --{build,host,target}=${CHOST} \
    --disable-werror \
    --disable-sim \
    $_conf \
    --without-libiconv-prefix \
    --without-libintl-prefix

  make
}

check() {
  cd "${srcdir}"/binutils-${CARCH}

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd "${srcdir}"/binutils-${CARCH}
  make DESTDIR=${pkgdir} install
  cp libiberty/libiberty.a ${pkgdir}/usr/lib

  # Remove unwanted files
  #rm ${pkgdir}/usr/share/info/{configure,standards}.info

  # Add some useful headers
  #install -m644 "${srcdir}"/binutils-${_basever}/include/libiberty.h ${pkgdir}/usr/include
  #install -m644 "${srcdir}"/binutils-${_basever}/include/demangle.h ${pkgdir}/usr/include
}
