# Maintainer: Christopher Degawa <ccom@randomderp.com>

pkgname=jo
pkgver=1.4
pkgrel=1
pkgdesc='JSON output from a shell'
arch=(any)
url=https://github.com/jpmens/jo
license=(GPL2)
source=("https://github.com/jpmens/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=(24c64d2eb863900947f58f32b502c95fec8f086105fd31151b91f54b7b5256a2)

build() {
  rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"  || return 1

  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build="${CARCH}" \
    --host="${CHOST}"
  make
}

check() {
  make -C "${srcdir}/build-${CARCH}" check
}

package() {
  DESTDIR="${pkgdir}" make -C "${srcdir}/build-${CARCH}" install

  # License
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
