# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>

pkgname=cmake
pkgver=3.5.2
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('i686' 'x86_64')
url="https://www.cmake.org/"
license=("MIT")
makedepends=("gcc" "pkg-config"
             #jsoncpp
             libcurl-devel
             libexpat-devel
             libarchive-devel
             ncurses-devel
             zlib-devel)
depends=("gcc-libs"
         "libcurl"
         "libexpat"
         "libarchive"
         "ncurses"
         "pkg-config"
         "zlib")
options=('staticlibs' 'strip')
source=("https://www.cmake.org/files/v3.5/${pkgname}-${pkgver}.tar.gz"
        "cmake-3.2.3-msys.patch"
        "disable-curses-for-msys.patch"
        "disable-response-files-for-msys.patch"
        3.1.0-case-sensitivity.patch
        3.1.0-cpuinfo.patch
        3.1.0-cygwin-paths.patch)
sha256sums=('92d8410d3d981bb881dfff2aed466da55a58d34c7390d50449aa59b32bb5e62a'
            'ad6fb1a537643082dd87839f5ce851ba926f940ee5756df516fc6efcc07d50fd'
            '054f2bcbf30a67f09f1fbfeeb90dd214601d51227ef53e8e6be451d79e3f8f07'
            'a3fb57b3eefb28f1c9d594869d2dfa2665d8db1a2b45b74a323263cde5ff7be3'
            '39db583b9f3897eae79ad740104c2bd3cc7b5c5f903c502366ce817203957894'
            'e89b1cd9d430dbbc3507095a7f9addda2ab8376caacc5f2b2066f9e254d0753e'
            '976d05c43d02aeb7c674a010523203ab6dab247e2139f3064f17ca9cbcc09209')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/cmake-3.2.3-msys.patch
  patch -p1 -i ${srcdir}/disable-curses-for-msys.patch
  patch -p1 -i ${srcdir}/disable-response-files-for-msys.patch
  patch -p1 -i ${srcdir}/3.1.0-case-sensitivity.patch
  patch -p1 -i ${srcdir}/3.1.0-cpuinfo.patch
  patch -p1 -i ${srcdir}/3.1.0-cygwin-paths.patch

}

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  MSYSTEM=MSYS "${srcdir}"/${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --system-libs \
    --no-system-expat \
    --no-system-jsoncpp \
    --no-qt-gui \
    --parallel=3 \
    --mandir=share \
    --docdir=share/doc/cmake

  plain "Start building..."
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR=$pkgdir install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/Copyright.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
