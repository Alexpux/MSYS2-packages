# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=make
pkgver=4.1
pkgrel=4
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('msys2-runtime' 'libintl' 'sh')
makedepends=('gettext' 'gettext-devel' 'git' 'wget' 'msys2-runtime-devel')
install=${pkgname}.install
options=() # 'debug' '!strip')
source=("https://ftp.gnu.org/gnu/make/${pkgname}-${pkgver}.tar.bz2"
        'make-autoconf.patch')
sha256sums=('0bc7613389650ee6a24554b52572a272f7356164fd2c4132b0bcf13123e4fca5'
            '6d3c7505698fe372ede6f27491925ad76d3580ef042a7c427f2b9d839926021d')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/make-autoconf.patch
  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --without-libintl-prefix \
    --without-libiconv-prefix \
    --without-guile \
    ac_cv_dos_paths=yes

  make -j1 all
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
