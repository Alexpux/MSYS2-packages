_realname=imagesize
pkgbase=python-imagesize
pkgname=("python3-${_realname}" "python2-${_realname}")
pkgver=0.7.1
pkgrel=1
pkgdesc='Getting image size from png/jpeg/jpeg2000/gif file'
url='https://github.com/shibukawa/imagesize_py'
arch=('any')
license=('MIT')
makedepends=('python3-setuptools' 'python2-setuptools')
_dlseq="6c6f1e787d9cab2cc733cf042f125abec07209a58308831c9f292504e826"
source=("https://pypi.python.org/packages/53/72/${_dlseq}/imagesize-$pkgver.tar.gz")
sha256sums=('0ab2c62b87987e3252f89d30b7cedbec12a01af9274af9ffa48108f2c13c6062')

build() {
  cp -r imagesize-$pkgver imagesize-$pkgver-py2

  cd "$srcdir"/${_realname}-$pkgver
  /usr/bin/python3 setup.py build

  cd "$srcdir"/${_realname}-$pkgver-py2
  /usr/bin/python2 setup.py build
}

check() {

  cd "$srcdir"/${_realname}-$pkgver/test
  /usr/bin/python3 ./test_get.py

  cd "$srcdir"/${_realname}-$pkgver-py2/test
  /usr/bin/python2 ./test_get.py
}

package_python3-imagesize() {
  depends=('python3')

  cd ${_realname}-$pkgver
  /usr/bin/python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-imagesize() {
  depends=('python2')

  cd ${_realname}-$pkgver-py2
  /usr/bin/python2 setup.py install --root="$pkgdir" --optimize=1
}
