
pkgname=pupnp
pkgver=1.14.6
pkgrel=1
pkgdesc="Portable SDK for UPnP* Devices"
arch=('i686' 'x86_64')
url="https://github.com/pupnp/pupnp"
license=("BSD")
makedepends=(gcc pkg-config)
source=(libupnp-${pkgver}.tar.bz2::https://github.com/pupnp/pupnp/releases/download/release-${pkgver}/libupnp-${pkgver}.tar.bz2)
sha256sums=('3168f676352e2a6e45afd6ea063721ed674c99f555394903fbd23f7f54f0a503')

prepare() {
  cd "${srcdir}/libupnp-${pkgver}"
  autoreconf -fi
}

build() {
  [[ -d ${srcdir}/build ]] && rm -rf ${srcdir}/build
  mkdir -p ${srcdir}/build && cd ${srcdir}/build

  ../libupnp-${pkgver}/configure \
    --prefix=/usr \
    --enable-static \
    --enable-shared \
    --enable-ipv6

  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/libupnp-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
