# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=XML-Simple
pkgname=perl-${_realname}
pkgver=2.24
pkgrel=1
groups=('perl-modules')
pkgdesc="Simple XML parser for perl"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-XML-Parser' 'perl')
options=('!emptydirs')
source=("http://ftp.cpan.org/pub/CPAN/authors/id/G/GR/GRANTM/${_realname}-${pkgver}.tar.gz")
sha256sums=('9a14819fd17c75fbb90adcec0446ceab356cab0ccaff870f2e1659205dc2424f')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
