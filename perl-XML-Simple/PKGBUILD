# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=XML-Simple
pkgname=perl-${_realname}
pkgver=2.20
pkgrel=2
groups=('perl-modules')
pkgdesc="Simple XML parser for perl"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-XML-Parser' 'perl')
options=('!emptydirs')
source=("http://ftp.cpan.org/pub/CPAN/authors/id/G/GR/GRANTM/${_realname}-${pkgver}.tar.gz")
sha256sums=('5cff13d0802792da1eb45895ce1be461903d98ec97c9c953bc8406af7294434a')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
