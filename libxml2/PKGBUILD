# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libxml2' 'libxml2-devel' 'libxml2-python')
pkgver=2.9.9
pkgrel=5
pkgdesc="XML parsing library, version 2"
arch=(i686 x86_64)
license=('MIT')
makedepends=('python2' 'python3' 'icu-devel>=59.1' 'libreadline-devel' 'ncurses-devel' 'liblzma-devel' 'zlib-devel' 'libcrypt-devel')
url="http://www.xmlsoft.org/"
source=(http://xmlsoft.org/sources/${pkgname}-${pkgver}.tar.gz
        https://www.w3.org/XML/Test/xmlts20130923.tar.gz
        libxml2-2.9.0-do-not-check-crc.patch
        libxml2-2.7.3-doc-install.patch
        libxml2-cygwin-reentrant.patch
        libxml2-2.9.1-msys2.patch
        0001-remove-buggy-linker-flags.patch)
sha256sums=('94fb70890143e3c6549f265cee93ec064c80a84c42ad0f23e85ee1fd6540a871'
            '9b61db9f5dbffa545f4b8d78422167083a8568c59bd1129f94138f936cf6fc1f'
            '5a53d640ebce5a96e35fbdf5f644c37642dfa3345a1dc4fc355728897c19b7e7'
            'bff6790eadec6d7b3affd2d3a7385ce7f80cec2cf4abbd2ed83e9cb297a614a9'
            'fbd5ddb92e3cd0ab0f1f058436ca73b677f12fa00f65d634be7ef791e3706e33'
            '5b87437da7fa6fc83bc96dda29e3566fe56f4402a1f3edbd096cc9dc13e0aca4'
            '01e47a8fcdeed3d8a9b7bf66ac16f9505c320e3e6b25fd246eca35d88e273784')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ] || [ -d $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #


prepare() {
  cd ${srcdir}
  del_file_exists ${pkgname}-${pkgver}/xmlconf
  mv xmlconf -t ${pkgname}-${pkgver}

  cd ${pkgname}-${pkgver}

  apply_patch_with_msg \
    libxml2-2.9.0-do-not-check-crc.patch \
    libxml2-2.7.3-doc-install.patch \
    libxml2-cygwin-reentrant.patch \
    libxml2-2.9.1-msys2.patch \
    0001-remove-buggy-linker-flags.patch

  sed -e 's|/usr/bin/python -u|/usr/bin/python2 -u|g' -e 's|/usr/bin/python$|/usr/bin/python2|g' -i python/tests/*.py *.py

  autoreconf -fi
}

_build() {
  cd ${srcdir}
  cp -r ${pkgname}-${pkgver} build-py$1
  cd build-py$1

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${CHOST} \
    --enable-ipv6 \
    --with-history \
    --with-icu \
    --with-python=/usr/bin/python$1 \
    --with-html-dir=/usr/share/doc \
    --with-html-subdir=libxml2/html

  make
}

build() {
  _build 2
  _build 3
}

check() {
  cd ${srcdir}/build-py2

  make check
}

package_libxml2() {
  depends=('coreutils' 'icu>=59.1' 'liblzma' 'libreadline' 'ncurses' 'zlib')
  groups=('libraries')
  install=libxml2.install

  cd ${srcdir}/build-py2
  make DESTDIR=${pkgdir} install
  cd ${srcdir}/build-py3
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/bin/xml2-config
  rm -rf ${pkgdir}/usr/share/gtk-doc
  rm -rf ${pkgdir}/usr/share/doc
  rm -rf ${pkgdir}/usr/share/aclocal
  rm -rf ${pkgdir}/usr/lib
  rm -rf ${pkgdir}/usr/lib/cmake
  rm -rf ${pkgdir}/usr/lib/pkgconfig
  rm -rf ${pkgdir}/usr/include

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_libxml2-devel() {
  pkgdesc="Libxml2 headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libxml2=${pkgver}" 'icu-devel>=59.1' 'libreadline-devel' 'ncurses-devel' 'liblzma-devel' 'zlib-devel')

  cd ${srcdir}/build-py2
  make DESTDIR=${pkgdir} install
  cd ${srcdir}/build-py3
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/bin/*.dll
  rm -rf ${pkgdir}/usr/bin/*.exe
  rm -rf ${pkgdir}/usr/lib/python*
  rm -rf ${pkgdir}/usr/share/man
  rm -rf ${pkgdir}/usr/share/doc/python-libxml2
}

package_libxml2-python(){
  pkgdesc="Libxml2 python modules"
  depends=('libxml2')
  groups=('python-modules')
  options=('staticlibs')
  install=libxml2-python.install

  cd ${srcdir}/build-py2
  make DESTDIR=${pkgdir} install
  cd ${srcdir}/build-py3
  make DESTDIR=${pkgdir} install

  for site in "${pkgdir}"/usr/lib/python*/site-packages; do
    mv "${site}"/msys-xml2mod.dll "${site}"/libxml2mod.dll
    rm -rf "${site}"/*.a
  done

  rm -rf ${pkgdir}/usr/bin
  rm -rf ${pkgdir}/usr/share/gtk-doc
  rm -rf ${pkgdir}/usr/share/doc/libxml2
  rm -rf ${pkgdir}/usr/share/aclocal
  rm -rf ${pkgdir}/usr/share/man
  rm -rf ${pkgdir}/usr/include
  rm -rf ${pkgdir}/usr/lib/cmake
  rm -rf ${pkgdir}/usr/lib/pkgconfig
  rm -rf ${pkgdir}/usr/lib/*.a
  rm -rf ${pkgdir}/usr/lib/*.sh
}
