# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=2.8.0
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-strace.cc-Don-t-set-MSYS-noglob.patch
        0020-Add-debugging-for-build_argv.patch
        0021-Add-debugging-for-strace-make_command_line.patch
        0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0023-path.cc-Ignore-zero-length-exclusions.patch
        0024-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
        0025-Pass-environment-variables-with-empty-values.patch
        0026-Handle-8-bit-characters-under-LOCALE-C.patch
        0027-Mention-the-extremely-useful-small_printf-function.patch
        0028-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
        0029-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0030-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
        0031-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0032-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0033-Leave-paths-containing-any-special-characters-alone.patch
        0034-Leave-paths-containing-alone.patch
        0035-Skip-posix-to-windows-conversion-when-is-seen.patch
        0036-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0037-Arguments-starting-with-are-no-paths.patch
        0038-Prevent-scp-style-arguments-from-being-mangled.patch
        0039-Fixed-path-converting-with-non-ascii-char.patch
        0040-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0041-Try-to-kill-Win32-processes-gently-upon-Ctrl-C.patch
        0042-kill-kill-Win32-processes-more-gently.patch
        0043-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0044-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0045-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0046-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0047-POSIX-ify-the-SHELL-variable.patch
        0048-Make-paths-WCS-MBS-conversion-explicit.patch
        0049-Handle-ORIGINAL_PATH-just-like-PATH.patch)
sha256sums=('SKIP'
            '933126ab82ff623ece35d4f2a42e9234ed87e26ea10757c936a7a15f2e76dc6f'
            '13466ae57bcfd8405c23b99ec92a4c59abe777f32aa7f0f0c71d6fa59ebd1bfc'
            '769eaa4cd1a36e863c0caeeab1d48de3687fc67f0232d97bf6f789046bef49d7'
            '980ff983f8ea4377699dcd156eee3848b8def2fec4efd22a50cd47570596a9e8'
            '154da799643f4b9c2285f9f7cf94bc0e18dcf5b0ad1b42329b9041702cd34791'
            '533ff40548cd8a316c5261f060b5122b5675919e772e3de7cbe5e5171792918c'
            '3af00bb8f3812afdcda955e9a16143a538eac60858a977d23f99ed70aa5f33e0'
            'fbcff84f469a4929da75e81c897b040c82a71e8f8fda2ef881f9b94115b29d65'
            'ac60b974c72b723039a149f46d978924b853034770008c21cf066b7612205d8d'
            '9803e6e1de1e21a4e4c25d0393557b07460d3ee1b27a8e9bff97d62a0b4eaba6'
            '06ecb33a0219816717b385af0955542ecdfb976fbe0117e9cf5257596734fefb'
            'da276aca69a263b78b91abcc0c19e998c3a07a5f751c2e56c2096f29be390f04'
            '0bcfb51a94d9a9927626435956f031a6f8a0cd9f8ef28f81bd629f8c80dd1127'
            '79ab862d161759b5e3e841768e9703d2b41aaf6ebc92f8d9ea1b90c7b6f6b1c0'
            '857cb963fbb58917e118dc17e007f1e2139609b4fbbf9d6a07959a5588fbc676'
            '1c0ddcfbf8824cf6622d7fe899019b501b47533297ad1f2e94e1b5f422e0c256'
            '90d825c8c683aa41119d09f9f1c39d0e0a2c5ffb78895eac2baccbb63a5c9787'
            '20a192f910616e34af43a4c4cbcdd45107be1bd31b9da0bf70299b1056c76ce4'
            '0fa0d650c02b0b6be409909257230cc4333220dd026c0f2c2c8d847a3ad914d7'
            'b21ffc3c8e6129630131686a4ada6d70a6543a2030b982d750d1d235d97a6ad1'
            '19395ebe2006a514a302158d3a047e16f536ea5964d21eecccd039da490ce98d'
            '2e9e2d6a2f686fe4bf7f6c2e69e078e171ee7fbdf31d4e69df7a06ef63e4cda6'
            'db642ede5157f8f5f53157055e5f19e3974d63cf9034e2105890c6682ae7fcef'
            '6188bb23c85654f9e64ce19b895a951966457640830b988398e1a07acd91a48f'
            '276835b8c1f334792e5462339e0d0562323a8e3df7c3c2298f410b21312e1953'
            '85326d3d4e76b207b60e09134f157daa428933ee245b0c8d540e90bb90fc1ee1'
            '2c2e86218cd4885cf84cf565f03f7938d283623e6ea35536a492e344604fb28d'
            'c538a8c64807aa96ca211a7473d933c0a032c0d5e4a00171c3c8a78b517c7bfe'
            '0dde3901db3689c01acf5ca004166853d34eb76c9050ae0a6f4000db2670b4c6'
            '322e77eeeb677b97af16eac6c0f33c85887f62dc2f55b7ce82b915133f2cefca'
            'b125b84116d93adc31c3c211e743ca454f698191b316920cf51babe5853075f0'
            'cb6237631344660a5330824b964d802cd66478d5a207c866dc15aefd780a5f12'
            'a39304f8565c909303be53cfdfddae92cf80cd24da9121fe7533a7158697c638'
            'af9fa14680766ace48752f693e083921c8e6baa811c0985c52d405261df3e5fa'
            '7f0bd356af56176054368968f9ba87bfa91b6760cf160941a68dfb948ded90ef'
            '4b0290444fb1f8e7721738c51fee70941fc213faf15cd26cacb6e0b8838070c6'
            '885ffd43310742f9d126e686776a8e5019fd1446c214224820b395f7113ff51a'
            '316169be7de1ed3413515a2400bc79f75649bf0dc8ccd375673df70fb9096d67'
            '09853d3610a8c51629823c9f16831d59a55d5fb37eb294ccf5d35de14f86e4e0'
            '6c233fb98ef04213fa8554c7409aecfdea1ba5e006513ddc313f3056131803c5'
            'c8a590230c45d4a31f314c42a54d8050c33fe4b5f3137eb77b4b2f1710601e38'
            '8d1e8723d599c3144ec5503a275ec58717343677a294661faef0f50d9cdae829'
            'cdf9ab3ecfa8898544a904a7f50917bc2e64b888063c73ccb2b288261ed58dfc'
            '4025e40a42d351b4bbbe81a4cab6c3099241210c3d26e92aec1697763c5c3791'
            '4a2716ded82b21c5752b1d36e929855d9755f0dc0a1e3f1abf7bbd6c2952adab'
            'ed817c8aadf2f7923e3910c68a88c4af54529e4b97c18e3016c1ee9259a9cc5b'
            '9c0b4795b7d0f0e468ebf638910f0179c070dba6bd57f74945f1be9e1e55aba2'
            '797582159f929a12aa84762960c2e4a8b6d721963d5ef4c2db8e1ec59990605a'
            '3dbbba7ff662208f5812fa803094bf0a7f17058f51ef031a1677b5ea3470ff2c')
prepare() {
  cd "${srcdir}"/msys2-runtime
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS-triplets.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Rename-DLL-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-convert-be-more-careful-to-stay-within-the-buffer.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-convert-check-safely-whether-we-could-allocate-the-b.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-convert-use-the-elegant-realloc-call.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-convert-plug-potential-memory-leak.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-convert-warn-when-we-cut-off-a-path.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0019-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-path.cc-Ignore-zero-length-exclusions.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
  git am --committer-date-is-author-date "${srcdir}"/0025-Pass-environment-variables-with-empty-values.patch
  git am --committer-date-is-author-date "${srcdir}"/0026-Handle-8-bit-characters-under-LOCALE-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0027-Mention-the-extremely-useful-small_printf-function.patch
  git am --committer-date-is-author-date "${srcdir}"/0028-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
  git am --committer-date-is-author-date "${srcdir}"/0029-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
  git am --committer-date-is-author-date "${srcdir}"/0030-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
  git am --committer-date-is-author-date "${srcdir}"/0031-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0032-Leave-Git-s-name-and-message-arguments-alone-please.patch
  git am --committer-date-is-author-date "${srcdir}"/0033-Leave-paths-containing-any-special-characters-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0034-Leave-paths-containing-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0035-Skip-posix-to-windows-conversion-when-is-seen.patch
  git am --committer-date-is-author-date "${srcdir}"/0036-Also-leave-Git-s-rev-.-name-syntax-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0037-Arguments-starting-with-are-no-paths.patch
  git am --committer-date-is-author-date "${srcdir}"/0038-Prevent-scp-style-arguments-from-being-mangled.patch
  git am --committer-date-is-author-date "${srcdir}"/0039-Fixed-path-converting-with-non-ascii-char.patch
  git am --committer-date-is-author-date "${srcdir}"/0040-path-conversion-Introduce-ability-to-switch-off-conv.patch
  git am --committer-date-is-author-date "${srcdir}"/0041-Try-to-kill-Win32-processes-gently-upon-Ctrl-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0042-kill-kill-Win32-processes-more-gently.patch
  git am --committer-date-is-author-date "${srcdir}"/0043-Allow-overriding-the-home-directory-via-the-HOME-var.patch
  git am --committer-date-is-author-date "${srcdir}"/0044-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
  git am --committer-date-is-author-date "${srcdir}"/0045-Respect-the-db_home-env-setting-under-more-circumsta.patch
  git am --committer-date-is-author-date "${srcdir}"/0046-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
  git am --committer-date-is-author-date "${srcdir}"/0047-POSIX-ify-the-SHELL-variable.patch
  git am --committer-date-is-author-date "${srcdir}"/0048-Make-paths-WCS-MBS-conversion-explicit.patch
  git am --committer-date-is-author-date "${srcdir}"/0049-Handle-ORIGINAL_PATH-just-like-PATH.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=frame-address -fno-delete-null-pointer-checks"
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=frame-address -fno-delete-null-pointer-checks"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('MSYS2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
