# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.0.0
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-strace.cc-Don-t-set-MSYS-noglob.patch
        0020-Add-debugging-for-build_argv.patch
        0021-Add-debugging-for-strace-make_command_line.patch
        0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0023-path.cc-Ignore-zero-length-exclusions.patch
        0024-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
        0025-Pass-environment-variables-with-empty-values.patch
        0026-Handle-8-bit-characters-under-LOCALE-C.patch
        0027-Mention-the-extremely-useful-small_printf-function.patch
        0028-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
        0029-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0030-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
        0031-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0032-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0033-Leave-paths-containing-any-special-characters-alone.patch
        0034-Leave-paths-containing-alone.patch
        0035-Skip-posix-to-windows-conversion-when-is-seen.patch
        0036-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0037-Arguments-starting-with-are-no-paths.patch
        0038-Prevent-scp-style-arguments-from-being-mangled.patch
        0039-Fixed-path-converting-with-non-ascii-char.patch
        0040-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0041-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0042-kill-kill-Win32-processes-more-gently.patch
        0043-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0044-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0045-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0046-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0047-POSIX-ify-the-SHELL-variable.patch
        0048-Make-paths-WCS-MBS-conversion-explicit.patch
        0049-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0050-Fix-incorrect-path-conversion-trailing-slash.patch
        0051-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0052-srandomdev-avoid-warning-about-uninitialized-use.patch)
sha256sums=('SKIP'
            'ee4b5f541c51329429b0385bc2d4fd295f54050022bba52c6b0c02fefcffdd22'
            '02fb13075f976b04a3ddb343dab2f149d2056f458d9048d80d3bc88cbefbf0e9'
            '9d0f2610beaa3b3b205b02aa48b27ac6d89186044ab09b9264ee34b3069631e9'
            '4277098013270e2fe38864df6f82571c4213cfe088bf305033c4721ba43a1474'
            '07a9c0b6d1a319dc07df27207f3100e2efc17e60094c5135b49d9f8600296afe'
            'd46ac5714bf7d725c0796a71eb3bf6b27a894f41766dd8d2005519ba10b3c324'
            '136bc9367e0dc6584530e86c0518d58501ba4c12a153c63f9cde1860cebe0420'
            'a6faa25b29225f440b21f6b79cbf4a8f8fd494f079fcea932ad43b2673cd4e4c'
            'e4dedeb893f918efea102191fed8504ebc5f9103cca8b9e4c20cc5febb36cfd3'
            '6737329f9aad3c5dd3335a615e048a8f07514eeb4d80c42a6ec8879c3bd8132f'
            '67f086af9a1dbba648f94b02a25c2d6e7f1d330606665c68119935a8b8058236'
            '0b915b12b2545013288c489a0d20330a6777aa8e0107753486026e1cdde2acc6'
            '58a94594f4169eafb1b40a80601296bd1259ef4ac4d64fb67c47461d31c1b266'
            'f3ca7266dd25510045cc82901097edefb3a02d3ba03aa5d7aab104e7d173e97c'
            'b7de15fcacf00dedc59344e7350661baf8cb493f155fdd3c82e8a8e96a426a78'
            '8c64166e25cbbb613468ca519a3e8de6b162021237297b2db4d8466ecc37e1f2'
            '4e34636a497e571572c5c41f9a78b079ed8a0e1c2afc206ea88fa66d15a39336'
            '887fd142fe7673eccad4690e3a1e6a42d1b6229e814bd2b9b980579d7e9fef72'
            '63bffd8f19cae086387590de48c75b1141ee8011d0fcb1bc4cd456ee21a5861e'
            '9fd20797a3bb8d287b083abe0f6f2650db907ebb4375f19df9c8d2afc035415b'
            'b94c52d54c2aa9c273f83e5f42aabeb9860c570cb1fa6b43d723763a0a0be3ad'
            '1218c55f496b79c19b10548ab2a59e836b34ee6c60b5027bc1ddbb7143291e12'
            '8269bb5b1a5caaba5bbc38c252d095ead90647dc4d520d478b3c440cf1452477'
            '0704d958f3fac8074d40cede10e5643c76a1b1bcc121971acc91b919b99b5e63'
            'e907ef520dfb7a73a35937a1c5e71cba9e1a6e66debc25a4bf0e2d2479fa634b'
            'aeae326aa7a957ef58c94fc43b227a16c8875890676ff4d5833796298ac56794'
            'd4c881608e9568ce19ab32fc03b4b0cb840d374bf9cecbf77d849a15bef4b6a9'
            'ff6e575d22c48ec11ebf91054535c50bba51691d339412a5e73c865a5ca2dd99'
            '978c9e81f18dbf5220e229f9bf68f992d421157bc37843409206f03a2ff01b6b'
            '932252e39139b7f9468080848a65504628a57f2fed95ba0f51f2972cb25a3e8d'
            '9b707599b898e3f0916681d22f323de9a576d1ef1e85418c75ed1110715f4884'
            'f45ab99a38b6c914748bb020cc2602faac5df10c8c43f7a00af47a8f88bc0f3a'
            'a504249519cc0198958a13f9d650acfa925b978a9d6c2395b032349b1da5da16'
            'eabd4561751261324b41cbd201208188dad9701d7f2655f8607162414c236b7f'
            '8252e01f603502bf5b4ad919d2ffbbdbd4eafb70c7ce2e3d326a732d53f91b5c'
            'cd391ca76391f3aebf0a67c33b8834634ff0cdc3d06c33db7c0acc07954d3996'
            'c2f358715ec0231f5257a139c9be7bbcc3a50427f4ec067cfc1b1d74717b61e1'
            '6c894eeb6d5045dca9ad75ef56fbb3eec8bc35ecaa57724976d7d6031cf765d0'
            '570ff31d492d977eb2dc672c1c5a0bdbdb6dd6b38bba7a8fef9c4a72f390e6cd'
            'd65b5b8b5199189143d2461451db9214863d94deec5e2ffdf9d17f90b27b9145'
            '093098070fb71052f8787d6f7c7e774fc65e16ad6939a7854b10f59bc034e5e0'
            '30099a5defccce7ba9c0e5c441f07f5830281f876bb0f6b0acbedda8a7d10a5d'
            '184cfa772922cc21e8354c2b0f976ead75ec486b6fa28396ef86495bf431d256'
            '19bdcdb0bdb78369c0d777b4e29d497ac418271c5df9d1ad510b7ca48a668416'
            '94a3ac897a493ee1abe14716dd4f4bcb63762ce9ea9d5c3a940b0d75848071d6'
            '0fc8b17cb87516fe74313a502770a4f34c682b9c007d93b41fd2919c97a88a48'
            'd0b419eeff9486bf53e62b8786992561ad5dea1063a00a916b9be00c1b36899a'
            '3b3dfbd0b2371153ea4b23a6e095c1b79a2cfdfdb58a9e525eeea6b301dd2ca1'
            '9898727c3d41dffd25a8daeac71f6a4f3dd913ff9af6649ae72fb9b793ec78c3'
            'b29e25880455f9aca47ec473a462ed8a88fed4fc69f1182df9513e391576b5fb'
            '47874f079b3a10e57a195b3883bda15ea0ce60bbe224174fe05a9c8270d9b961'
            '5de96805e62cb3707be0621ac595c8b567099fb8b75179d2f75a0b7fb9420c87')

# Helper macro to help make tasks easier #
del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}

prepare() {
  cd "${srcdir}"/msys2-runtime
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS-triplets.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Rename-DLL-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-convert-be-more-careful-to-stay-within-the-buffer.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-convert-check-safely-whether-we-could-allocate-the-b.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-convert-use-the-elegant-realloc-call.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-convert-plug-potential-memory-leak.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-convert-warn-when-we-cut-off-a-path.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0019-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-path.cc-Ignore-zero-length-exclusions.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
  git am --committer-date-is-author-date "${srcdir}"/0025-Pass-environment-variables-with-empty-values.patch
  git am --committer-date-is-author-date "${srcdir}"/0026-Handle-8-bit-characters-under-LOCALE-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0027-Mention-the-extremely-useful-small_printf-function.patch
  git am --committer-date-is-author-date "${srcdir}"/0028-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
  git am --committer-date-is-author-date "${srcdir}"/0029-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
  git am --committer-date-is-author-date "${srcdir}"/0030-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
  git am --committer-date-is-author-date "${srcdir}"/0031-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0032-Leave-Git-s-name-and-message-arguments-alone-please.patch
  git am --committer-date-is-author-date "${srcdir}"/0033-Leave-paths-containing-any-special-characters-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0034-Leave-paths-containing-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0035-Skip-posix-to-windows-conversion-when-is-seen.patch
  git am --committer-date-is-author-date "${srcdir}"/0036-Also-leave-Git-s-rev-.-name-syntax-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0037-Arguments-starting-with-are-no-paths.patch
  git am --committer-date-is-author-date "${srcdir}"/0038-Prevent-scp-style-arguments-from-being-mangled.patch
  git am --committer-date-is-author-date "${srcdir}"/0039-Fixed-path-converting-with-non-ascii-char.patch
  git am --committer-date-is-author-date "${srcdir}"/0040-path-conversion-Introduce-ability-to-switch-off-conv.patch
  git am --committer-date-is-author-date "${srcdir}"/0041-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0042-kill-kill-Win32-processes-more-gently.patch
  git am --committer-date-is-author-date "${srcdir}"/0043-Allow-overriding-the-home-directory-via-the-HOME-var.patch
  git am --committer-date-is-author-date "${srcdir}"/0044-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
  git am --committer-date-is-author-date "${srcdir}"/0045-Respect-the-db_home-env-setting-under-more-circumsta.patch
  git am --committer-date-is-author-date "${srcdir}"/0046-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
  git am --committer-date-is-author-date "${srcdir}"/0047-POSIX-ify-the-SHELL-variable.patch
  git am --committer-date-is-author-date "${srcdir}"/0048-Make-paths-WCS-MBS-conversion-explicit.patch
  git am --committer-date-is-author-date "${srcdir}"/0049-Handle-ORIGINAL_PATH-just-like-PATH.patch
  git am --committer-date-is-author-date "${srcdir}"/0050-Fix-incorrect-path-conversion-trailing-slash.patch
  git am --committer-date-is-author-date "${srcdir}"/0051-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
  git am --committer-date-is-author-date "${srcdir}"/0052-srandomdev-avoid-warning-about-uninitialized-use.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
