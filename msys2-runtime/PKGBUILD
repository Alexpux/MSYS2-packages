# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=2.8.0
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-strace.cc-Don-t-set-MSYS-noglob.patch
        0020-Add-debugging-for-build_argv.patch
        0021-Add-debugging-for-strace-make_command_line.patch
        0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0023-path.cc-Ignore-zero-length-exclusions.patch)
sha256sums=('SKIP'
            'b1c5301bbb7a99b69ad6e5cb89f383f59d349ec7c7913060213c78d4550c7bb7'
            'bafc8df65d29c23014776ba19a524e403435bd8099df1c41985bc25bb901cb94'
            '1c7195fc436ed938617fd763b31aacc16ca0ccddc40a2045bf62cfe420a334f2'
            'ab588e075b0c6574d96bf06ab94e49958cb3bb8a417797d3a201e5b3d62d3dbc'
            'dceff6bee69e3294f5b18504ccaab7b0599af5d4423a2244be01ed4b85a1ac2d'
            '6beefca7f2eca9b316f0427e9e6f0de980d71379c52357c98627e1386db73e87'
            '6501c001b55b9ff1c32d7652d84728e4a0915dae0025383130af9b243e52bc38'
            '2d34a94ee07832b3dd0aa9abe1e0a0fe4c09d5a9cb8795565f0166a93eb6be3c'
            'aa55e0577f1550104521a408b81123fcc43e96a507eba565c25ee52b669a0301'
            '1af2236b081015dcb8ec46720295723592d7e4ca7c7a5ae9c093cb213e298cb1'
            'c504e6863e4040ae966e83e0968df7e304e0231139b2ea7e65876d8fce89d4ea'
            '8576817a21aeb395138aa6e9109c5e901d4c2b93fb561a76250cdb87c1f257e1'
            '7bacb6039d217deb997d1bc4f31a5c46e5301ae1396e000ebf38aa5c219d1c2c'
            'f693a9310330532638998d0da1f937880e22c375994860dedd272689d9ade5b4'
            '6099e7e12e1e558374aa26690e069074aa2a6eab5d91cc9b5b8a75313f2ac271'
            '579989cfeb6a73056d6f1e32c916d56d0137ef12e930be3d255da3b619fd3376'
            '30b232b901f1e72588c613f58b9515e7d8e7823ece69a9411bdbb87f01c733fb'
            '1035cb8055557f5bcc679edab2c22980afa31aa99dcf823624bf19a5aa0c6d70'
            '9efcf38400b1d428d7229efc5e0b4f4b8e3529b2e9ebdadb15282259122181ba'
            '9e844186fb1e0cb7612a412968623c8ab2339a6dd768927733a571672a2bfb54'
            'a7866fd6bf627477b52112a24d5504fb75678e334c9cd283f176d5820b1dea55'
            '7afc2bac55c3db92a7f849a497113d82be7966d3e8bb4793e30ed006ef7229e0'
            '98ff1fb66a962ab2365222333acd6b148d44d47fd919971fea81f2ba4482b6f5')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  msg2 "Applying $1"
  patch -Nbp1 -i "${srcdir}/$1"
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #


prepare() {
  cd "${srcdir}"/msys2-runtime
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h 
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0001-Add-MSYS-triplets.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0002-Rename-DLL-from-cygwin-to-msys.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0008-Do-not-convert-environment-for-strace.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0009-convert-be-more-careful-to-stay-within-the-buffer.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0010-convert-check-safely-whether-we-could-allocate-the-b.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0011-convert-use-the-elegant-realloc-call.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0012-convert-plug-potential-memory-leak.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0013-convert-warn-when-we-cut-off-a-path.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0016-Special-case-for-converting-root-directory-to-have-t.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0019-strace.cc-Don-t-set-MSYS-noglob.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0020-Add-debugging-for-build_argv.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0021-Add-debugging-for-strace-make_command_line.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
#  git am --committer-date-is-author-date 
 apply_patch_with_msg 0023-path.cc-Ignore-zero-length-exclusions.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=frame-address -fno-delete-null-pointer-checks"
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=frame-address -fno-delete-null-pointer-checks"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('MSYS2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
