# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=2.11.2
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-strace.cc-Don-t-set-MSYS-noglob.patch
        0020-Add-debugging-for-build_argv.patch
        0021-Add-debugging-for-strace-make_command_line.patch
        0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0023-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
        0024-Pass-environment-variables-with-empty-values.patch
        0025-Handle-8-bit-characters-under-LOCALE-C.patch
        0026-path.cc-Ignore-zero-length-exclusions.patch
        0027-Mention-the-extremely-useful-small_printf-function.patch
        0028-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
        0029-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0030-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
        0031-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0032-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0033-Leave-paths-containing-any-special-characters-alone.patch
        0034-Leave-paths-containing-alone.patch
        0035-Skip-posix-to-windows-conversion-when-is-seen.patch
        0036-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0037-Arguments-starting-with-are-no-paths.patch
        0038-Prevent-scp-style-arguments-from-being-mangled.patch
        0039-Fixed-path-converting-with-non-ascii-char.patch
        0040-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0041-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0042-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0043-kill-kill-Win32-processes-more-gently.patch
        0044-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0045-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0046-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0047-POSIX-ify-the-SHELL-variable.patch
        0048-Make-paths-WCS-MBS-conversion-explicit.patch
        0049-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0050-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0051-Fix-incorrect-path-conversion-trailing-slash.patch
        0052-srandomdev-avoid-warning-about-uninitialized-use.patch)
sha256sums=('SKIP'
            '622a957b1162d948622af28b7019fd5b17a45a4243aad2ee9ca35480f98d14cf'
            'dd682f5a8ededdbe3cb31b25fb68febe0075a3134eb44baa2a85d5a0e65c91b4'
            '8e817136bb93de47190f4beea4599b386397b5b6f32e2e38f75e49083e156d00'
            'c5d5de63030aa94aae2a138d3deea405aa6d75dd02429f5d1fde20e0d5a726a1'
            'd55ac0171b47e6c0922072777433165e29ac4c5cddf759ac6fd87080dfdee815'
            '0f04bdbae9e52b4ef0de4f1a62dca34079abf2d4eb3aa79361eb0ac53b34f162'
            '5f5bd01000a2108892d79c90eb4ad638750a72e66afedc7f21fec40f7854f69d'
            '91cbc878023ddbe060c30e4149dcfb36a9afaf3bba3c2723bbe6492678ed8e85'
            'dc468f16b0fa00b9e077c234736e2949feee67ce3dd1b46871b7c96246b7b3b9'
            '81775fe4af67bd7f0968727c9e7387d5a7109549634c91347fc7e98e7bb13caa'
            'b9f5bb43ff0dd44e76bf7398094ab6dd23c93e8483176fef6fb8428686f0b3e8'
            '361f473526b0b7402fb39134cdf8f916c315b34fa0097bc80c02e92f01b45b90'
            'fa10cbeeb6a9128b6756df4e0d5cca13a6c60c6b4ff210793309d2514894f15e'
            '6b1564971bc1f0e6f29ea198848ca8f3253e6a54e1a30431b6b4083a144dd120'
            '8228861074e2588e30ab568532c1d7d4148ef6b5bcf13b8bd5dce39e4321d11b'
            'dee2f42097ebd1e40c4f9910899800304517dbccc9043155845f15406fbe89f1'
            '56ae14b2c38d2b9d0ac31a403d0fbcfd5b630adaef338f751660c29a9c5f87c4'
            '3809963f27e95c4f98de986d432bb09d749ad2a0f7b1e78c2ebb0a2460c3ba3c'
            '595da547bf7e87dddae3eb134af19b8e73dfdc43375edcf3e67e27192cd80d3f'
            '918ecbf8410d86d24543d12b48e6e16ea61e60271ab57ba052585ebe851173c9'
            'c18a8304cf148f1b7b5728a8891c7fff83650e5f7ff21eb608bea6a323fc4b8d'
            'c44ee54596e6d0343d541c1320ccb6009a814d05e788c687b3c283f4c754976f'
            'cf5d5a58692b4f84c17b40912765569d3e868083d65de9ab08987403f24c925d'
            '9eab9bfff2960344f11e6ccfb5093074f1e7ec054f6956d0fcce2a932b7a34c5'
            '13ad34743441375878a6e45b9ab16457c1639a45df8eb23eece5c81968479aa2'
            'b11c298d102a67b937697a4fbc2a1e2ef0ce5e323867111bf15cc89bd50ca03f'
            'd280b34c7a6f53b4ef2647075fd81bdf0872c0ccad72e2c0349249a7b295ccf0'
            'c83b5326d7d4c2cdcbfe8ce08fc9cb4a931ba240482e92df535a808db71491dd'
            'e65de1e667d4f5f6716715c8905692a596f561520185380d1418766f0f5fcce9'
            '47bb618abe28f57f290d52248e60a3154bce01472b38457687be14d145dcb5ec'
            '0f29ab22f7946ac0e2674cbe37988cb0b793281c18a508e05de34c12c76fbd08'
            'bdbf3dc500bba4a676542fb79ab7376cf97a391c4604ead965c650f896bdc99b'
            '0adf471b5324dc190c154244b10a57e75cd2ee2cb295f4812331158d732e6f43'
            'ced6ff263e90c767ea97f1e184d5da5f4be045a740797238beffc949ddfb6fdd'
            'bc15be43e26fcd6414d2783897eb5b6fd113f9c8d984d3b00329f53ce8e41de4'
            'c0a949ccbe5e6ad64bd30afe357dba3a869798afdc884e7f3c5527a45d61daa6'
            'a1ce4e8c27a90d9e30c0e7564f393a2a969144a82681ff09dc5812f67757c72c'
            'a039dad8a7043680530fef56d53bafba8926fccf10ee3344d2f253477c2c3671'
            '933b1d083c81a8d681ea95a89463e4fdf2b56a4614358cbc8700c6af2bd22f11'
            '67349f9a22da727b91652e78204c4a296554fae020f12087d1b89676220dfd39'
            '896f26f44c051f51037d8a42da05c1799cd4092079b4c6f145e749e09af3ff89'
            '262e05484e14801e6d4e0239cc05d9c68c7e44a771f7e53179e7e516ddc7dd21'
            '0acdb4dd3480bca90b61d25131294df986adae39be48f60ccef849fc1b17f0dc'
            'fb4f1838aaf0be76a9b80e2747df102def392b245607c18890a059030f06fcef'
            'd27acc7675f5e457152fc402d87712662ea69794b269d854c792f8dfbf0b9326'
            '2ebcb76104322e267d89f35b05de04dc55192687df36978490c1d408df602aba'
            '5b7009f2c527440c35326382054c34a98624247b5091e0220cb16c6894df00af'
            '3dfc021a2509f0637bb22ea318d6bc433bcbe37c2e091ef68b55689200891c72'
            '9ad05ecde1f7608ae7447fa4c1fe1c0be8876fe6a00df2d89410aad9f02cf793'
            '7b382aa5884b9d4124e63770ddec32dc677208ea7b08e57dd2c35663c7eb42ee'
            'f0bbef0160778c32017a6b6888617c859ab20ce89084c5dd703b08b06e561331'
            'e03dedee1a9cf39410b9116b7972c8777d2cfba92e851bb7f082387c47f90b56')

# Helper macro to help make tasks easier #
del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}

prepare() {
  cd "${srcdir}"/msys2-runtime
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS-triplets.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Rename-DLL-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-convert-be-more-careful-to-stay-within-the-buffer.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-convert-check-safely-whether-we-could-allocate-the-b.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-convert-use-the-elegant-realloc-call.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-convert-plug-potential-memory-leak.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-convert-warn-when-we-cut-off-a-path.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0019-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-Pass-environment-variables-with-empty-values.patch
  git am --committer-date-is-author-date "${srcdir}"/0025-Handle-8-bit-characters-under-LOCALE-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0026-path.cc-Ignore-zero-length-exclusions.patch
  git am --committer-date-is-author-date "${srcdir}"/0027-Mention-the-extremely-useful-small_printf-function.patch
  git am --committer-date-is-author-date "${srcdir}"/0028-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
  git am --committer-date-is-author-date "${srcdir}"/0029-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
  git am --committer-date-is-author-date "${srcdir}"/0030-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
  git am --committer-date-is-author-date "${srcdir}"/0031-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0032-Leave-Git-s-name-and-message-arguments-alone-please.patch
  git am --committer-date-is-author-date "${srcdir}"/0033-Leave-paths-containing-any-special-characters-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0034-Leave-paths-containing-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0035-Skip-posix-to-windows-conversion-when-is-seen.patch
  git am --committer-date-is-author-date "${srcdir}"/0036-Also-leave-Git-s-rev-.-name-syntax-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0037-Arguments-starting-with-are-no-paths.patch
  git am --committer-date-is-author-date "${srcdir}"/0038-Prevent-scp-style-arguments-from-being-mangled.patch
  git am --committer-date-is-author-date "${srcdir}"/0039-Fixed-path-converting-with-non-ascii-char.patch
  git am --committer-date-is-author-date "${srcdir}"/0040-path-conversion-Introduce-ability-to-switch-off-conv.patch
  git am --committer-date-is-author-date "${srcdir}"/0041-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0042-Allow-overriding-the-home-directory-via-the-HOME-var.patch
  git am --committer-date-is-author-date "${srcdir}"/0043-kill-kill-Win32-processes-more-gently.patch
  git am --committer-date-is-author-date "${srcdir}"/0044-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
  git am --committer-date-is-author-date "${srcdir}"/0045-Respect-the-db_home-env-setting-under-more-circumsta.patch
  git am --committer-date-is-author-date "${srcdir}"/0046-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
  git am --committer-date-is-author-date "${srcdir}"/0047-POSIX-ify-the-SHELL-variable.patch
  git am --committer-date-is-author-date "${srcdir}"/0048-Make-paths-WCS-MBS-conversion-explicit.patch
  git am --committer-date-is-author-date "${srcdir}"/0049-Handle-ORIGINAL_PATH-just-like-PATH.patch
  git am --committer-date-is-author-date "${srcdir}"/0050-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
  git am --committer-date-is-author-date "${srcdir}"/0051-Fix-incorrect-path-conversion-trailing-slash.patch
  git am --committer-date-is-author-date "${srcdir}"/0052-srandomdev-avoid-warning-about-uninitialized-use.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
