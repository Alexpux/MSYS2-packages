# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=2.9.0
pkgrel=2
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-strace.cc-Don-t-set-MSYS-noglob.patch
        0020-Add-debugging-for-build_argv.patch
        0021-Add-debugging-for-strace-make_command_line.patch
        0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0023-path.cc-Ignore-zero-length-exclusions.patch
        0024-fix-strnstr-for-msys2-runtime.patch
        0100-cygwin-regression.patch
        0110-cygwin-workaround-GCC-6-changes.patch
)
sha256sums=('SKIP'
            '795684453684c1f20935e9402f04e3a3ac0fdd9cac29efd573d65c643b31088a'
            'fc673b4e66510bc9105910fddf9e5263f8f60e792a5c4f13021e91bc992ef4d2'
            '7cedf75a3241b6fbdfaf58f4f1f5fd1ec4659d29614135a115eca8c96698983a'
            '02e2787c61d265842e6ffb310d5935b483a732232dccd67c1534c0117317de4a'
            '4d6b821a4e5bbf490be7580fbff635cb9edd3b984a9766d5a64d5e0fb93c8e07'
            '154f4b143edecb35d86c79d67705f98199f49fcc7fa8f2fc81687f40abf229f6'
            '2e1d6dd765561738e93247cf60b050e324cbb934353b427cced17f694c6adad1'
            'bd853f1c5102074bd6f0dab7c5253901fa23aecbf82505c1bec6586839bb6428'
            '1e938abe464fb561c677851ad77b07611ce084508ab5cdec307155890f239ec5'
            '7ed4ad66e1903efd0f73cdeca18eb5c2f7d646a799fa243234283b1c14880cfb'
            '6399587b230f863fb2dbe12c5ec5500f735eb4b8962c02ba7f7099b9f03cb21b'
            '2b9b3c98f431bbe161684b02490ec86e2445aadb3e768b830a5c216e37b01a0c'
            'eae525643fab09c2c1380309b1e19cbbcbe68873027441436ef26a628927bedc'
            '39e075b3769b6827829b4abbf9e81b467601c94cb3228cbcd5eb0f51503212be'
            '1907a42fff993e2b4d3763fc096ec7ca1f40ef5e236ecd9a78c6acb4ebe9c128'
            'bfe16c2eea1d809833f4442eb52d6088c4072c87ca09604f0f57513e8e045e61'
            '3ef241f27a12be4e4f196b9a5f0d71dcb386137e81c573e99d0ae52e83502297'
            '270b4bc6c5f597a3c282f2d2f5fe7a124554c09bfe81750c2bbf30a83d368734'
            '14cbaa441d47244280491bd73c0e7310537ea8bce4182d6e5e926a24a78cdba1'
            '8cf66769444a1ea692478ee65aa531bdab45dd6b60a94008601b6ea46a11023b'
            '8d9ff8588b57aec409fc42fe291a05ba46d3dbe8eb1ef014632a09fe83c2e43e'
            '7dd47fc775edca7fac2b4833e48709a117509bfee3a0cc1d17ab6618c5a425c3'
            '3408e6b55cdcb3cd3375afd9509d4562611c614f4e46f5ceb5ebf1eea443fd13'
            '07a5a41f08af455df5a1799878a6fd5ad97c1da943784dbfd4f2c84868f6503f'
            '7bd1e43841acd5d33fcb3c3704abd047b433ebcac30f3dacd578cb3aa4fa3289'
            '921c62c7573ffa1c464225a1351ff2c6fa003fcf1c752e02a9424f83982aebe0')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git am --committer-date-is-author-date "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h 
 apply_git_am_with_msg 0001-Add-MSYS-triplets.patch \
  0002-Rename-DLL-from-cygwin-to-msys.patch \
  0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch \
  0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch \
  0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0008-Do-not-convert-environment-for-strace.patch \
  0009-convert-be-more-careful-to-stay-within-the-buffer.patch \
  0010-convert-check-safely-whether-we-could-allocate-the-b.patch \
  0011-convert-use-the-elegant-realloc-call.patch \
  0012-convert-plug-potential-memory-leak.patch \
  0013-convert-warn-when-we-cut-off-a-path.patch \
  0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch \
  0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch \
  0016-Special-case-for-converting-root-directory-to-have-t.patch \
  0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0019-strace.cc-Don-t-set-MSYS-noglob.patch \
  0020-Add-debugging-for-build_argv.patch \
  0021-Add-debugging-for-strace-make_command_line.patch \
  0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0023-path.cc-Ignore-zero-length-exclusions.patch \
  0024-fix-strnstr-for-msys2-runtime.patch \
  0100-cygwin-regression.patch \
  0110-cygwin-workaround-GCC-6-changes.patch

}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('MSYS2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
