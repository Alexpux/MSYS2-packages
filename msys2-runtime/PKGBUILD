# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=2.7.0
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-strace.cc-Don-t-set-MSYS-noglob.patch
        0020-Add-debugging-for-build_argv.patch
        0021-Add-debugging-for-strace-make_command_line.patch
        0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0023-path.cc-Ignore-zero-length-exclusions.patch
        0024-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
        0025-Pass-environment-variables-with-empty-values.patch
        0026-Handle-8-bit-characters-under-LOCALE-C.patch
        0027-Mention-the-extremely-useful-small_printf-function.patch
        0028-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
        0029-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0030-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
        0031-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0032-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0033-Leave-paths-containing-any-special-characters-alone.patch
        0034-Leave-paths-containing-alone.patch
        0035-Skip-posix-to-windows-conversion-when-is-seen.patch
        0036-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0037-Arguments-starting-with-are-no-paths.patch
        0038-Prevent-scp-style-arguments-from-being-mangled.patch
        0039-Fixed-path-converting-with-non-ascii-char.patch
        0040-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0041-Kill-also-children-of-the-process-to-be-killed-via-C.patch
        0042-Export-the-kill_process_tree-function.patch
        0043-kill-Handle-Win32-console-processes-children-too.patch
        0044-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0045-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0046-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0047-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0048-POSIX-ify-the-SHELL-variable.patch
        0049-Make-paths-WCS-MBS-conversion-explicit.patch)
sha256sums=('SKIP'
            '748a29bc5436f28cba0d3c3e90f4a1fd87381375348b7aeb86397b5f9decc05f'
            'ffb0fe40836a43899eadc7cdf89e894f6d467af129afe6eb034ea370f357a977'
            '7835b276994aa399da938b83ebcd3b103d8987a0d71b8e2a2aeba94178be7581'
            '8546f55308c8366519f7f743881bf8d294a0c860f579013a61581c9a81ece192'
            'c2791ad426863d5cac6122df4029c773725a80909ce0ed3d1f4bebbb635a08b4'
            '159f29ee19ba98065a1c6a2762c0c7ae94f557d18c5525c57961546297e3fd7b'
            'e66ebed6c5ce1dc0f21bd5ea9aab80613f09caec85bf688e3806589c705db6a9'
            'b3fe7b87ec0110469561c61c70ec7cbd5639470b205b045eb81ffd29044672c6'
            'a22471d7e02d005f52daf336f0e938f82075ca8ecee4137b58e786e5d45aec3c'
            '121c2b0bdfad1c832244c4f6606318238e6de58ebe6bad1e1459b41fa840a247'
            '249e22492886f9a753f11eca9bde00fb5c0914e2e124d687b167b30ec524fc79'
            'e7d63f0bc397f3dd957dfe160940cbb14163f9d526f535ebdab1c40be60a6607'
            '1772f221ce0170d9b9f59363e36420cd356a088289b29c2f40a701594152f0c1'
            '9b70c220fb516c6b9228a53aa68bb3e0c932632719cc959829b6ae0c323f92e1'
            '9a5292f961bc620da3a554dbfb0038fb85ff0e77992d83eff58653c22bfea072'
            '798ba9eb7f664b6b92d18b488b34ae87fa2b13ee91e27c6ae625a0fda53b1c03'
            '59eb531a6c8e7110333a65f3bf66de9cc4b5cabfe0de87ad277564e8723cfc96'
            '6f9f4a07fa11ec79b9cde3ffdded19ed611fe3c754c3fac430f5feb48a2a0a59'
            '6b2d3e66e18f594d44c29a0cd0f17288e32bdc18c14f303fbbe49ee3f9e9ba36'
            'e41f77ea6c0bddd91b228ef5d5e6a255b4ad35210b47560b8185dc05099123cc'
            'cee42eeccc0b56953de033ea95ce6ea28780740a16be4d81635ced36b75f16a8'
            'b1cfc4e79d8d1423e8b7e28dae7a3ed76da5e44888b0d0bd56072502ba61d224'
            '79f587f4aaf8aec424df64226a81d2371cedc8db82ca135d42c571e81688560f'
            'ae88b0f04b35b2eae1f339acd0ac3480cc74d29d791c7eaf838376c494a29d19'
            '2ac3f6dd42eda45b0a26a7022f2bcbe752cd5d1375d563796aaea3ba6b047589'
            '2a92a2a143250b395c62e129d6f5525cdec9de1fb1200034a3618f40d5314832'
            '4ebc928fbb33c914e0fdb14fbe804f5243e0aab7136caf9ec3e4b440e8b9b38a'
            '133f5d9c2b7d1813d33ae676a48d730e27d11f6421c6b4ee07f7c8500355b2f4'
            'c8f8c060ee79536611e8bdade946a60c81e89e46f636ff99e5b98eadc65a5ff5'
            '8ac7158b28454c3f3672211e1c20c6fc20e34660e2da6ed5a31e5635f0da8943'
            '47e7b26da8db5db56e6ffb150bb6b8a7ce275300fe8a0ba86280aa5fc371c2f6'
            'a7dba8d626537304e80caf9ea7dccf081a1de391b6c657f9a624d88d9a0b2938'
            '73bf4f8ca705451d95f18b7ab4c910603f5ccea5dc8608fdc54414474f172941'
            'fe54e5217b7924154dc0069943249dea31768896e064f35dd6fd259e8c6ee076'
            '3501dde9c1898a0bafc83dc2608412d3716bda5a2349e789a6593a38136b3811'
            '9c29cf9151647ff0e62d5ee6ac022ffc33430c191216ecf6826a907b1863a91e'
            'dc469a46f4889f9ee861d6cc4960336651aaed9ccd394f2075e46594555cf4f4'
            '13f3a27cd6bb433eafa993c27a1e404507946478daa1089242d16b6e87b6f666'
            'b564fdaab1044247efbbf1575617bbb3bc5ccbf9d3c9bd33ffcafb11ac33d84f'
            'fcaa451869842a03a68b7060cdccc09492f85245a5c670b03ef011f1359a0aa7'
            '53b60dead936149442aaee2101a93ac532b64970ebb022a4535011751760202c'
            '3d9d2072f3c7dd7f7c9b962671f64025388e03396576cb25cfd86112eb9d5320'
            'd183403de32d4133a2aa30ec34fff578240160ca0d5b6c2b129fca1e77b431c7'
            '5865b11de6ff34f2008930fd149ad0175f6b5c134e0feaf028f67791b27d374c'
            'af878f54ec819d03faa7c6ad74121696eca1718d8318107ad9422ea93e9babbf'
            '717a55421e0a7cdc4b81355258dd4e3a1959c063e055b58c2da8eae70e678e42'
            '4f7fc6c3bee097673b95eb57a2654a53320e8f85f1ae983a06f0fbe861d59cd1'
            '3ec4a6a6d1eb816a8cc361033b9290606e81007ce0a04236dc14dedcbe5031f2'
            '23ab3c8d6e4b128c15f14f7c7a36d12a6603ba06b9c85bf41b86dc993772bae9')
prepare() {
  cd "${srcdir}"/msys2-runtime
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS-triplets.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Rename-DLL-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-convert-be-more-careful-to-stay-within-the-buffer.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-convert-check-safely-whether-we-could-allocate-the-b.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-convert-use-the-elegant-realloc-call.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-convert-plug-potential-memory-leak.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-convert-warn-when-we-cut-off-a-path.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0019-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-path.cc-Ignore-zero-length-exclusions.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
  git am --committer-date-is-author-date "${srcdir}"/0025-Pass-environment-variables-with-empty-values.patch
  git am --committer-date-is-author-date "${srcdir}"/0026-Handle-8-bit-characters-under-LOCALE-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0027-Mention-the-extremely-useful-small_printf-function.patch
  git am --committer-date-is-author-date "${srcdir}"/0028-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
  git am --committer-date-is-author-date "${srcdir}"/0029-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
  git am --committer-date-is-author-date "${srcdir}"/0030-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
  git am --committer-date-is-author-date "${srcdir}"/0031-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0032-Leave-Git-s-name-and-message-arguments-alone-please.patch
  git am --committer-date-is-author-date "${srcdir}"/0033-Leave-paths-containing-any-special-characters-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0034-Leave-paths-containing-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0035-Skip-posix-to-windows-conversion-when-is-seen.patch
  git am --committer-date-is-author-date "${srcdir}"/0036-Also-leave-Git-s-rev-.-name-syntax-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0037-Arguments-starting-with-are-no-paths.patch
  git am --committer-date-is-author-date "${srcdir}"/0038-Prevent-scp-style-arguments-from-being-mangled.patch
  git am --committer-date-is-author-date "${srcdir}"/0039-Fixed-path-converting-with-non-ascii-char.patch
  git am --committer-date-is-author-date "${srcdir}"/0040-path-conversion-Introduce-ability-to-switch-off-conv.patch
  git am --committer-date-is-author-date "${srcdir}"/0041-Kill-also-children-of-the-process-to-be-killed-via-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0042-Export-the-kill_process_tree-function.patch
  git am --committer-date-is-author-date "${srcdir}"/0043-kill-Handle-Win32-console-processes-children-too.patch
  git am --committer-date-is-author-date "${srcdir}"/0044-Allow-overriding-the-home-directory-via-the-HOME-var.patch
  git am --committer-date-is-author-date "${srcdir}"/0045-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
  git am --committer-date-is-author-date "${srcdir}"/0046-Respect-the-db_home-env-setting-under-more-circumsta.patch
  git am --committer-date-is-author-date "${srcdir}"/0047-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
  git am --committer-date-is-author-date "${srcdir}"/0048-POSIX-ify-the-SHELL-variable.patch
  git am --committer-date-is-author-date "${srcdir}"/0049-Make-paths-WCS-MBS-conversion-explicit.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('MSYS2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
