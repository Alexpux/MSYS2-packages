# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=2.6.1
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-strace.cc-Don-t-set-MSYS-noglob.patch
        0020-Add-debugging-for-build_argv.patch
        0021-Add-debugging-for-strace-make_command_line.patch
        0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0023-path.cc-Ignore-zero-length-exclusions.patch
        0024-FAST_CWD-adjust-the-initial-search-scope.patch)
sha256sums=('SKIP'
            '1a09a4f2ce042a4da69780ed713cd575b80b8074d744ccf87f1d2055ef9f50b7'
            'cee6df0633566bd114040b1b21bb38ac06d015f28b0f6929a70738e7032f62c4'
            '92ce0850f013b97a9502d218f0c8ca5b31b8e0be17c6180340d4bdfddfcff96c'
            '1a9bbd21fc6c20f25e9e28f7f94ac88a9752bf951ed11b977b14b3467e38d43a'
            '457afb9a8be605491c4dfab3be82066213eec58529cd712c78a10ec538f4622a'
            '5604b48b21e4adda08b66c3344862769d6e044c0d1a57d72d4b288dfb2886c81'
            '876cb6890941b62fec46b1770b323a87903c79a82a6f9b2c9041ef34cf43f4b6'
            'ff09df643357aa093f4686d182a36acb4cc505e184c92fb45b01316664bcb76c'
            'afdecc214eb6e7a687503910dd4486de4d46e6b4f6fa0cd56b7ec55f5b6a99f4'
            '46328b44b6cf41b9590a32866b3ba8a6823867039c43d09bb5167af75d36f3c0'
            '648accbcb310863108d9b02172e3fd3cc9b6d12e3232220fd1c8e6e76f48eb08'
            'dacfeb02f70c148f8467b1acc2511e348acf58c25e9bd36c7d97611c602d96d5'
            'e253e5354436b29bcdcdcffec74ce6e53e89ae371105c303e422eff19b6b8ec2'
            '4693ba06240e952ccd302b9bd26586ca3d026f40e908d37628c85b9bd8cd0cb8'
            'a18c578beca81f1435af61b09f6657957e977c3333fc3e01edd9805f30bd723a'
            'f3ce43f3ef056dccf85a63c8d46950721eaa138a8cf4d59c12a2bf709259fa05'
            '7c86181300abf4889e2d32278484741ff263b9537fbfdc2662e61caeb24eabc3'
            '4f32922b22e06b8677d4b36541ab136095db4120ac664d5946b03ded37f357e1'
            '4f9714044ee2b68efcedc024e06a609699870d283cfdbdef33fbfc62160f3e52'
            '8333fc1446f062db0eb7167a67a7416ab8d7272f14a030e643a57c69a8e43c19'
            'c1cc89c1769ec713dde0a6481e9feec7e439a7b027c9003a8da59925c76eccc3'
            '45b617c4b34714af88311fe322107e14845f851a28952957d45a34012ffb0689'
            '461b9bbaf90d6c9ce3bd5a8e09895d15f7f8ef4f5bdd4fcbf33a14d27bbd4fa0'
            'f1a0f056fc39d2d8228c17d7061ab1f290b36f7dc583b0461a4ebe3ec7b20993')
prepare() {
  cd "${srcdir}"/msys2-runtime
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS-triplets.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Rename-DLL-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-convert-be-more-careful-to-stay-within-the-buffer.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-convert-check-safely-whether-we-could-allocate-the-b.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-convert-use-the-elegant-realloc-call.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-convert-plug-potential-memory-leak.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-convert-warn-when-we-cut-off-a-path.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0019-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-path.cc-Ignore-zero-length-exclusions.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-FAST_CWD-adjust-the-initial-search-scope.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('MSYS2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
