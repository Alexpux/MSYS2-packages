# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gnupg
conflicts=('gnupg2')
replaces=('gnupg2')
pkgver=1.4.19
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='http://www.gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
optdepends=('curl: gpg2keys_curl')
makedepends=('curl' 'libbz2-devel' 'libcurl-devel' 'libiconv-devel' 'gettext-devel')
depends=('bzip2' 'libcurl' 'libiconv' 'libintl')
install=${pkgname}.install
source=("ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        'gnupg-1.4.15-msys2.patch')
sha1sums=('5503f7faa0a0e84450838706a67621546241ca50'
          'SKIP'
          '2ab42d0e8dd26902cb301746679f3390d9245d76')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/gnupg-1.4.15-msys2.patch

  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --enable-maintainer-mode

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
