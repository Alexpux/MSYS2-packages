# Maintainer: Martell Malone <martell malone at g mail dot com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>
# Contributor: David Macek <david.macek.0@gmail.com>

_realname=winpty
pkgname="${_realname}-git"
epoch=1
pkgver=0.2.2.374.ccbdaf7
pkgrel=1
pkgdesc="A Windows software package providing an interface similar to a Unix pty-master for communicating with Windows console programs"
arch=('i686' 'x86_64')
url="https://github.com/rprichard/winpty"
license=("MIT")
makedepends=("git" "tar" "automake" "xz"
             "mingw-w64-cross-gcc")
provides=("${_realname}")
conflicts=("${_realname}")
#depends=( "ncurses-devel" )
options=('staticlibs' 'strip')
source=("${_realname}"::"git+https://github.com/rprichard/winpty.git"
        0001-Apply-POSIX-to-Win-conversion-on-all-arguments-on-MSYS.patch)
sha256sums=('SKIP'
            'c9705d86feba11f1155e4259d28131afb14db3c30c17efe96bdd655afa72ee04')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "%s.%s.%s" "$(cat VERSION.txt)" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  git am ${srcdir}/0001-Apply-POSIX-to-Win-conversion-on-all-arguments-on-MSYS.patch
}

build() {
  cd "${srcdir}/${_realname}"
  ./configure --build=${CHOST} \
    --prefix=/usr

  make UNIX_ADAPTER_EXE=winpty.exe
}

package() {
  cd "${srcdir}/${_realname}"
  make PREFIX=${pkgdir}/usr UNIX_ADAPTER_EXE=winpty.exe install
}
