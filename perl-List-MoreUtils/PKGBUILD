# Maintainer: Andrea Dalle Vacche

_realname=List-MoreUtils
pkgname=perl-${_realname}
pkgver=0.413
pkgrel=1
pkgdesc="List::MoreUtils provides some trivial but commonly needed functionality on lists which is not going to go into List::Util"
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/~rehsack/List-MoreUtils-0.413/"
depends=('perl' 'perl-Exporter-Tiny')
groups=('perl-modules')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/R/RE/REHSACK/${_realname}-${pkgver}.tar.gz)
sha256sums=('4d6429d5672ce74a59d6490320252cb8b5b8285db8fe9c6551a4162e5375ef37')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
