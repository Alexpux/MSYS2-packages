# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=swig
pkgver=3.0.7
pkgrel=1
pkgdesc="Generate scripting interfaces to C/C++ code"
arch=('i686' 'x86_64')
url="http://www.swig.org/"
license=('custom')
groups=('base-devel')
depends=('zlib' 'libpcre')
makedepends=('zlib-devel' 'pcre-devel')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        swig-3.0.7-msys2.patch)
sha256sums=('06dc8816a225667ce1eee545af3caf87e1bbaa379c32838d4cea53152514348d'
            '799b08eea238f66dcb9034d51ad8c99123c7c6487b1479b51c167664db1dcc50')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/swig-3.0.7-msys2.patch

  ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure -build=${CHOST} --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -D -m644 LICENSE-UNIVERSITIES "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE-UNIVERSITIES
}
