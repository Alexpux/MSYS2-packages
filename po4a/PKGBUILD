# Maintainer: Peter Budai <peterbudai at hotmail dot com>

_dlid=4176
pkgname=po4a
pkgver=0.48
pkgrel=2
pkgdesc="The po4a (PO for anything) project goal is to ease translations (and more interestingly, the maintenance of translations) using gettext tools on areas where they were not expected like documentation"
arch=('any')
url="https://po4a.alioth.debian.org/"
license=('GPL')
depends=("perl" "gettext" 'perl-Text-WrapI18N' 'perl-TermReadKey' 'perl-sgmls' 'perl-Locale-Gettext' 'perl-Unicode-GCString' 'docbook-xsl')
makedepends=('perl-Module-Build')
options=('!emptydirs')
source=(https://alioth.debian.org/frs/download.php/file/${_dlid}/${pkgname}-${pkgver}.tar.gz
        001-fix-man-names.patch)
sha256sums=('a89595ca42e896c97ce4ffc231a075cc88692216b4bc05df40414d7428c4286c'
            'b2dc4f9cd6aa1ee38fa5a32b6f36847c34d876ce8852011e462c2ce7c6149aff')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-fix-man-names.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  perl Build.PL INSTALLDIRS=vendor create_packlist=0
  # The script expects a UTF-8 locale
  LC_ALL=en_US.UTF-8 perl Build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  perl Build destdir="${pkgdir}" install

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
