# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=Module-Build
pkgname=perl-${_realname}
pkgver=0.4224
pkgrel=1
pkgdesc="Build, test, and install Perl modules"
arch=('any')
url="http://search.cpan.org/dist/Module-Build"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.8.0' 'perl-cpan-meta>=2.150010')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/L/LE/LEONT/${_realname}-$pkgver.tar.gz
        module-build-0.4212-msysize.patch)
sha512sums=('c08e84c542a882aa16a682a81cbb7a4860774a3f6a0ec2e720ba9e9bcea76dab331999fee86bf8b92edc08560d7cef4a7c2dfa4e626ab9b22da965e715af531f'
            '9483ac219e01d773db58a80e65111f47872d3009c67801b1b7caada50042b39f942ec2fbe6fa1d1269fc3e8b878ca87e02e9c1fa23746fdc2a48e6bf84dceb25')

prepare() {
  cd ${_realname}-$pkgver
  chmod 644 inc/Perl/OSType.pm
  chmod 644 t/destinations.t
  patch -p1 -i ${srcdir}/module-build-0.4212-msysize.patch
}

build() {
  cd ${_realname}-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd ${_realname}-$pkgver
  perl Build test
}

package() {
  cd ${_realname}-$pkgver
  perl Build install destdir="$pkgdir"
}
