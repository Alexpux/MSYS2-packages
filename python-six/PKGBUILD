# ArchLinux:
# Maintainer: Alexander Duscheleit <jinks@archlinux.us>
# MSYS2:
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_pyname=six
pkgname=(python2-${_pyname} python3-${_pyname})
pkgver=1.10.0
pkgrel=1
pkgdesc="Python 2 and 3 compatibility utilities"
arch=('any')
url="https://pypi.python.org/pypi/six"
license=('MIT')
makedepends=('python2' 'python3')
source=(https://pypi.python.org/packages/source/s/six/six-${pkgver}.tar.gz)
sha256sums=('105f8d68616f8248e24bf0e9372ef04d3cc10104f1980f54d57b2ce73a5ad56a')


prepare() {
  for pver in {3,2}; do
    rm -rf python${pver} || true
    cp -r ${_pyname}-${pkgver} python${pver}
  done
}

build() {
  for pver in {3,2}; do
    cd "$srcdir/python${pver}"
    /usr/bin/python${pver} setup.py build
  done
}

check() {
  for pver in {3,2}; do
    msg "Python ${pver} test"
    cd "${srcdir}/python${pver}"
    /usr/bin/python${pver} setup.py check
  done
}

package_python2-six() {
  depends=('python2')

  cd ${srcdir}/python2
  /usr/bin/python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python3-six() {
  depends=('python3')

  cd ${srcdir}/python3
  /usr/bin/python3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
