# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ExtUtils-Depends
pkgname=perl-${_realname}
pkgver=0.404
pkgrel=1
pkgdesc="The Perl depends module"
arch=('any')
url="http://search.cpan.org/dist/${_realname}/"
groups=('perl-modules')
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/X/XA/XAOC/${_realname}-${pkgver}.tar.gz"
        'ExtUtils-Depends-0.304.patch')
md5sums=('2b66dbb4bcfc2639c6b26309569471ad'
         'a296017a0e7a52301f5dbc9e28d737ce')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/ExtUtils-Depends-0.304.patch
}

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
