# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=jinja
pkgbase=python-${_realname}
pkgname=("python2-${_realname}" "python3-${_realname}")
pkgver=2.8
pkgrel=1
pkgdesc="A simple pythonic template language written in Python"
arch=('any')
license=('BSD')
url="http://jinja.pocoo.org/"
makedepends=("python3-setuptools"
             "python3-markupsafe"
             "python2-setuptools"
             "python2-markupsafe")
source=(https://pypi.python.org/packages/source/J/Jinja2/Jinja2-${pkgver}.tar.gz)
sha256sums=('bc1ff2ff88dbfacefde4ddde471d1417d3b304e8df103a7a9437d47269201bf4')

build() {
  cd "${srcdir}"
  rm -rf python{2,3}-build
  for builddir in python{2,3}-build; do
    cp -r Jinja2-${pkgver} $builddir
    pushd $builddir
    /usr/bin/${builddir%-build} setup.py build
    popd
  done
}

package_python3-jinja() {
  depends=("python3-setuptools" "python3-markupsafe")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    /usr/bin/python3 setup.py install --prefix=/usr --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-jinja() {
  depends=("python2-setuptools" "python2-markupsafe")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    /usr/bin/python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python2-${_realname}/LICENSE"
}

