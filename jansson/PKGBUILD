# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=jansson
pkgname=("${pkgbase}" "${pkgbase}-devel")
pkgver=2.10
pkgrel=1
pkgdesc='C library for encoding, decoding and manipulating JSON data'
arch=('i686' 'x86_64')
url='http://www.digip.org/jansson/'
license=('MIT')
options=('staticlibs')
source=("http://www.digip.org/$pkgname/releases/$pkgname-$pkgver.tar.bz2"
        "jansson-2.10-msys2.patch")
sha256sums=('241125a55f739cd713808c4e0089986b8c3da746da8b384952912ad659fa2f5a'
            '78851fe97aa1b12b32643b1ded1060732c282ab60f43e256129e7f4f47d498d4')


# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd $srcdir/$pkgname-$pkgver
  apply_patch_with_msg jansson-2.10-msys2.patch
  autoreconf -fiv
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-static=yes --enable-shared=yes
  make
  # put this here so we can have split packages with a test.
  make check
  make DESTDIR="${srcdir}/dest" install
}


package_jansson() {
  pkgdesc='C library for encoding, decoding and manipulating JSON data (runtime)'
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_jansson-devel() {
  pkgdesc="jansson headers and libraries"
  depends=("jansson=${pkgver}")
  options=('staticlibs')
  groups=('development')

  mkdir -p ${pkgdir}/usr/lib
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

# vim:set ts=2 sw=2 et:
