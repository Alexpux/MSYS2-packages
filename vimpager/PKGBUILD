# Maintainer: Renato Silva <br.renatosilva@gmail.com>

pkgname='vimpager'
pkgdesc='Use ViM as PAGER'
url='http://www.vim.org/scripts/script.php?script_id=1723'
license=(BSD)
arch=(any)
pkgver=2.02
pkgrel=1
depends=(vim sharutils)
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/rkitover/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('d0e59b33b1c21f996992ddeeaf87a63f6494b60443847acf28e3f3376acd6a05')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make vimpager
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" SYSCONFDIR='/etc' PREFIX='/usr' install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
