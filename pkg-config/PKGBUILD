# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=pkg-config
pkgver=0.28
pkgrel=2
pkgdesc="A system for managing library compile/link flags"
arch=('i686' 'x86_64')
url="https://www.freedesktop.org/wiki/Software/pkg-config/"
license=('GPL')
groups=('base-devel')
depends=('glib2')
provides=("pkgconfig=${pkgver}")
conflicts=('pkgconfig')
replaces=('pkgconfig' 'pkgconf')
source=(https://pkgconfig.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
        glib-cygwin.patch
        pkg-config-0.28-msys2.patch)
sha256sums=('6b6eb31c6ec4421174578652c7e141fdaae2dabad1021f420d8713206ac1f845'
            '622ae8c52e6051209e8703b06bbffa622902b484f796d4de4617c96a221580fd'
            '7d8a3d5544046b38d0eccacb8f8ed1b2d145251a12e317c24d3c5f8d6108b5f4')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p2 -i ${srcdir}/glib-cygwin.patch
  patch -p1 -i ${srcdir}/pkg-config-0.28-msys2.patch

  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure \
    --build=${CHOST} \
    --host=${CHOST} \
    --prefix=/usr \
    --with-internal-glib \
    --enable-indirect-deps
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
