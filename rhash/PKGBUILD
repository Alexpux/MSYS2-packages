# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
pkgbase=rhash
pkgname=("lib${pkgbase}" "lib${pkgbase}-devel" "${pkgbase}")
pkgver=1.3.5
pkgrel=1
pkgdesc='Utility for verifying hash sums of files'
url='http://rhash.anz.ru/?l=en'
license=('custom' 'MIT')
arch=('i686' 'x86_64')
source=("https://downloads.sourceforge.net/rhash/rhash-$pkgver-src.tar.gz"
        001-mingw-cygwin-msys.patch
        002-fix-symlinks.patch)
makedepends=('openssl-devel')
sha256sums=('98e0688acae29e68c298ffbcdbb0f838864105f9b2bd8857980664435b1f1f2e'
            '6d9cffe4dc72ee88df9f4364586c4d7c9f8a295a482abc26dd709bafc5af6d11'
            '8d1c0c742c2beb95dbb465e2936820429739384a63131e82c1d95d528f74e382')

_make_params="CC=/usr/bin/gcc AR=/usr/bin/ar IS_WIN=yes DLLNAME_PREFIX=msys- ADDCFLAGS=-DOPENSSL_RUNTIME"

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -i "${srcdir}"/${_fname}
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ] || [ -d $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #


prepare() {
  cd ${srcdir}/RHash-${pkgver}
  apply_patch_with_msg \
    001-mingw-cygwin-msys.patch \
    002-fix-symlinks.patch
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  cp -r "${srcdir}"/RHash-${pkgver} "${srcdir}"/build-${CARCH}
  cd "${srcdir}"/build-${CARCH}
  export ADDCFLAGS="$CFLAGS"
  export ADDLDFLAGS="$LDFLAGS"
  make ${_make_params} lib-shared lib-static build-shared
  make install ${_make_params} install-conf install-lib-shared install-lib-static DESTDIR="${srcdir}/dest" PREFIX="/usr" 
}

check() {
   echo "Skipping make check"
   cd "${srcdir}"/build-${CARCH}
   make ${_make_params} test-shared-lib test-static-lib test
}

package_librhash() {
  groups=('base')
  pkgdesc="LibRHash shared library"
  depends=('libopenssl')
  cd ${srcdir}/dest
  mkdir -p "${pkgdir}/usr/bin"
  cp -f usr/bin/*.dll "${pkgdir}/usr/bin"
  cd "${srcdir}"/build-${CARCH}
  # license
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/rhash/LICENSE
}

package_librhash-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("librhash=$pkgver")
  pkgdesc="Headers and static library for LibRHash"
  cd ${srcdir}/dest
  mkdir -p $pkgdir/usr
  cp -rf ${srcdir}/dest/usr/include $pkgdir/usr/
  cp -rf ${srcdir}/dest/usr/lib $pkgdir/usr/
}

package_rhash() {
  depends=('libopenssl')
  backup=('etc/rhashrc')
  cd ${srcdir}/dest
  mkdir -p "${pkgdir}/usr/bin"
  cp -rf ${srcdir}/dest/usr/bin/* $pkgdir/usr/bin/
  rm  -f $pkgdir/usr/bin/*.dll
  cp -rf ${srcdir}/dest/usr/share/* $pkgdir/usr
  cp -rf ${srcdir}/dest/etc $pkgdir/
}

