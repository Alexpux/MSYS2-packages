# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=('libiconv' 'libiconv-devel')
pkgver=1.14
pkgrel=2
pkgdesc='Libiconv is a conversion library'
groups=('libraries')
arch=('i686' 'x86_64')
url='https://www.gnu.org/software/libiconv/'
license=('LGPL')
source=("https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        1.14-cross-install.patch
        libiconv-1.14-reloc.patch
        libiconv-1.14-wchar.patch
        msys2-libiconv-1.14.patch)
sha256sums=('72b24ded17d687193c3366d0ebe7cde1e6b18f0df8c55438ac95be39e8a30613'
            '0891f33528131003939c5e7fa610384834d580e80396f411de0b9f26ec1c520f'
            'ed5d4365512781b9b23473cc375a4a2c1491eae31b13b76cda2d55751bff5cdf'
            'd2a57c55683361e49e30ed0de10942e7fe4f4392dc47fe8a24c7b67b21ce9dd7'
            '2ec4b97974e60681d843fbd766e18df6df2a7c90a3ff8ccb709ad65edd649689')
options=(!libtool)

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p2 -i ${srcdir}/1.14-cross-install.patch
  patch -p2 -i ${srcdir}/libiconv-1.14-reloc.patch
  patch -p2 -i ${srcdir}/libiconv-1.14-wchar.patch
  patch -p1 -i ${srcdir}/msys2-libiconv-1.14.patch

  #./autogen.sh --skip-gnulib
}

build() {
  cd $srcdir/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-static --enable-shared
  make
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package_libiconv() {
  depends=('gcc-libs')

  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/bin ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/share ${pkgdir}/usr/
}

package_libiconv-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("libiconv=${pkgver}")
  pkgdesc="libiconv headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/include ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/lib ${pkgdir}/usr/
  rm -f ${pkgdir}/usr/lib/charset.alias
}
