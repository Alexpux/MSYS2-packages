# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libpipeline' 'libpipeline-devel')
pkgver=1.4.1
pkgrel=1
pkgdesc="a C library for manipulating pipelines of subprocesses in a flexible and convenient way"
url="http://libpipeline.nongnu.org/"
groups=('libraries')
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('openssl-devel' 'zlib-devel')
options=('!libtool')
source=(https://download.savannah.gnu.org/releases/libpipeline/libpipeline-$pkgver.tar.gz{,.sig}
		libpipeline-1.4.1-update-gnulib.patch
        libpipeline-1.4.1-msys2.patch)
sha256sums=('da46d7b20163aadb9db2faae483f734e9096a7550c84b94029abeab62dd1b9ee'
            'SKIP'
			'28f1d5b45822f4f7067650243f89fc6e25aa76d16212fa8997b794b91840750e'
            'c3fb29cd682b1d977aa896a9ee13e2bcd263754db7c625bd670dc9305747cec8')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ${srcdir}/libpipeline-1.4.1-update-gnulib.patch
  patch -p1 -i ${srcdir}/libpipeline-1.4.1-msys2.patch

  # cp -rf build-aux/snippet ${srcdir}/
  autoreconf -fi
  # cp -rf ${srcdir}/snippet build-aux/
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --build=${CHOST} --prefix=/usr
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package_libpipeline() {
  depends=('gcc-libs')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libpipeline-devel() {
  pkgdesc="libpipeline headers and libraries"
  options=('staticlibs')
  depends=("libpipeline=${pkgver}")
  groups=('development')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
