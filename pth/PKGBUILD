# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('pth' 'pth-devel')
pkgver=2.0.7
pkgrel=1
pkgdesc="The GNU Portable Threads"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/pth/"
groups=('base' 'base-devel')
license=('LGPL')
depends=('msys2-runtime')
source=(https://ftp.gnu.org/gnu/pth/$pkgname-$pkgver.tar.gz{,.sig}
        pth-2.0.7-2.src.patch
        msysize.patch)
sha256sums=('72353660c5a2caafd601b20e12e75d865fd88f6cf1a088b306a3963f0bc77232'
            'SKIP'
            '95a34fd2051e1d661a0eef6354d6edba650a649000876e4faa7d80d40f036e48'
            '6ae1103f467704acc3c5274cbc4bf3dcf1c215ef6fe4405b5676360f36036e21')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p2 -i ${srcdir}/pth-2.0.7-2.src.patch
  patch -p1 -i ${srcdir}/msysize.patch

  libtoolize -cifv
  autoreconf -fvi
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --enable-shared \
    --disable-static \
    --enable-syscall-soft

  sed -i "s|awk=''|awk=/usr/bin/awk|g" shtool # "Cannot find a reasonable Awk"
  make -j1
  make -j1 DESTDIR="${srcdir}/dest" install
}

package_pth() {
  groups=('libraries')

  mkdir -p $pkgdir/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll $pkgdir/usr/bin/
  cp -rf ${srcdir}/dest/usr/share $pkgdir/usr/
}

package_pth-devel() {
  pkgdesc="Pth headers and libraries"
  groups=('development')
  options=('!staticlibs')
  depends=("pth=${pkgver}")

  mkdir -p $pkgdir/usr/bin
  cp -f ${srcdir}/dest/usr/bin/pth-config $pkgdir/usr/bin/
  cp -rf ${srcdir}/dest/usr/include $pkgdir/usr/
  cp -rf ${srcdir}/dest/usr/lib $pkgdir/usr/
}
