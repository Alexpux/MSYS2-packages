# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=tar
pkgver=1.28
pkgrel=3
pkgdesc="Utility used to store, backup, and transport files"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/tar/tar.html"
license=('GPL3')
groups=('compression')
depends=('msys2-runtime' 'libiconv' 'libintl' 'sh')
makedepends=('libiconv-devel' 'gettext-devel')
options=('!emptydirs')
install=tar.install
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        1.25-mode-binary.patch
        tar-1.27-msys2.patch)
sha256sums=('64ee8d88ec1b47a0961033493f919d27218c41b580138fd6802327462aff22f2'
            'SKIP'
            '3ac7187b8572887db26cbfe016af6fb233955476cfc758a43159112cc3b66a67'
            'fc603608d5a4af49f8dea8392b3b5edc326f9fe47f3ffc40249d7ceb40a2ff6f')

prepare() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -p2 -i ${srcdir}/1.25-mode-binary.patch
  patch -p1 -i ${srcdir}/tar-1.27-msys2.patch

  autoreconf -fi
}

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/tar \
    --enable-backup-scripts
  make
}

check() {
  cd ${srcdir}/$pkgname-$pkgver
  make check
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
}
