# Maintainer: Renato Silva <br.renatosilva@gmail.com>

pkgname='colordiff'
pkgver='1.0.16'
pkgrel=1
pkgdesc='Diff wrapper with pretty syntax highlighting'
url='https://www.colordiff.org/'
license=('GPL2')
arch=(any)

depends=(diffutils perl)
makedepends=(w3m)
backup=(etc/colordiffrc)
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/daveewart/colordiff/archive/v${pkgver}.tar.gz"
        001-colorize-filenames.patch
        002-build-fix.patch)
sha256sums=('ccb81f4f5669bcc6a8643afe8ed2b8edd61e2906f4f7aafce009d61ee7ef63bb'
            'deb380507f19aaa2eb73d7cbff208572f4799b4e94de0cecbba2e3f5f19e2308'
            '24f406f30a5fe3d67b8bbb8ffbb5fd482b44a8774c87197a5181cf16ee1db01b')

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -p1 -i "${srcdir}/001-colorize-filenames.patch"
    patch -p1 -i "${srcdir}/002-build-fix.patch"
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" MAN_DIR='/usr/share/man/man1' INSTALL_DIR='/usr/bin' install doc
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
