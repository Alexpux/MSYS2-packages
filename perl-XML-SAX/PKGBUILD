# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=XML-SAX
pkgname=perl-${_realname}
pkgver=0.99
pkgrel=2
pkgdesc="Simple API for XML"
arch=('any')
url="http://search.cpan.org/dist/XML-SAX"
groups=('perl-modules')
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-XML-SAX-Base' 'perl-XML-NamespaceSupport')
options=('!emptydirs')
install=perl-xml-sax.install
source=("https://www.cpan.org/authors/id/G/GR/GRANTM/${_realname}-${pkgver}.tar.gz"
        'perl-xml-sax.patch')
sha256sums=('32b04b8e36b6cc4cfc486de2d859d87af5386dd930f2383c49347050d6f5ad84'
            'cf5452ccd81b7eb9cd50315a25747099628a6c0442ea3a6c3849edc3e7e4af18')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -Np1 -i "${srcdir}/perl-xml-sax.patch"
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR=$pkgdir

  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
