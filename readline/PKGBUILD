# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgbase=readline
pkgname=('libreadline' 'libreadline-devel')
_basever=6.3
_patchlevel=008 #prepare for some patches
pkgver=${_basever}.${_patchlevel}
pkgrel=7
pkgdesc="GNU readline library"
arch=('i686' 'x86_64')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
makedepends=('ncurses-devel')
#backup=('etc/inputrc')
options=('!emptydirs')
source=(https://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig}
        readline-6.3-cygwin.patch
        readline-6.3-msys2.patch
        readline-6.3-paste-utf8.patch)
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-${_basever}-patches/readline${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi
sha256sums=('56ba6071b9462f980c5a72ab0023893b65ba6debb4eeb475d7a563dc65cafd43'
            'SKIP'
            'cf8523e276c65f59d91e46577c8ca564015c451c907b1e20f96f1c3126cec2e3'
            '4f2f97334d2e7fe362c9ef4e1fd06018d34115a8370c0323d0da67ca2b3d0761'
            '2142f1417536e2670829d3cf6f4f4f2560687a92955ccf11a121bde5dbbccbc9'
            '1a79bbb6eaee750e0d6f7f3d059b30a45fc54e8e388a8e05e9c3ae598590146f'
            'SKIP'
            '39e304c7a526888f9e112e733848215736fb7b9d540729b9e31f3347b7a1e0a5'
            'SKIP'
            'ec41bdd8b00fd884e847708513df41d51b1243cecb680189e31b7173d01ca52f'
            'SKIP'
            '4547b906fb2570866c21887807de5dee19838a60a1afb66385b272155e4355cc'
            'SKIP'
            '877788f9228d1a9907a4bcfe3d6dd0439c08d728949458b41208d9bf9060274b'
            'SKIP'
            '5c237ab3c6c97c23cf52b2a118adc265b7fb411b57c93a5f7c221d50fafbe556'
            'SKIP'
            '4d79b5a2adec3c2e8114cbd3d63c1771f7c6cf64035368624903d257014f5bea'
            'SKIP'
            '3bc093cf526ceac23eb80256b0ec87fa1735540d659742107b6284d635c43787'
            'SKIP')

prepare() {
  cd ${srcdir}/${pkgbase}-${_basever}
  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
    patch -p0 -i $srcdir/readline${_basever//./}-$(printf "%03d" $p)
  done
  patch -p1 -i $srcdir/readline-6.3-cygwin.patch
  patch -p1 -i $srcdir/readline-6.3-msys2.patch
  patch -p1 -i $srcdir/readline-6.3-paste-utf8.patch
}

build() {
  cd ${srcdir}/${pkgbase}-${_basever}

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  ./configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --with-curses \
    bash_cv_termcap_lib=libncurses
  make VERBOSE=1

  make DESTDIR=${srcdir}/dest install
}

package_libreadline() {
  depends=('ncurses')
  groups=('libraries')
  install=readline.install

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libreadline-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("libreadline=${pkgver}" 'ncurses-devel')
  pkgdesc="readline headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
