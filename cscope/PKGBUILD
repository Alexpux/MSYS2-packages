# Maintainer: João Guerra <joca.bt@gmail.com>

pkgname="cscope"
pkgver="15.8a"
pkgrel="1"
pkgdesc="A tool for browsing source code"
arch=("i686" "x86_64")
url="http://cscope.sourceforge.net/"
license="BSD"
makedepends=("ncurses-devel")
source=("https://sourceforge.net/projects/cscope/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('eb736ac40d5abebe8fa46820c7a8eccc8a17966a9a5f70375367b77177874d1e')

prepare () {
  cd "${pkgname}-${pkgver}"
  sed -i 's|/usr/local/lib/cs|/usr/lib/cs|' contrib/ocs
}

build () {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST}
  make
}

package () {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
