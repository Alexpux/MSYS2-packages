# Maintainer: João Guerra <joca.bt@gmail.com>

pkgname="cscope"
pkgver="15.8b"
pkgrel="1"
pkgdesc="A tool for browsing source code"
arch=("i686" "x86_64")
url="https://cscope.sourceforge.io/"
license=('BSD')
makedepends=("ncurses-devel")
source=("https://sourceforge.net/projects/cscope/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('4889d091f05aa0845384b1e4965aa31d2b20911fb2c001b2cdcffbcb7212d3af')

prepare () {
  cd "${pkgname}-${pkgver}"
  sed -i 's|/usr/local/lib/cs|/usr/lib/cs|' contrib/ocs
}

build () {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST}
  make
}

package () {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
