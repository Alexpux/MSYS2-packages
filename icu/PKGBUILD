# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=icu
pkgname=('icu' 'icu-devel')
pkgver=56.1
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=('i686' 'x86_64')
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=('gcc-libs' 'sh')
source=(http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver//./_}-src.tgz
        56.1-cygwin-no-w32api.patch
        56.1-putil.patch
        54.1-implicit_declaration.patch
        icu-56.1-msys2.patch)
sha256sums=('3a64e9105c734dcf631c0b3ed60404531bce6c0f5a64bfe1a6402a4cc2314816'
            '275246b89a908da82a1aa58adcbde3967707a1c714328771d6c707ed67a06e2c'
            'fca7b3cfe2dd890eecc97c8314d6dd967a526f47f8c06b0791976c557412adf0'
            '5b2edd523dffe7e82b7d2f70dec1446297d588d11eb81f76d15e9afd0691221a'
            'bb62a6c01158ee37a1236b02263adf3aa40d114347e038046ad52a6253229614')

prepare() {
  cd ${srcdir}/icu/source
  patch -p2 -i ${srcdir}/54.1-implicit_declaration.patch
  patch -p2 -i ${srcdir}/56.1-putil.patch
  patch -p2 -i ${srcdir}/56.1-cygwin-no-w32api.patch

  patch -p2 -i ${srcdir}/icu-56.1-msys2.patch

  libtoolize --force --copy
  autoreconf -fi
}

build() {
  cd ${srcdir}/icu/source
  
  CC=/usr/bin/gcc \
  CXX=/usr/bin/g++ \
  ./configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin
  make

  make -j1 DESTDIR=${srcdir}/dest install
  pushd ${srcdir}/dest/usr/lib
    # fix installation directory
    mv msys-icu*${pkgver%%.*}.dll ${srcdir}/dest/usr/bin/
    # remove unversioned DLL symlinks
    rm -f *.dll
    # create unversioned implib symlinks
    for l in *.dll.a; do
      #dosym ${l} /usr/lib/${l//${pkgver%%.*}}
      cp ${l} ${l//${pkgver%%.*}}
    done
  popd

  sed -e '/^ICUUC_FILE=/ s/libdir/bindir/' \
    -e 's/l${LIBICU}/l${ICUPREFIX}/g' \
    -e '/l${ICUPREFIX}/ s/${ICULIBSUFFIX_VERSION}//g' \
    -i ${srcdir}/dest/usr/bin/icu-config \
    || error "sed icu-config failed"

  # Install license
  install -Dm644 ${srcdir}/icu/license.html ${srcdir}/dest/usr/share/licenses/icu/license.html
}

check() {
  cd icu/source
  make -k check # passes all
}

package_icu() {
  depends=('gcc-libs')
  groups=('libraries')
  mkdir -p $pkgdir/usr/bin
  cp -f $srcdir/dest/usr/bin/*.dll $pkgdir/usr/bin/
}

package_icu-devel() {
  pkgdesc="ICU headers and libraries"
  groups=('development')
  depends=("icu=${pkgver}")

  cp -rf $srcdir/dest/usr $pkgdir/
  rm -f $pkgdir/usr/bin/*.dll
}
