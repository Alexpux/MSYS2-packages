# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Compress-Bzip2
pkgname=perl-${_realname}
pkgver=2.22
pkgrel=2
pkgdesc="Interface to Bzip2 compression library"
arch=(i686 x86_64)
license=(GPL2)
makedepends=('libbz2-devel')
depends=('perl' 'libbz2')
groups=('perl-modules')
url="http://search.cpan.org/dist/Compress-Bzip2"
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/R/RU/RURBAN/$_realname-$pkgver.tar.gz")
sha256sums=('1603e284e07953415b8eaa132698db8b03f46383f883c0902926f36eecb7e895')

build() {
  cd "$srcdir/$_realname-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver"
  make DESTDIR="$pkgdir" install
}
