# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('catgets' 'libcatgets' 'libcatgets-devel')
pkgver=1.1
pkgrel=2
pkgdesc="catgets message catalog API"
groups=('base')
arch=('i686' 'x86_64')
license=('custom')
url="https://www.gnu.org/software/libc/"
makedepends=('libiconv-devel' 'gettext-devel')
source=(catgets-${pkgver}.tar.bz2
        catgets-1.1-2.src.patch
        catgets-1.1-msys2.patch)
sha256sums=('d957740de9a1582798f4cc227bd983baef2167da6da281a9d678c019156c1158'
            'ffb78a4012cee2aff07e2010a75768c4faf96d7b1023b4c835a2f060b420d1bc'
            'ce7d5551ef03edda4665ea867783f82b39761d6017705db538bc025e35602516')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p2 -i $srcdir/catgets-1.1-2.src.patch
  patch -p1 -i $srcdir/catgets-1.1-msys2.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make CC=gcc CCLD=gcc AR=ar OSTYPE=msys
  make DESTDIR=${srcdir}/dest install
}

package_catgets() {
  depends=('libiconv' 'libintl' 'libcatgets')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/gencat.exe ${pkgdir}/usr/bin/
}

package_libcatgets() {
  pkgesc="message catalog API library"
  groups=('libraries')
  depends=('gcc-libs')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_libcatgets-devel() {
  pkgdesc="Libcatgets headers and libraries"
  groups=('development')
  depends=("libcatgets=${pkgver}")
  options=('staticlibs')

  mkdir -p ${pkgdir}/usr/{include,lib}
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
