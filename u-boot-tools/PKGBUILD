# Maintainer: Bin Meng <bmeng.cn@gmail.com>

pkgname=u-boot-tools
pkgver=2020.04
pkgrel=2
pkgdesc="U-Boot Tools"
arch=('i686' 'x86_64')
url="https://github.com/u-boot/u-boot"
license=('GPL2')
groups=('base')
depends=('dtc' 'openssl')
makedepends=('unzip' 'openssl-devel')
source=("${pkgname}-${pkgver}.zip"::"https://github.com/u-boot/u-boot/archive/v${pkgver}.zip"
        mkimage-fit-Unmmap-the-memory-before-closing-fd-in-fit_import_data.patch)
noextract=("${pkgname}-${pkgver}.zip")
sha256sums=('SKIP'
            '087dc658adf3bb06988aaefe0b2c8f3613a6d99d0e8b48d6b975bdb7c70aa1c9')

prepare() {
  # workaround an issue of bsdtar for extracting symbolic links in the tarball
  unzip ${srcdir}/${pkgname}-${pkgver}.zip
  # apply patches
  cd ${srcdir}/u-boot-${pkgver}
  patch -p1 -i ${srcdir}/mkimage-fit-Unmmap-the-memory-before-closing-fd-in-fit_import_data.patch
}

build() {
  cd ${srcdir}/u-boot-${pkgver}
  make tools-only_defconfig tools-only NO_SDL=1
}

package() {
  mkdir -p ${pkgdir}/usr/bin
  cd ${srcdir}/u-boot-${pkgver}
  cp -f tools/dumpimage.exe ${pkgdir}/usr/bin/
  cp -f tools/fdtgrep.exe ${pkgdir}/usr/bin/
  cp -f tools/fit_info.exe ${pkgdir}/usr/bin/
  cp -f tools/fit_check_sign.exe ${pkgdir}/usr/bin/
  cp -f tools/mkimage.exe ${pkgdir}/usr/bin/
}
