# Maintainer: João Guerra <joca.bt@gmail.com>

pkgname="global"
pkgver="6.3.4"
pkgrel="2"
pkgdesc="A source code tagging system"
arch=("i686" "x86_64")
url="https://www.gnu.org/software/global/"
license="GPL3"
depends=("libltdl")
optdepends=("ctags" "python2-pygments" "python3-pygments")
makedepends=("ncurses-devel")
options=(libtool !emptydirs)
source=("https://ftp.gnu.org/gnu/global/${pkgname}-${pkgver}.tar.gz")
sha256sums=('05b1b8a1f3658dfed8b0e371829727b8952548308afee75113c449ef15a39741')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -fiv
}

build () {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --enable-shared \
              --disable-static \
              --with-exuberant-ctags=ctags
  make
}

package () {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
