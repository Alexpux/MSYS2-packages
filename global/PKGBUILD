# Maintainer: João Guerra <joca.bt@gmail.com>

pkgname="global"
pkgver="6.3.4"
pkgrel="2"
pkgdesc="A source code tagging system"
arch=("i686" "x86_64")
url="https://www.gnu.org/software/global/"
license=("GPL3")
depends=("libltdl")
optdepends=("ctags" "python2-pygments" "python3-pygments")
makedepends=("ncurses-devel")
options=(libtool !emptydirs)
source=("https://ftp.gnu.org/gnu/global/${pkgname}-${pkgver}.tar.gz")
sha1sums=("6b73c0b3c7eea025c8004f8d82d836f2021d0c9e")

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -fiv
}

build () {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --enable-shared \
              --disable-static \
              --with-exuberant-ctags=ctags
  make
}

package () {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
