# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=('mpc' 'mpc-devel')
pkgver=1.0.3
pkgrel=1
pkgdesc='Multiple precision complex arithmetic library'
groups=('libraries')
url='http://www.multiprecision.org'
license=('LGPL')
arch=('i686' 'x86_64')
depends=('gmp>=5.0' 'mpfr')
makedepends=('gmp-devel' 'mpfr-devel')
install=mpc.install
source=("http://www.multiprecision.org/mpc/download/${pkgname}-${pkgver}.tar.gz"
        mpc-1.0.1-msys2.patch)
sha1sums=('b8be66396c726fdc36ebb0f692ed8a8cca3bcc66'
          '0fa851f841aa3d38d9688941ee46317c916da925')

options=('!emptydirs')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/mpc-1.0.1-msys2.patch

  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-shared --disable-static
  make
  make DESTDIR="$srcdir/dest" install
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package_mpc() {
  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/bin ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/share ${pkgdir}/usr/
}

package_mpc-devel() {
  groups=('development')
  options=('staticlibs')
  pkgdesc="MPC headers and libraries"
  depends=("mpc=${pkgver}" 'gmp-devel' 'mpfr-devel')

  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/include ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/lib ${pkgdir}/usr/
}
