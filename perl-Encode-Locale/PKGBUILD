# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Encode-Locale
pkgname=perl-${_realname}
pkgver=1.04
pkgrel=1
pkgdesc="Determine the locale encoding"
arch=('any')
license=(PerlArtistic GPL)
options=('!emptydirs')
depends=('perl>=5.008')
makedepends=()
checkdepends=()
groups=('perl-modules')
conflicts=('perl-libwww<6')
url=http://search.cpan.org/dist/Encode-Locale
source=(https://www.cpan.org/authors/id/G/GA/GAAS/${_realname}-${pkgver}.tar.gz)
sha256sums=('33aca4aa35829bd5c8c5e64ebfde86d0a2e8cd59dac2c4de707ffe9dc10bfbd4')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${_realname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${_realname}-${pkgver}"
    make test
  )
}

package() {
  cd "${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
