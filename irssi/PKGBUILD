# Maintainer: Marty Plummer (The_NetZ) <ntzrmtthihu777 (at) gmail (dot) com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=irssi
pkgver=0.8.17
pkgrel=0
pkgdesc="Modular text mode IRC client with Perl scripting"
arch=('i686' 'x86_64')
url="https://irssi.org/"
license=('GPL')
depends=('glib2-devel' 'openssl' 'libcrypt-devel' 'pcre-devel')
optdepends=('perl-libwww: for the scriptassist script')
backup=('etc/irssi.conf')
source=("https://github.com/irssi-import/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        "0000_msysize.patch")
options=('!strip')
sha256sums=('3c9600cad2edf58f1d012febc1a0ba844274df6e331c01a9e935467705166807'
            '59dd34cbc6e2c0734bb8979910a0de5049d033684efa12a0f703bf278be24176')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/0000_msysize.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --disable-glibtest \
    --enable-ipv6 \
    --sysconfdir=/etc \
    --with-perl-lib=vendor \
    --build=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
