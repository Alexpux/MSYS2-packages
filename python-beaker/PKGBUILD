# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=beaker
pkgname=("python3-${pkgbase}" "python2-${pkgbase}")
pkgver=1.6.5
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('i686' 'x86_64')
license=('PSF')
url="http://pypi.python.org/pypi/setuptools"
depends=("python2-setuptools" "python3-setuptools")
source=(https://github.com/bbangert/beaker/archive/v${pkgver}.tar.gz)
md5sums=('7776b5722b4669bfa0946731d3bd11d8')

prepare() {
  cd "${srcdir}"

  cp -a ${pkgbase}-${pkgver}{,-python2}
}

build() {
  # Build python 3 module
  cd "${srcdir}"/${pkgbase}-${pkgver}
  sed -i "s#/usr/bin/python#/usr/bin/python3#" beaker/crypto/pbkdf2.py
  python3 setup.py build

  # Build python 2 module
  cd ../${pkgbase}-${pkgver}-python2
  sed -i "s#/usr/bin/python#/usr/bin/python2#" beaker/crypto/pbkdf2.py
  python2 setup.py build
}

package_python3-beaker() {
  depends=('python>=3.3')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python3-beaker/LICENSE"
}

package_python2-beaker() {
  depends=('python2>=2.7')

  cd "${srcdir}/${pkgbase}-${pkgver}-python2"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/python2-beaker/LICENSE"
}
