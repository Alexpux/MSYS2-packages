# Maintainer: yumetodo <yume-wikijp@live.jp>

_realname=sl
pkgname=${_realname}-git
pkgver=5.02.r32.923e7d7
pkgrel=1
pkgdesc="Cure your bad habit of mistyping"
arch=('i686' 'x86_64')
url="https://github.com/mtoyoda/sl"
license=('custom')
depends=('ncurses')
makedepends=('ncurses-devel' 'git')
source=('git+https://github.com/mtoyoda/sl')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${_realname}"
  printf "%s.r%s.%s" "$(git describe --long --tags | sed -r 's/([[:digit:]]+)\.([[:digit:]]+).+/\1.\2/')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/${_realname}"
  make
}

package() {
  cd "${srcdir}/${_realname}"
  mkdir -p ${pkgdir}/usr/{bin,share}
  mkdir -p ${pkgdir}/usr/share/man/man1
  mkdir -p ${pkgdir}/usr/share/licenses/${_realname}

  install -m755 sl.exe ${pkgdir}/usr/bin/sl.exe
  install -m644 sl.1 ${pkgdir}/usr/share/man/man1/sl.1
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${_realname}/
}
