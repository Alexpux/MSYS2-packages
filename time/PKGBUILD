# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=time
pkgver=1.7
pkgrel=1
pkgdesc="Utility for monitoring a program's use of system resources"
depends=('msys2-runtime')
makedepends=('gcc')
groups=('base')
url='https://www.gnu.org/directory/time.html'
license=('GPL')
arch=('i686' 'x86_64')
intall=time.install
source=(https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        time-1.7-Recompute-CPU-usage-at-microsecond-level.patch
        time-1.7-ru_maxrss-is-in-kilobytes-on-Linux.patch
        time-1.7-cygwin.patch)
sha256sums=('e37ea79a253bf85a85ada2f7c632c14e481a5fd262a362f6f4fd58e68601496d'
            '360b5ff9c413cdc14a35d15934eaf07b67d181becdfe3717afe528386c75c7b6'
            '5a4efc406962df9790ff7a2f88b03a846b25e17d3c95bd3c5ef534c4bb70cfa2'
            '799600e27fb0b9c029d88d3c416602146b74bd784f0379c12373b2a6f8c06393')

prepare() {
  cd "$pkgname-$pkgver"

  # cf https://bugzilla.redhat.com/show_bug.cgi?id=527276
  patch -Np1 < "$srcdir"/time-1.7-Recompute-CPU-usage-at-microsecond-level.patch

  # cf https://bugzilla.redhat.com/show_bug.cgi?id=702826
  patch -Np1 < "$srcdir"/time-1.7-ru_maxrss-is-in-kilobytes-on-Linux.patch

  patch -Np1 < "$srcdir"/time-1.7-cygwin.patch

  # rebuild the ancient autoconf setup to fix a variety of build issues
  # surrounding missing/incorrect includes.
  autoreconf -fisv
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --infodir=/usr/share/info \
    --build=${CHOST}
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make exec_prefix="$pkgdir/usr" infodir="$pkgdir/usr/share/info" install
}
