# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=gzip
pkgver=1.6
pkgrel=1
pkgdesc="GNU compression utility"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/gzip/"
license=('GPL3')
groups=('base' 'compression')
depends=('msys2-runtime' 'bash' 'less')
install=gzip.install
source=(https://ftp.gnu.org/pub/gnu/gzip/gzip-$pkgver.tar.xz{,.sig}
        'gzip-1.6-fpending.patch'
        'gzip-1.6-msys2.patch')
sha256sums=('37dfed1a485d53212c43b3fa2a7c7952f09bf5cd86e37121c222341ee1b27847'
            'SKIP'
            '86a33a7413ecf49ef12574d19b0feac0e9f52bd15575739fe0cbac9d2804ee43'
            '63e319421223d4968aa71620350afa892fce9c6d8f84cf807c164367fbbf8964')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/gzip-1.6-fpending.patch
  patch -p1 -i ${srcdir}/gzip-1.6-msys2.patch
  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
