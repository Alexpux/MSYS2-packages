# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=guile
pkgname=("$pkgbase" "lib${pkgbase}" "lib${pkgbase}-devel")
pkgver=2.0.14
pkgrel=1
pkgdesc="a portable, embeddable Scheme implementation written in C"
url="https://www.gnu.org/software/guile/"
arch=(i686 x86_64)
license=('GPL')
makedepends=('libltdl' 'gmp-devel' 'ncurses-devel' 'libunistring-devel' 'libgc-devel' 'libffi-devel' 'texinfo')
source=("https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        guile-2.2.2-msys2.patch)
options=('!libtool')
sha256sums=('8aeb2f353881282fe01694cce76bb72f7ffdd296a12c7a1a39255c27b0dfe5f1'
            '59f1c5dac06b542e48159e563ff2704b60343346d8f9fcee60538e9cd20ac1e5')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  patch -p1 -i ${srcdir}/guile-2.2.2-msys2.patch

  cp -rf build-aux/snippet ${srcdir}/snippet
  autoreconf -fi
  cp -f ${srcdir}/snippet/* build-aux/snippet/
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --disable-debug-malloc \
    --disable-guile-debug \
    --disable-error-on-warning \
    --disable-rpath \
    --enable-deprecated \
    --enable-networking \
    --enable-nls \
    --enable-posix \
    --enable-regex \
    --without-threads \
    --with-modules \
    --disable-static
  make
  make DESTDIR="${srcdir}/dest" install
}

package_guile() {
  depends=('libguile' 'info')
  install=${pkgbase}.install

  mkdir -p ${pkgdir}/usr/bin
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  rm -f ${pkgdir}/usr/bin/*.dll
  rm -f ${pkgdir}/usr/bin/*-config

  mkdir -p ${pkgdir}/usr/share
  #cp -rf ${srcdir}/dest/usr/share/doc $pkgdir/usr/share/
  cp -rf ${srcdir}/dest/usr/share/info ${pkgdir}/usr/share/
  cp -rf ${srcdir}/dest/usr/share/man ${pkgdir}/usr/share/
}

package_libguile() {
  depends=('gmp' 'libltdl' 'ncurses' 'libunistring' 'libgc' 'libffi')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  
  (ls ${srcdir}/dest/usr/bin/*.dll &>/dev/null) && \
  install -Dm755 ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  
  (ls ${srcdir}/dest/usr/lib/*.dll &>/dev/null) && \
  install -Dm755 ${srcdir}/dest/usr/lib/*.dll ${pkgdir}/usr/bin/

  #cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  mkdir -p ${pkgdir}/usr/share
  cp -rf ${srcdir}/dest/usr/share/guile ${pkgdir}/usr/share/  
  mkdir -p ${pkgdir}/usr/lib
  cp -rf ${srcdir}/dest/usr/lib/guile ${pkgdir}/usr/lib/
}

package_libguile-devel() {
  depends=("libguile=${pkgver}")
  groups=('development')
  options=('staticlibs')

  mkdir -p ${pkgdir}/usr/bin
  install -Dm755 ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  mkdir -p ${pkgdir}/usr/lib
  cp -rf ${srcdir}/dest/usr/lib/*.a ${pkgdir}/usr/lib/
  cp -rf ${srcdir}/dest/usr/lib/pkgconfig ${pkgdir}/usr/lib/
  mkdir -p ${pkgdir}/usr/share
  cp -rf ${srcdir}/dest/usr/share/aclocal ${pkgdir}/usr/share/
}
