# Maintainer: Alexandre Ferreira < alex.jorge.m [at] gmail.com >

_realname=cython
pkgbase=${_realname}
pkgname=('cython')
pkgver=0.29.17
pkgrel=1
pkgdesc='C-Extensions for Python'
arch=('i686' 'x86_64')
url='https://cython.org/'
license=('APACHE')
depends=('python-setuptools')
makedepends=('gcc' 'libcrypt-devel')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/cython/cython/archive/${pkgver}.tar.gz")
sha256sums=('5ab5c1e92fe334bcd3cde3b0c34dd96d1b007d64e7fa9fdbf53bb368c5d27e22')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${CARCH}| true
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
}

build() {
  cd "${srcdir}/python-build-${CARCH}"
  DISTUTILS_DEBUG=1 /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  DISTUTILS_DEBUG=1 /usr/bin/python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}