# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('ruby' 'ruby-docs')
pkgver=2.6.3
pkgrel=3
arch=('i686' 'x86_64')
url='https://www.ruby-lang.org/en/'
license=('BSD' 'custom')
makedepends=('openssl-devel' 'libffi-devel' 'doxygen' 'libcrypt-devel'  'gmp-devel' 'libyaml-devel' 'libgdbm-devel' 'libiconv-devel' 'libreadline-devel' 'zlib-devel')
options=('!emptydirs' '!makeflags' 'staticlibs')
#libffi patch should be included because fiddle might contain the bundled version.
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver%.*}/ruby-${pkgver//_/-}.tar.bz2"
        2.5.0-cygwin-configure.patch
        2.4.0-cygwin-rubygems.patch
        2.4.0-cygwin-compile.patch
        2.5.0-pkgconfig-version.patch
        ruby-1.9.3-mkmf-verbose.patch
        ruby-2.1.0-always-use-i386.patch
        ruby-2.1.0-custom-rubygems-location.patch
        ruby-2.1.0-Enable-configuration-of-archlibdir.patch
        ruby-2.1.0-Prevent-duplicated-paths-when-empty-version-string-i.patch
        ruby-2.2.3-Generate-preludes-using-miniruby.patch
        ruby-2.3.0-ruby_version.patch
        ruby-2.6.0-config-support-include-directive.patch
        'gemrc'
        'ruby-2.6.0-msys2.patch'
        'libffi-3.2.1-msysize.patch')
## Populated by the updpkgprovs script
provides=(
    "ruby-did_you_mean=1.3.0"
    "ruby-minitest=5.11.3"
    "ruby-net-telnet=0.2.0"
    "ruby-power_assert=1.1.3"
    "ruby-rake=12.3.2"
    "ruby-test-unit=3.2.9"
    "ruby-xmlrpc=0.3.0"
    "ruby-bigdecimal=1.4.1"
    "ruby-bundler=1.17.2"
    "ruby-cmath=1.0.0"
    "ruby-csv=3.0.9"
    "ruby-date=2.0.0"
    "ruby-dbm=1.0.0"
    "ruby-e2mmap=0.1.0"
    "ruby-etc=1.0.1"
    "ruby-fcntl=1.0.0"
    "ruby-fiddle=1.0.0"
    "ruby-fileutils=1.1.0"
    "ruby-forwardable=1.2.0"
    "ruby-gdbm=2.0.0"
    "ruby-io-console=0.4.7"
    "ruby-ipaddr=1.2.2"
    "ruby-irb=1.0.0"
    "ruby-json=2.1.0"
    "ruby-logger=1.3.0"
    "ruby-matrix=0.1.0"
    "ruby-mutex_m=0.1.0"
    "ruby-openssl=2.1.2"
    "ruby-ostruct=0.1.0"
    "ruby-prime=0.1.0"
    "ruby-psych=3.1.0"
    "ruby-rdoc=6.1.0"
    "ruby-rexml=3.1.9"
    "ruby-rss=0.2.7"
    "ruby-scanf=1.0.0"
    "ruby-sdbm=1.0.0"
    "ruby-shell=0.7"
    "ruby-stringio=0.0.2"
    "ruby-strscan=1.0.0"
    "ruby-sync=0.5.0"
    "ruby-thwait=0.1.0"
    "ruby-tracer=0.1.0"
    "ruby-webrick=1.4.2"
    "ruby-zlib=1.0.0"
)
## End of pkgupdprovs modifications
sha256sums=('dd638bf42059182c1d04af0d5577131d4ce70b79105231c4cc0a60de77b14f2e'
            '2f5eec5a7835b63fd1f0fb2af6a6a42e9bba1ee06fee35e4eafb0c5ad5fbfd03'
            '01a59cbfffa98eeca6b66f2f179474793dbb6ec32063d717a24e5ca03f28d767'
            '5fffe4fa469721bfe271650dae142c5a0274c22705e4be01541371d74a5de23d'
            'ee88cfec3610b21908a8c0725b43b4d1157ec394179489dd9c86309b1f663e4b'
            '7c99b662ca67c7c3611fbd350c5429262edf7e23af4b4bc7196344ffa6eae54d'
            'a86427213d94a87b8296609ba502cf4dc0b72f7d89a6f5d892671592f4bf3461'
            'b16ac2d41cbe3e4e961292eace726048ecca8bbbe834acd1338ef33e3d8c5ed9'
            'abb6f18a8436930c6b1f506bdcb8ad52ea8b159fcbb225041eeef88ed38cce61'
            '3485d276e4bdddbea9774a5e8e97cc9f10fb713debcf0db68a60691e6ccc5c52'
            '021c7b357bd066df9d8dde40e7275e3638bf503858e9776a82ca00a7ff581055'
            '1259bc06a8ac34da4e4fcdd94cdba75359aa5ab897b203ca4e1e91ac6dd99337'
            'e4af2795365fb2deb1be0c973865e75b52e7c303d443faf58f5fce1ebd6b8037'
            '4bb7eb2fe66e396ed16b589cdb656831407b39ad4e138d88536754c0448ac614'
            '2385dc4f84e8b207320cb4381d167eb800f712596a0f9d0f8c7cdce3bbc1fd0f'
            'a700b3f7b77c39cb42a76581cdcb0d34b71daf20f9a222502b53f437c6f64800')
noextract=(ruby-${pkgver}.tar.bz2)

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  msg2 "Applying $1"
  patch -Np1 -i "${srcdir}"/$1
}

apply_patch_with_msg_p2() {
  msg2 "Applying $1"
  patch -Np2 -i "${srcdir}"/$1
}


del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #


prepare() {
  [[ -d ${srcdir}/ruby-${pkgver} ]] && rm -rf ${srcdir}/ruby-${pkgver}
  tar -xjf ${srcdir}/ruby-${pkgver}.tar.bz2 -C ${srcdir} || true

  cd ${srcdir}/ruby-${pkgver//_/-}
  apply_patch_with_msg ruby-1.9.3-mkmf-verbose.patch \
    ruby-2.1.0-always-use-i386.patch \
    ruby-2.1.0-custom-rubygems-location.patch \
    ruby-2.1.0-Enable-configuration-of-archlibdir.patch \
    ruby-2.1.0-Prevent-duplicated-paths-when-empty-version-string-i.patch \
    ruby-2.2.3-Generate-preludes-using-miniruby.patch \
    ruby-2.3.0-ruby_version.patch \
    ruby-2.6.0-config-support-include-directive.patch
  apply_patch_with_msg_p2 2.5.0-cygwin-configure.patch
  apply_patch_with_msg_p2 2.4.0-cygwin-rubygems.patch
  apply_patch_with_msg_p2 2.4.0-cygwin-compile.patch
  apply_patch_with_msg 2.5.0-pkgconfig-version.patch
  apply_patch_with_msg ruby-2.6.0-msys2.patch
  # This is just in case the build process the included libffi
  # It might not be ncessary but just in case.
  pushd ext/fiddle/libffi-3.2.1
    apply_patch_with_msg libffi-3.2.1-msysize.patch
    autoreconf -fiv
  popd
  
  #cp -f config.{sub,guess} tool/
  autoreconf -fi
}

build() {
  mkdir -p ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}

  CFLAGS+=" -fno-strict-aliasing "
  CXXFLAGS+=" -fno-strict-aliasing "
  ../ruby-${pkgver//_/-}/configure \
    --build=${CHOST} \
    --prefix=/usr \
    --exec_prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libexecdir=/usr/lib/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat \
    LDSHARED="gcc -shared" \
    DLDFLAGS="-Wl,--export-all-symbols"
  make
}

check() {
  cd ruby-${pkgver//_/-}

  make test
}

package_ruby() {
  pkgdesc='An object-oriented language for quick and easy programming'
  depends=('gcc-libs' 'libopenssl' 'libffi' 'libcrypt'  'gmp' 'libyaml' 'libgdbm' 'libiconv' 'libreadline' 'zlib')
  optdepends=(#'tk: for Ruby/TK'
              'ruby-docs: Ruby documentation')
  provides=('rubygems' 'rake')
  conflicts=('rake')
  backup=('etc/gemrc')
  install='ruby.install'

  cd ${srcdir}/build-${CHOST}

  make DESTDIR="${pkgdir}" install-nodoc

  install -D -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"

  install -D -m644 ${srcdir}/ruby-${pkgver//_/-}/COPYING "${pkgdir}/usr/share/licenses/ruby/LICENSE"
  install -D -m644 ${srcdir}/ruby-${pkgver//_/-}/BSDL "${pkgdir}/usr/share/licenses/ruby/BSDL"
}

package_ruby-docs() {
  pkgdesc='Documentation files for ruby'

  cd ${srcdir}/build-${CHOST}

  make DESTDIR="${pkgdir}" install-doc install-capi

  install -D -m644 ${srcdir}/ruby-${pkgver//_/-}/COPYING "${pkgdir}/usr/share/licenses/ruby-docs/LICENSE"
  install -D -m644 ${srcdir}/ruby-${pkgver//_/-}/BSDL "${pkgdir}/usr/share/licenses/ruby-docs/BSDL"
  find "${pkgdir}/usr/share/doc/ruby/capi/html" -name "*.html" -exec \
    sed -e "s|${srcdir}|ruby-${pkgver}|g" -i {} \;
}
