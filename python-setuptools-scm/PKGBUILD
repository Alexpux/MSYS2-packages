# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=setuptools-scm
pkgname=("python3-${pkgbase}" "python2-${pkgbase}")
pkgver=1.15.6
pkgrel=1
pkgdesc="Handles managing your python package versions in scm metadata"
arch=('any')
license=('MIT')
url='https://github.com/pypa/setuptools_scm'
makedepends=('python3-setuptools' 'python2-setuptools')
source=("https://pypi.org/packages/source/${pkgbase::1}/${pkgbase}/${pkgbase/-/_}-${pkgver}.tar.gz")
sha256sums=('49ab4685589986a42da85706b3311a2f74f1af567d39fee6cb1e088d7a75fb5f')

prepare() {
  cd ${srcdir}

  for pver in {2,3}; do 
    rm -rf python${pver}-build-${CARCH} | true
    cp -r "${pkgbase/-/_}-${pkgver}" "python${pver}-build-${CARCH}"
  done
}

package_python3-setuptools-scm() {
  depends=('python3')

  cd "${srcdir}"/python3-build-${CARCH}
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/python3-${pkgbase}/LICENSE"
}

package_python2-setuptools-scm() {
  depends=('python2')

  cd "${srcdir}"/python2-build-${CARCH}
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/python2-${pkgbase}/LICENSE"
}
