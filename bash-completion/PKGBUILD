# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=bash-completion
pkgver=2.1
pkgrel=6
pkgdesc="Programmable completion for the bash shell"
arch=('any')
url="https://bash-completion.alioth.debian.org/"
license=('GPL')
groups=('base')
depends=('bash')
options=('!emptydirs' '!makeflags')
source=(https://bash-completion.alioth.debian.org/files/${pkgname}-${pkgver}.tar.bz2
        words_bad_array_subscript.patch
        quote_readline_by_ref_fixes.patch)
sha256sums=('2b606804a7d5f823380a882e0f7b6c8a37b0e768e72c3d4107c51fbe8a46ae4f'
            '1c48279c796fb82a4e1ec9d52548418e455e96f079a123118dd3d6dcd9ff2964'
            'f1b95da22e56909681ac05e08bfcc497cecfdad1060902ca415d54d2d0b265a3')
prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/words_bad_array_subscript.patch
  patch -p1 -i ${srcdir}/quote_readline_by_ref_fixes.patch

  autoreconf --install
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  mkdir -p ${pkgdir}/usr/lib
  mv -f ${pkgdir}/usr/share/pkgconfig ${pkgdir}/usr/lib/
  # bash-completion is sourced in /etc/bash.bashrc so that non-bash shell don't source it
  #  rm "${pkgdir}/etc/profile.d/bash_completion.sh"

  rm "${pkgdir}"/usr/share/bash-completion/completions/{cal,makepkg,hexdump,look,renice}
}
