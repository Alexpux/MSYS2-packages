# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libunistring' 'libunistring-devel')
pkgver=0.9.7
pkgrel=2
pkgdesc="Library for manipulating Unicode strings and C strings."
url="https://www.gnu.org/software/libunistring/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('msys2-runtime' 'libiconv')
makedepends=('libiconv-devel')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        libunistring-0.9.3-2.src.patch
        libunistring-0.9.3-msys2.patch)
sha256sums=('9ce081cbee1951b55597b30e7030bda9d7b2f034ef901a135ff3a020be5a41e5'
            '8b00b7960b3e2f2e0db1f65bd2b2fea8eb36de98f48a40c8520d8fb00b57f249'
            '17db9c0a7f6a6d07be65049fed71be338f04fa6c2c036bde82413306ce263231')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p2 -i ${srcdir}/libunistring-0.9.3-2.src.patch
  patch -p1 -i ${srcdir}/libunistring-0.9.3-msys2.patch

  #cp -rf build-aux/snippet ${srcdir}/
  autoreconf -fi
  #mv -f ${srcdir}/snippet/* build-aux/snippet/
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --build=${CHOST} --prefix=/usr
  make
  make DESTDIR="${srcdir}/dest" install
}

package_libunistring() {
  install=${pkgname}.install
  groups=('libraries')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libunistring-devel() {
  pkgdesc="libunistring headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libunistring=${pkgver}" "libiconv-devel")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
