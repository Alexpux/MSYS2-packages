# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('bzip2' 'libbz2' 'libbz2-devel')
pkgver=1.0.6
pkgrel=2
pkgdesc="A high-quality data compression program"
groups=('base' 'compression')
arch=('i686' 'x86_64')
license=('custom')
url="http://www.bzip.org"
makedepends=('gcc' 'make' 'patch')
source=(http://www.bzip.org/$pkgver/bzip2-$pkgver.tar.gz
        bzip2-1.0.6-msys-dll.patch
        bzip2-1.0.6-msys2.patch)
sha256sums=('a2848f34fcd5d6cf47def00461fcb528a0484d8edef8208d6d2e2909dc61d9cd'
            '8af25c0df4a063b19855f7468b1c10b4c58159d317be67fe63013b6bed42fec0'
            'faca1f994390b287b28a804ee4d8912759a67107227ab3bda320c6daf54cb944')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i ${srcdir}/bzip2-1.0.6-msys-dll.patch
  patch -p1 -i ${srcdir}/bzip2-1.0.6-msys2.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-shared
  make all
  make PREFIX="${srcdir}/dest" install
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make test
}

package_bzip2() {
  depends=('libbz2')

  mkdir -p ${pkgdir}/usr/bin

  cp -f ${srcdir}/dest/bin/b* ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/share ${pkgdir}/usr/

}

package_libbz2() {
  groups=('libraries')
  depends=('gcc-libs')

  mkdir -p ${pkgdir}/usr/bin

  cp -f ${srcdir}/dest/bin/*.dll ${pkgdir}/usr/bin/
}

package_libbz2-devel() {
  pkgdesc="Libbz2 headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libbz2=${pkgver}")

  mkdir -p ${pkgdir}/usr/{include,lib}

  cp -rf ${srcdir}/dest/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/lib ${pkgdir}/usr/
}
