# Maintainer: Martell Malone <martellmalone@gmail.com>

pkgname=bzr
pkgver=2.6.0
pkgrel=3
pkgdesc="Bazaar is a version control system that helps you track project history over time and to collaborate easily with others."
arch=('i686' 'x86_64')
url="http://bazaar.canonical.com/en/"
license=('GPL')
groups=('VCS')
depends=('python2')
#optdepends=('python-paramiko: for sftp support')
source=("https://launchpad.net/${pkgname}/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"{,.sig}
        0001-setup.py-man-install.patch
        0002-add-msys2-certs-location.patch)
sha256sums=('0994797182eb828867eee81cccc79480bd2946c99304266bc427b902cf91dab0'
            'SKIP'
            'f8b22c317e0b47b4cae024533b00ee3c682275963041b3cc7d8067ee6ca0cda3'
            'c4e5ea569825e8fa4acd59cce372b95f616f16656e9826b830f071133f7a9a8a')

prepare(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-setup.py-man-install.patch
  patch -p1 -i ${srcdir}/0002-add-msys2-certs-location.patch
  sed -e 's|/usr/bin/env python|/usr/bin/env python2|' \
      -i bzrlib/{plugins/bash_completion/bashcomp.py,tests/ssl_certs/create_ssls.py,patiencediff.py,_patiencediff_py.py}
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  # bash-completion
  install -D -m644 contrib/bash/bzr "${pkgdir}/usr/share/bash-completion/completions/bzr"
}
