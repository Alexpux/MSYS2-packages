# Maintainer: James Ross-Gowan <rossymiles@gmail.com>

pkgname=('protobuf' 'protobuf-devel')
pkgver=2.6.1
pkgrel=1
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=('i686' 'x86_64')
url='https://developers.google.com/protocol-buffers/'
groups=('libraries')
license=('BSD')
depends=('gcc-libs' 'zlib')
makedepends=('zlib-devel')
source=("https://github.com/google/protobuf/releases/download/v$pkgver/protobuf-$pkgver.tar.bz2"
        config.guess
        config.sub
        01-msysize.patch)
sha256sums=('ee445612d544d885ae240ffbcbf9267faa9f593b7b101f21d58beceb92661910'
            '4bdec63f60b9ad86ac82c24df9b9b0537ac5c056a0effeed33923c807ea2983e'
            '5f683355b19d4f2342c03a99dfc2c792593b4bd1b0eb4e4132cb1dfce2c5cd11'
            'cb1c051303341a7619e8ed5dd9c268632c368449cacaa05ecb78d5a1000218b9')

prepare() {
  cd "${srcdir}/protobuf-${pkgver}"

  cp "$srcdir/config.sub" .
  cp "$srcdir/config.guess" .
  patch -p1 -i "$srcdir/01-msysize.patch"
  ./autogen.sh
}

build() {
  cd "${srcdir}/protobuf-${pkgver}"

  ./configure \
    --host=${CHOST} \
    --prefix=/usr
  make
  make DESTDIR="$srcdir/dest" install
}

check() {
  cd "${srcdir}/protobuf-${pkgver}"
  make check
}

package_protobuf() {
  mkdir -p "$pkgdir/usr/bin"
  cp -f "$srcdir/dest/usr/bin/"*.dll "$pkgdir/usr/bin/"
  install -Dm644 "$srcdir/protobuf-${pkgver}/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_protobuf-devel() {
  pkgdesc="Protobuf headers and libraries"
  groups=('development')
  depends=("protobuf=${pkgver}")

  mkdir -p "$pkgdir/usr/bin"
  cp -f "$srcdir/dest/usr/bin/"*.exe "$pkgdir/usr/bin/"
  cp -rf "$srcdir/dest/usr/include" "$pkgdir/usr/"
  cp -rf "$srcdir/dest/usr/lib" "$pkgdir/usr/"
}
