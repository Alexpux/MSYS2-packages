# $Id: PKGBUILD 135906 2015-06-26 14:54:21Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>
# Contributor: Wieland Hoffmann <themineo+aur@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-py
pkgname=('python3-py' 'python2-py')
pkgver=1.4.34
pkgrel=1
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="https://pylib.readthedocs.io/"
makedepends=('python3-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/p/py/py-${pkgver}.tar.gz")
sha512sums=('706617f4075663bbdfdbccf9014a7332566cb4342903e51d297c86319492acc39caf98b63b5803f715bbc64adf93d4bbab9576360288b9d78a017c5abd6ce3cb')

prepare() {
  cp -a py-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/py-${pkgver}"
  /usr/bin/python3 setup.py build

  cd "$srcdir/py-${pkgver}-py2"
  /usr/bin/python2 setup.py build
}

package_python3-py() {
  depends=('python3')

  cd py-${pkgver}
  /usr/bin/python3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-py() {
  depends=('python2')

  cd py-${pkgver}-py2
  /usr/bin/python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

