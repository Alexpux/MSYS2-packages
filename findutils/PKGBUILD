# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=findutils
pkgver=4.6.0
pkgrel=1
pkgdesc="GNU utilities to locate files"
arch=('i686' 'x86_64')
license=('GPL3')
groups=('base')
depends=('libiconv' 'libintl')
makedepends=('libiconv-devel' 'gettext-devel')
url="http://www.gnu.org/software/findutils"
source=(http://ftp.gnu.org/pub/gnu/findutils/${pkgname}-${pkgver}.tar.gz{,.sig}
        findutils-4.5.12-msys2.patch)
install=findutils.install
sha1sums=('f18e8aaee3f3d4173a1f598001003be8706d28b0'
          'SKIP'
          'b6aa140fc54356c79d2b494a8cb4255155fd723f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i $srcdir/findutils-4.5.12-msys2.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Don't build or install locate because we use mlocate,
  # which is a secure version of locate.
  # sed -i '/^SUBDIRS/s/locate//' Makefile.in

  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    DEFAULT_ARG_SIZE="(32u*1024)"

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
