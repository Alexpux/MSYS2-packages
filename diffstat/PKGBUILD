# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=diffstat
pkgver=1.58
pkgrel=1
pkgdesc="Display a histogram of diff changes"
arch=('i686' 'x86_64')
url="http://invisible-island.net/diffstat"
depends=('msys2-runtime')
groups=('base-devel')
license=('MIT')
source=("ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver}.tgz"
        diffstat-1.58-msys2.patch)
sha256sums=('fad5135199c3b9aea132c5d45874248f4ce0ff35f61abb8d03c3b90258713793'
            'c4ec808391eea37809ba1f3eb043f01d52b486798db713cd6aef34d9f2a29162')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ${srcdir}/diffstat-1.58-msys2.patch
}

build () {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --build=${CHOST} \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir}/ install
  install -Dm644 package/debian/copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
