# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=mintty
pkgver=2.7.3
pkgrel=1
epoch=1
pkgdesc="Terminal emulator with native Windows look and feel"
arch=('i686' 'x86_64')
license=('GPL3')
groups=('base')
depends=('sh')
url="https://mintty.github.io"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mintty/mintty/archive/${pkgver}.tar.gz
        001-fix-identation-without-guard.patch)
sha256sums=('eeaa5e2b5ed68354ae2dbea3ce4998cbbd620f13b330ab64383fd4e7e6b89e47'
            'bc6011ebaa214083b6f15c6f21f217b79435139e549891b49cfd1380651a8c19')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-fix-identation-without-guard.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p ${pkgdir}/usr/{bin,share}
  mkdir -p ${pkgdir}/usr/share/man/man1
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}

  install -m755 bin/mintty.exe ${pkgdir}/usr/bin/mintty.exe
  install -m644 docs/mintty.1 ${pkgdir}/usr/share/man/man1/mintty.1

  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE.Oxygen ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE.PuTTY ${pkgdir}/usr/share/licenses/${pkgname}/
}
