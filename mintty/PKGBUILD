# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=mintty
pkgver=2.7.6
pkgrel=1
epoch=1
pkgdesc="Terminal emulator with native Windows look and feel"
arch=('i686' 'x86_64')
license=('GPL3')
groups=('base')
depends=('sh')
url="https://mintty.github.io"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mintty/mintty/archive/${pkgver}.tar.gz
        0002-add-msys2-launcher.patch
        0003-fix-current-dir-inheritance-for-alt-f2-on-msys2.patch)
sha256sums=('67092f0c0c0291c52e6ad65747706fd704d6d09ab11e14b01004be30706b3bbd'
            '2206240168fa481e54346406e96820a1e0a678a9cc21076bd8fac4c97c527dd4'
            'd2398c8586c8eaa04dc2f49d663855f420a17feec918f6ac63a8b975037cd86c')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/0002-add-msys2-launcher.patch
  patch -p1 -i ${srcdir}/0003-fix-current-dir-inheritance-for-alt-f2-on-msys2.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p ${pkgdir}/usr/{bin,share}
  mkdir -p ${pkgdir}/usr/share/man/man1
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}

  install -m755 bin/mintty.exe ${pkgdir}/usr/bin/mintty.exe
  install -m644 docs/mintty.1 ${pkgdir}/usr/share/man/man1/mintty.1

  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE.Oxygen ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE.PuTTY ${pkgdir}/usr/share/licenses/${pkgname}/
}
