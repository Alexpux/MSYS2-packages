# Maintainer: Masanori Shirayama <mashir43@gmail.com>

pkgname=fish
pkgver=2.5.0
pkgrel=1
epoch=
pkgdesc="a smart and user-friendly command line shell"
arch=('i686' 'x86_64')
url="https://fishshell.com/"
license=('GPL')
groups=()
depends=('gcc-libs' 'ncurses' 'gettext' 'libiconv' 'man-db')
makedepends=('gcc' 'ncurses-devel' 'gettext-devel' 'intltool' 'libiconv-devel' 'doxygen')
optdepends=('python: for manual page completion parser and web configuration tool')
install=fish.install
source=("https://fishshell.com/files/$pkgver/$pkgname-$pkgver.tar.gz"
        01-msysize.patch)
sha256sums=('f8c0edadca2de379ccf305aeace660a9255fa2180c72e85e97705a24c256b2a5'
            '1e2743177c499e890d681be685fed6f849e42cb282e13b4e56324169a43bd987')

prepare() {
  cd "${srcdir}/$pkgname-$pkgver"
  patch -p1 -i ${srcdir}/01-msysize.patch
  autoreconf -ivf
}

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure --prefix=/usr --with-doxygen --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
