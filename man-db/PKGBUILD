# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=man-db
pkgver=2.7.4
pkgrel=1
pkgdesc="A utility for reading man pages"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/man-db/"
license=('GPL' 'LGPL')
groups=('base-devel')
depends=( 'bash' 'gdbm' 'zlib' 'groff' 'libpipeline' 'less')
optdepends=('gzip')
makedepends=('libgdbm-devel' 'libpipeline-devel' 'zlib-devel' 'pkg-config')
backup=('etc/man_db.conf')
conflicts=('man')
provides=('man')
replaces=('man')
install=${pkgname}.install
source=(http://download-mirror.savannah.gnu.org/releases/man-db/${pkgname}-${pkgver}.tar.xz{,.sig}
        convert-mans
        man-db-2.6.7.1-msysize.patch)
sha256sums=('153f4d3c33f5f9b0c8484bb39d9d271f6ae4aa1b3f5d6d515879692dba944f0b'
            'SKIP'
            '15c9452984c06335543d9692e25d7825063d39e4d7897a224a36bbbd5e0ffaa8'
            'a34881dc86c6e60204a6cddb33a6c1e66c397ef86d31ca8155ffb6308526120c')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/man-db-2.6.7.1-msysize.patch

  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --with-db=gdbm \
    --disable-setuid \
    --enable-mandirs=GNU \
    --enable-static \
    --disable-shared \
    --with-sections="1 n l 8 3 0 2 5 4 9 6 7"
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # part of groff pkg
  rm -f ${pkgdir}/usr/bin/zsoelim

  # script from LFS to convert manpages, see
  # http://www.linuxfromscratch.org/lfs/view/6.4/chapter06/man-db.html
  install -D -m755 ${srcdir}/convert-mans  ${pkgdir}/usr/bin/convert-mans
}
