# Maintainer: Andy Gross <andy.gross@linaro.org>

pkgname=dtc
pkgver=1.4.4
pkgrel=1
pkgdesc="Device Tree Compiler"
arch=('i686' 'x86_64')
url="https://git.kernel.org/pub/scm/utils/dtc/dtc/git"
license=('GPL2')
groups=('base')

source=("https://git.kernel.org/pub/scm/utils/dtc/dtc.git/snapshot/${pkgname}-${pkgver}.tar.gz"
        fpic.patch)
sha256sums=('2f2c0bf4d84763595953885bdcd2159b0b85410018c8ba48cc31b3d6e443e4d8'
            '72c5f92003c3120ee2400939aaf2c0ebc372fc9c922c6ae07b6be8b0c2470a74')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/fpic.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX= DESTDIR="${pkgdir}/usr" install

  # license
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/README.license ${pkgdir}/usr/share/licenses/${pkgname}/README.license
}

