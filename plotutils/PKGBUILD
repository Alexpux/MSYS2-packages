# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgname=plotutils
pkgver=2.6
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc="Set of utilities and libraries for plotting."
url="http://directory.fsf.org/graphics/plotutils.html"
license=("GPL")
makedepends=("libpng-devel")
depends=("libpng" "gcc-libs")
source=(http://ftp.gnu.org/pub/gnu/plotutils/$pkgname-$pkgver.tar.gz
        plotutils-2.6-libpng-1.5.patch
        plotutils-2.6-fix-autoreconf-error.patch
        plotutils-2.6-fix-undefined-linking.patch)
sha1sums=('7921301d9dfe8991e3df2829bd733df6b2a70838'
          '492f0e04f8265ab50d9ba0905a0f5adf3ec06ab8'
          'fc0a0eccc1007093d16ea7c631f764b9b4cabb16'
          'a672f3c4c88fb6bf64b946979b51fe3ff417cd05')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i ${srcdir}/plotutils-2.6-libpng-1.5.patch
  patch -Np1 -i ${srcdir}/plotutils-2.6-fix-autoreconf-error.patch
  patch -Np1 -i ${srcdir}/plotutils-2.6-fix-undefined-linking.patch
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr \
	--with-gnu-ld \
	--enable-libplotter
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
