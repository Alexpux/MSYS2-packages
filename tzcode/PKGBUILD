# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=tzcode
_ver=2017b
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=1
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="https://www.iana.org/time-zones"
license=('GPL')
options=('!emptydirs')
groups=('base')
depends=('coreutils' 'gawk' 'sed')
source=(
        https://www.iana.org/time-zones/repository/releases/tzdata${_ver}.tar.gz
        https://www.iana.org/time-zones/repository/releases/${pkgname}${_ver}.tar.gz
        tzcode-makefile.patch
        tzcode-makefile-exe-extensions.patch
        tzcode-tzfile.patch
        tzcode-man1.patch
        tzcode-man3.patch
        tzcode-man8.patch)
sha256sums=('f8242a522ea3496b0ce4ff4f2e75a049178da21001a08b8e666d8cbe07d18086'
            '4d1735bb54e22b8d7443d4d1f1a13d007ae11be79a35e51f8e8322fb8e292d40'
            'e04b27832ff14af0d10526a33de9138c1b978713bf23a44bc389abcf9f81b13b'
            '96ab15be155fd382366aff571d87f8616891509ef575a4b41f7c49f82576c170'
            'cefc765ca2c471590f433cb4927553ba0c9467f3d3857c55028d42149eae8b7f'
            '2ccfcf8e4392905c7f00165921ea60663112e8e8500a1b100a24fdac69e2f111'
            '5f69e8b81ea43d67b8f50b1f0d39c1014fee213097a7049331241a15faa14513'
            '79d2e53258fdf728980d850eaa296684c9eb67a86484f4e04e232b7977eddc8a')

timezones=('africa' 'antarctica' 'asia' 'australasia'
           'europe' 'northamerica' 'southamerica'
           'pacificnew' 'etcetera' 'backward'
           'systemv' 'factory')

prepare() {
  cd ${srcdir}

  patch -p1 -i ${srcdir}/tzcode-makefile.patch
  patch -p1 -i ${srcdir}/tzcode-makefile-exe-extensions.patch
  patch -p1 -i ${srcdir}/tzcode-tzfile.patch
  patch -p1 -i ${srcdir}/tzcode-man1.patch
  patch -p1 -i ${srcdir}/tzcode-man3.patch
  patch -p1 -i ${srcdir}/tzcode-man8.patch

}

build() {
 cd ${srcdir}

 make CFLAGS="${CFLAGS} -std=gnu99"
}

package() {
  cd ${srcdir}

  make -j1 DESTDIR=${pkgdir} install
  mv ${pkgdir}/usr/share/zoneinfo-leaps ${pkgdir}/usr/share/zoneinfo/right
  mv ${pkgdir}/usr/share/zoneinfo-posix ${pkgdir}/usr/share/zoneinfo/posix

  ./zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo ${timezones[@]}
  ./zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo/posix ${timezones[@]}
  ./zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo/right -L leapseconds ${timezones[@]}

  ./zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo -p America/New_York
  install -m444 -t ${pkgdir}/usr/share/zoneinfo iso3166.tab zone.tab
}
