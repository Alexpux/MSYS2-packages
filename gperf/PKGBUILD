# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gperf
pkgver=3.0.4
pkgrel=3
pkgdesc="Perfect hash function generator"
arch=('x86_64' 'i686')
url="https://www.gnu.org/software/gperf/"
license=('GPL3')
groups=('base-devel')
depends=('gcc-libs' 'info')
makedepends=('gcc' 'texinfo')
install=gperf.install
source=("https://ftp.gnu.org/pub/gnu/gperf/${pkgname}-${pkgver}.tar.gz"{,.sig}
        gperf-3.0.4-tests-cygwin-setmode.patch)
sha256sums=('767112a204407e62dbc3106647cf839ed544f3cf5d0f0523aaa2508623aad63e'
            'SKIP'
            'fe1295821511164d60f870981b7fd31dacfd3ab7bd9f2f3b0c5029201700aef9')
options=('!makeflags')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/gperf-3.0.4-tests-cygwin-setmode.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --docdir=/usr/share/gperf/doc/
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
