# Maintainer: Maciej Suminski <maciej.suminski@cern.ch>
#
# Based on Arch ngspice PKGBUILD:
# https://git.archlinux.org/svntogit/community.git/tree/trunk/PKGBUILD?h=packages/ngspice
# Original contributors:
#   Contributor: Kyle Keen <keenerd@gmail.com>
#   Contributor: Abhishek Dasgupta <abhidg@gmail.com>
#   Contributor: Jason Taylor <jftaylor21@gmail.com>
#   Contributor: Luis Henrique <lmello.009@gmail.com>

pkgname=ngspice
pkgname="${pkgname}"
pkgver=26
pkgrel=3
pkgdesc='Mixed-level/Mixed-signal circuit simulator based on Spice3f5, Ciber1b1, and Xspice.'
url='http://ngspice.sourceforge.net'
license=('BSD')
arch=('i686' 'x86_64')
makedepends=("gcc-libs")
source=("http://downloads.sourceforge.net/project/${pkgname}/ng-spice-rework/${pkgver}/$pkgname-${pkgver}.tar.gz"
        "http://downloads.sourceforge.net/project/${pkgname}/ng-spice-rework/${pkgver}/$pkgname-doc-${pkgver}.tar.gz")
sha256sums=('51e230c8b720802d93747bc580c0a29d1fb530f3dd06f213b6a700ca9a4d0108'
            'adc722cf627ee4506ea612c3fcb79f9c73c497e328547ab2d1ea97da588459cb')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"

  # FS#45230, create so lib
  # shared lib sets flags and modifies headers, needs dedicated pass
  # adding --with-readline disables libngspice-0.dll
  ../${pkgname}-${pkgver}/configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --enable-openmp \
              --enable-xspice \
              --enable-cider \
              --with-ngshared
  make

  ../${pkgname}-${pkgver}/configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --enable-xspice \
              --enable-cider \
              --enable-openmp
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make install DESTDIR="${pkgdir}"
  install -D -m755 "src/.libs/libngspice-0.dll" "${pkgdir}/usr/bin"
  install -D -m644 "${srcdir}/${pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644  "${srcdir}/${pkgname}-doc-${pkgver}/manual.pdf" "${pkgdir}/usr/share/doc/${pkgname}/manual.pdf"
}
