# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=('mpfr' 'mpfr-devel')
_pkgver=4.0.0
_patchlevel=1
pkgver=${_pkgver}
if [ -n "${_patchlevel}" ]; then
  pkgver+=.${_patchlevel}
fi
#pkgver=${_pkgver}
pkgrel=3
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
url="http://www.mpfr.org/"
license=('LGPL')
depends=('gmp>=5.0')
makedepends=('gmp-devel>=5.0')
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc}
        mpfr-3.1.6-p1.patch
        mpfr-4.0.0-msys2.patch)
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D')
sha256sums=('fbe2cd1418b321f5c899ce4f0f0f4e73f5ecc7d02145b0e1fd096f5c3afb8a1d'
            'SKIP'
            '5d37e47d9d206b1b306dfd590b3019c3f2651013403bb5cb290d1173ed080b28'
            '54d418ee7afc1f3e74f4db08e8205eec48751f4d8da96f536d4c7e706a5d67c0')

prepare() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
#  patch -p1 -i ${srcdir}/mpfr-3.1.6-p1.patch
  patch -p1 -i ${srcdir}/mpfr-4.0.0-msys2.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --enable-shared \
    --disable-static
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make check
}

package_mpfr() {
  groups=('libraries')
  install=mpfr.install

  mkdir -p ${pkgdir}/usr
#This bit is a crude hack to prevent breakage
#with gcc and awk.  That stuff breaks build
#processes including autoconf.
  cp ${srcdir}/dest/usr/bin/msys-mpfr-6.dll \
    ${srcdir}/dest/usr/bin/msys-mpfr-4.dll

  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_mpfr-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("mpfr=${pkgver}" 'gmp-devel')
  pkgdesc="MPFR headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
