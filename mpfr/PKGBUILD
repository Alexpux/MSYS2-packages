# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=('mpfr' 'mpfr-devel')
_pkgver=3.1.5
_patchlevel=1
pkgver=${_pkgver}
if [ -n "${_patchlevel}" ]; then
  pkgver+=.${_patchlevel}
fi
#pkgver=${_pkgver}
pkgrel=3
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
url="http://www.mpfr.org/"
license=('LGPL')
depends=('gmp>=5.0')
makedepends=('gmp-devel>=5.0')
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc}
        mpfr-3.1.5-p1.patch
        mpfr-3.1.2-msys2.patch)
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D')
sha256sums=('015fde82b3979fbe5f83501986d328331ba8ddf008c1ff3da3c238f49ca062bc'
            'SKIP'
            'd9a45fe5757301bcd5f995c7b12f1fc34c28d0ecd4e44e8f3db97c8145b111b9'
            'c2203787c656ef82878d5fe839eb135232c3387668490ef59d84880f3573c308')

prepare() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  patch -p1 -i ${srcdir}/mpfr-3.1.5-p1.patch
  patch -p1 -i ${srcdir}/mpfr-3.1.2-msys2.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --enable-shared \
    --disable-static
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make check
}

package_mpfr() {
  groups=('libraries')
  install=mpfr.install

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_mpfr-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("mpfr=${pkgver}" 'gmp-devel')
  pkgdesc="MPFR headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
