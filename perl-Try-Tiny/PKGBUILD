# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Try-Tiny
pkgname=perl-${_realname}
pkgver=0.22
pkgrel=2
pkgdesc="Minimal try/catch with proper localization of \$@"
arch=('any')
url="http://search.cpan.org/dist/Try-Tiny"
groups=('perl-modules')
license=('MIT')
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/D/DO/DOY/${_realname}-${pkgver}.tar.gz")
sha256sums=('60fba46f4693d33d54539104f9001df008dabb400b6837e9605c39a6ee6a1b19')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
