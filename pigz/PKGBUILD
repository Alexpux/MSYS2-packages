# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski
# Contributor: Laszlo Papp <djszapi2@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pigz
pkgver=2.4
pkgrel=1
pkgdesc='Parallel implementation of the gzip file compressor'
arch=('x86_64' 'i686')
url='https://www.zlib.net/pigz'
license=('custom')
depends=('zlib')
source=(https://www.zlib.net/$pkgname/$pkgname-$pkgver.tar.gz
        $pkgname-$pkgver.tar.gz.sig::http://zlib.net/pigz/pigz-2.4-sig.txt)
sha256sums=('a4f816222a7b4269bd232680590b579ccc72591f1bb5adafcd7208ca77e14f73')
validpgpkeys=('58BCAFBA')

build() {
  cd $pkgname-$pkgver

  make
}

package() {
  cd $pkgname-$pkgver

  install -Dm755 "$srcdir"/$pkgname-$pkgver/$pkgname \
    "$pkgdir"/usr/bin/$pkgname 

  pushd "$pkgdir"/usr/bin
    ln -s pigz unpigz 
  popd

  install -Dm644 "$srcdir"/$pkgname-$pkgver/$pkgname.1 \
    "$pkgdir"/usr/share/man/man1/$pkgname.1
  install -Dm644 "$srcdir"/$pkgname-$pkgver/$pkgname.pdf \
    "$pkgdir"/usr/share/doc/$pkgname/$pkgname.pdf
  install -Dm644 "$srcdir"/$pkgname-$pkgver/README \
    "$pkgdir"/usr/share/licenses/$pkgname/README
}
