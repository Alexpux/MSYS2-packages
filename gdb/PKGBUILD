# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gdb
pkgver=7.11.1
pkgrel=1
_gcc_ver=6.3.0
pkgdesc="GNU Debugger (MSYS2 version)"
arch=('i686' 'x86_64')
license=('GPL3')
url="https://www.gnu.org/software/gdb/"
groups=('base-devel')
depends=("libiconv" "zlib" "expat" "python2" "libexpat" "libreadline")
#checkdepends=('dejagnu' 'bc')
makedepends=("libiconv-devel" "zlib-devel" "ncurses-devel" "liblzma-devel" "libexpat-devel" "libreadline-devel")
options=('staticlibs' '!distcc' '!ccache')
source=("https://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.xz"{,.sig}
        'gdbinit'
        0001-Teach-gdb-how-to-unwind-cygwin-_sigbe-and-sigdelayed.patch
        0002-7.8-windows-nat-cygwin.patch
        0003-Better-handling-for-realpath-failures-in-windows_mak.patch
        0004-Support-settings-thread-name-MS-Windows.patch
        0005-Remove-a-spurious-target_terminal_ours-from-windows_.patch
        '7.8-symtab-cygwin.patch'
        'gdb-7.11.1-msysize.patch')
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3')
sha256sums=('e9216da4e3755e9f414c1aa0026b626251dfc57ffe572a266e98da4f6988fc70'
            'SKIP'
            '2bbe7eddb1828c394d0ff99777058df79b1a596172603bb0e30d983fc1ea8785'
            'dd30d69f767dc48c6ed694f6c4934e246fc04372af65f7a112dbbb4644762a61'
            '7253203b0f8d679daea84578910a9f39da2eed1887806ade0282e77e45196716'
            'cb2c3aad769130310c5644e8cf125a922c524914b09dae8959ce2c6632de53bd'
            'a5792649864915c60f11ec9e64361e77eeddbd151a1419abc1954e82f277dbf2'
            'cadde28c9b294d2193d88202b88e559fd30df500af0d1fc12bc9eb6eccfde6e8'
            '100680e4ddba097accf1159d096ec50d3be36cf3ed15346384c64978297e1aff'
            '328b5d401ac9e442cc0927a14b40b9d8bbff98bf3aa14c28698a14f2b0f4a762')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/0001-Teach-gdb-how-to-unwind-cygwin-_sigbe-and-sigdelayed.patch
  patch -p1 -i ${srcdir}/0002-7.8-windows-nat-cygwin.patch
  patch -p1 -i ${srcdir}/0003-Better-handling-for-realpath-failures-in-windows_mak.patch
  patch -p1 -i ${srcdir}/0004-Support-settings-thread-name-MS-Windows.patch
  patch -p1 -i ${srcdir}/0005-Remove-a-spurious-target_terminal_ours-from-windows_.patch
  patch -p2 -i ${srcdir}/7.8-symtab-cygwin.patch
  patch -p1 -i ${srcdir}/gdb-7.11.1-msysize.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure \
    --build=${CHOST} \
    --host=${CHOST} \
    --prefix=/usr \
    --enable-64-bit-bfd \
    --disable-werror \
    --with-system-gdbinit=/etc/gdbinit \
    --with-python=/usr/bin/python2 \
    --without-tcl \
    --without-tk \
    --without-guile \
    --with-expat \
    --with-lzma \
    --with-system-readline \
    --with-libiconv-prefix=/usr \
    --with-zlib \
    --enable-tui
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install "custom" system gdbinit
  install -D -m644 ${srcdir}/gdbinit ${pkgdir}/etc/gdbinit
  sed -i 's|%GCC_NAME%|gcc-'${_gcc_ver}'|g' ${pkgdir}/etc/gdbinit

  # these are shipped by binutils
  rm -fr ${pkgdir}/usr/{include,lib}/ ${pkgdir}/usr/share/locale/
  rm -f ${pkgdir}/usr/share/info/{bfd,configure,standards}.info
}
