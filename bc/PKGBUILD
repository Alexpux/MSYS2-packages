# Maintainer: Sundaram Ramaswamy <legends2k@yahoo.com>

pkgname=bc
pkgver=1.06
pkgrel=3
pkgdesc="An arbitrary precision calculator language"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/bc"
license=('GPL')
depends=('libreadline' 'ncurses')
builddepends=('gcc' 'libreadline-devel' 'ncurses-devel')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        0001-Fix-readline-prototype.patch)
sha256sums=('4ef6d9f17c3c0d92d8798e35666175ecd3d8efac4009d6457b5c99cea72c0e33'
            'c8318a28af3571bedf28ddacb267f2e477ad7c4d9bc34425a0f504023a0cbb49')

prepare () {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-Fix-readline-prototype.patch
}

build () {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}
  
  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --with-readline

  make
}

check () {
  cd build-${CHOST}
  echo "quit" | ./bc/bc -l "${srcdir}"/${pkgname}-${pkgver}/Test/checklib.b # 10 failures
}

package () {
  cd build-${CHOST}
  make prefix="${pkgdir}/usr" install
}
