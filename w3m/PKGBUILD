# Maintainer: Johannes Schindelin <johannes.schindelin@gmx.de>

pkgname=w3m
_basever=0.5.3
pkgver=${_basever}
pkgrel=1
pkgdesc="w3m is a pager with WWW capability"
arch=('i686' 'x86_64')
url="http://w3m.sourceforge.net/"
license=('BSD')
depends=('libgc' 'libiconv' 'libintl' 'openssl' 'zlib' 'ncurses')
makedepends=('libgc-devel' 'libiconv-devel' 'gettext-devel' 'openssl-devel' 'zlib-devel' 'libtool' 'ncurses-devel')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
  '0001-Accomodate-MSys2-s-libgc-API.patch'
  '0002-MSys-Make-sure-that-setmode-is-declared.patch')
md5sums=('1b845a983a50b8dec0169ac48479eacc'
  'bbcb377e3027726fed88e8b489d9f538'
  '847a8d5d4d568116da7fcf22ad256409')

prepare() {
  cd ${srcdir}/${pkgname}-${_basever}

  cp /usr/share/libtool/build-aux/config.{guess,sub} ./
  patch -p1 <${srcdir}/0001-Accomodate-MSys2-s-libgc-API.patch
  patch -p1 <${srcdir}/0002-MSys-Make-sure-that-setmode-is-declared.patch

  ./configure
}

build() {
  cd ${srcdir}/${pkgname}-${_basever}

  make
}

package() {
  cd ${srcdir}/${pkgname}-${_basever}

  make DESTDIR=${pkgdir} install
}
