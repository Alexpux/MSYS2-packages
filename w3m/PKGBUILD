# Maintainer: Johannes Schindelin <johannes.schindelin@gmx.de>

pkgname=w3m
_basever=0.5.3
pkgver=${_basever}
pkgrel=1
pkgdesc="w3m is a pager with WWW capability"
arch=('i686' 'x86_64')
url="https://w3m.sourceforge.io/"
license=('BSD')
depends=('libgc' 'libiconv' 'libintl' 'openssl' 'zlib' 'ncurses')
makedepends=('libgc-devel' 'libiconv-devel' 'gettext-devel' 'openssl-devel' 'zlib-devel' 'libtool' 'ncurses-devel')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        '0001-Accomodate-MSys2-s-libgc-API.patch'
        '0002-MSys-Make-sure-that-setmode-is-declared.patch')
sha256sums=('e994d263f2fd2c22febfbe45103526e00145a7674a0fda79c822b97c2770a9e3'
            '9a92075378f8e32ea457bb21d4b682ee722fe2c24533ce84d7540564ca076394'
            '06271c4c614112cfa9b7aeb1013f265d4abdf353d264261e1861a482b3f1d396')

prepare() {
  cd ${srcdir}/${pkgname}-${_basever}

  cp /usr/share/libtool/build-aux/config.{guess,sub} ./
  patch -p1 <${srcdir}/0001-Accomodate-MSys2-s-libgc-API.patch
  patch -p1 <${srcdir}/0002-MSys-Make-sure-that-setmode-is-declared.patch

  ./configure
}

build() {
  cd ${srcdir}/${pkgname}-${_basever}

  make
}

package() {
  cd ${srcdir}/${pkgname}-${_basever}

  make DESTDIR=${pkgdir} install
}
