# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=rsync
pkgver=3.1.2
pkgrel=1
pkgdesc="A file transfer program to keep remote files in sync"
arch=('i686' 'x86_64')
url="https://rsync.samba.org"
groups=('net-utils')
license=('GPL3')
depends=('perl')
source=("https://rsync.samba.org/ftp/rsync/$pkgname-$pkgver.tar.gz"
        "https://rsync.samba.org/ftp/rsync/$pkgname-$pkgver.tar.gz.asc"
        rsync-3.1.0-msys2.patch
        ssh-6-option.patch)
sha256sums=('ecfa62a7fa3c4c18b9eccd8c16eaddee4bd308a76ea50b5c02a5840f09c0a1c2'
            'SKIP'
            'f7dfeb7162abafb3a6bd58d3df23ec0519df4a8c0b6ebb28592844468d5f9ce6'
            'ea88046d8935e7a597cca7379d21edffefa520792d896bd7ce663b4cdc0f2069')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ${srcdir}/rsync-3.1.0-msys2.patch
  patch -p1 -i ${srcdir}/ssh-6-option.patch

  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
      --build=${CHOST} \
      --prefix=/usr \
      --without-included-zlib
  
  make reconfigure
  make man all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm755 support/rrsync "${pkgdir}/usr/lib/rsync/rrsync"
}
