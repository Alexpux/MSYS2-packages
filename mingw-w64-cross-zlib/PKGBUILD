# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=zlib
_mingw_suff=mingw-w64-cross
pkgname=("${_mingw_suff}-${_realname}")
pkgdesc="Compression library implementing the deflate compression method found in gzip and PKZIP"
pkgver=1.2.8
pkgrel=1
arch=('i686' 'x86_64')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=('')
makedepends=("${_mingw_suff}-gcc")
options=('staticlibs' '!strip' '!libtool' '!emptydirs' '!buildflags')
source=("http://zlib.net/current/${_realname}-${pkgver}.tar.gz"
    01-zlib-1.2.7-1-buildsys.mingw.patch
        02-no-undefined.mingw.patch
        03-dont-put-sodir-into-L.mingw.patch
        04-wrong-w8-check.mingw.patch
        05-fix-a-typo.mingw.patch)
sha256sums=('36658cb768a54c1d4dec43c3116c27ed893e88b02ecfcb44f2166f9c0b7f2a0d'
            'd173a6d5e0ca550bb5ee6f53a4309959413e225e73d1507a984cae52d53c4ed2'
            '44e23000cc4cb7502f303aa252be17ffee66384fe76a66607dc1f4df0a815030'
            '3d039f42194aade91dadf4174f55fd6db349fd8044db93875bed1042dcfe1f31'
            '053fd492a821aca1fcb988f8ccb8857bb62e1826a5c3e5e1794f7f5f17797e3f'
            'd0fb89b59d629a8f8a4685425542a92255633ea3126edc12d21522dc41241fb7')

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  grep -A 24 '^  Copyright' zlib.h > LICENSE

  patch -p2 -i ${srcdir}/01-zlib-1.2.7-1-buildsys.mingw.patch
  patch -p2 -i ${srcdir}/02-no-undefined.mingw.patch
  patch -p2 -i ${srcdir}/03-dont-put-sodir-into-L.mingw.patch
  patch -p2 -i ${srcdir}/04-wrong-w8-check.mingw.patch
  patch -p2 -i ${srcdir}/05-fix-a-typo.mingw.patch
}

build() {
  for _target in ${_targets}; do
    msg "Configuring ${_realname} for ${_target}"
  cp -rf ${srcdir}/${_realname}-${pkgver} ${srcdir}/${_realname}-${_target}
    cd ${srcdir}/${_realname}-${_target}
    CHOST=${_target} ./configure --prefix=/opt/${_target} --shared
  make
  done
}

package() {
  for _target in ${_targets}; do
    msg "Installing ${_realname} for ${_target}"
    cd ${srcdir}/${_realname}-${_target}
    make DESTDIR=${pkgdir} install
  done
}
