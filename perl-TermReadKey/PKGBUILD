# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=TermReadKey
pkgname=perl-${_realname}
pkgver=2.33
pkgrel=1
pkgdesc="Provides simple control over terminal driver modes"
arch=('i686' 'x86_64')
license=('custom')
depends=('perl')
url="http://search.cpan.org/~jstowe/${_realname}/"
groups=('perl-modules')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/J/JS/JSTOWE/${_realname}-${pkgver}.tar.gz)
md5sums=('1b60aa12b05b5c59d09061fa03087fdb')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/usr/share/licenses/${pkgname}/"
  head -7 README > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
