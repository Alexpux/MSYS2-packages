# Maintainer: micbou <contact@micbou.com>

pkgname=libqrencode
pkgver=4.0.2
pkgrel=1
pkgdesc='QR Code encoding library'
arch=('i686' 'x86_64')
url='https://fukuchi.org/works/qrencode/'
license=('LGPL')
depends=("mingw-w64-${CARCH}-libpng")
makedepends=('autoconf' 'automake' 'libtool' 'pkg-config' 'gcc')
source=("https://github.com/fukuchi/libqrencode/archive/v${pkgver}.tar.gz")
sha256sums=('43091fea4752101f0fe61a957310ead10a7cb4b81e170ce61e5baa73a6291ac2')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./autogen.sh
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr

  make LDFLAGS="${LDFLAGS} -no-undefined"
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
