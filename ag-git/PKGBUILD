# Maintainer: Mihail Konev <k.mvc@ya.ru>

pkgname=ag-git
srcname=the_silver_searcher
pkgver=0.33.0.1777.b4215ac
pkgrel=1
pkgdesc="A better grep"
arch=('i686' 'x86_64')
license=('Apache')
url="https://github.com/ggreer/the_silver_searcher"
provides=('ag')
conflicts=('ag')
depends=('libintl' 'libpcre' 'liblzma' 'zlib')
makedepends=('gettext-devel' 'pcre-devel' 'liblzma-devel' 'zlib-devel')
source=('git+https://github.com/ggreer/the_silver_searcher.git')
sha256sums=('SKIP')

pkgver() {
  cd ${srcdir}/${srcname}
  printf "%s.%s.%s" \
    "$(git describe --tags | sed -e "s/-.*//")" \
    "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${srcname}

  autoreconf -fi
}

build() {
  cd ${srcdir}/${srcname}
  ./configure --build=${CHOST} \
      --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${srcname}
  make DESTDIR=${pkgdir} install
}
