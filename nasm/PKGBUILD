# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=nasm
pkgver=2.13.01
pkgrel=1
pkgdesc="An 80x86 assembler designed for portability and modularity"
arch=('i686' 'x86_64')
url="http://www.nasm.us"
license=('BSD')
makedepends=('perl-Font-TTF' 'perl-Sort-Versions' 'mingw-w64-i686-fontconfig' 'mingw-w64-i686-ghostscript')
depends=('msys2-runtime')
install=nasm.install
source=("http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.xz"
		"Arial.ttf"
		"Arial-Bold.ttf"
		"Arial-Italic.ttf"
		"Arial-BoldItalic.ttf"
		"SourceCodePro-Bold.ttf"
		"SourceCodePro-Regular.ttf"
		"SourceCodePro-Semibold.ttf")
sha256sums=('aa0213008f0433ecbe07bb628506a5c4be8079be20fc3532a5031fd639db9a5e'
			'SKIP'
			'SKIP'
			'SKIP'
			'SKIP'
			'SKIP'
			'SKIP'
			'SKIP')

fonts=( "Arial.ttf"
		"Arial-Bold.ttf"
		"Arial-Italic.ttf"
		"Arial-BoldItalic.ttf"
		"SourceCodePro-Bold.ttf"
		"SourceCodePro-Regular.ttf"
		"SourceCodePro-Semibold.ttf")

prepare() {
	local f
	
	[ -d ~/.fonts/ ] || mkdir -pv ~/.fonts/
	
	for f in ${fonts[@]}; do
		[ -f ~/.fonts/${f} ] || cp -vf ${srcdir}/${f} ~/.fonts/
	done
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make all -j1
  
  export PATH=${PATH}:/mingw32/bin
  make -C doc
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALLROOT="${pkgdir}" install install_rdf
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/nasm/LICENSE
}
