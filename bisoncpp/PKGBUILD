# Maintainer: LoveSy <shana@zju.edu.cn>
pkgname=bisonc++
pkgver=6.00.00
pkgrel=1
pkgdesc='C++ parser generator'
arch=('i686' 'x86_64')
url='https://fbb-git.github.io/bisoncpp/'
license=('GPL')
# Versions taken from the 'required' file in sources
depends=('libbobcat>=4.02.00')
makedepends=('icmake>=8.01.00' 'yodl>=3.08.01')
optdepends=()
source=("https://github.com/yujincheng08/bisoncpp/archive/${pkgver}-${pkgrel}.tar.gz"
        "manual_license.patch")
sha256sums=('f5a179b33bf32af494ad8bb436eaf9638c762d124bc8efd627c3ec606b30d9bb'
            '6f41ebf87253fe458bd4ed5df297e2c69f10bdb73b929297a233df5bd0cd993e')

build() {
  cd "$srcdir/bisoncpp-${pkgver}-${pkgrel}/bisonc++"

  patch -p1 -i "$srcdir/manual_license.patch"
  CXXFLAGS="$CXXFLAGS --std=c++14"
  # Add the -P option not to use precompiled headers, which can be useful since
  # they require a lot of free space, compared to a normal compilation:
  # ./build -P program
  ./build program
  ./build man
  ./build manual
}

package() {
  cd "$srcdir/bisoncpp-${pkgver}-${pkgrel}/bisonc++"

  # Since 4.12.00, first argument to install is <what to install> (x = all),
  # and second is the base directory
  ./build install x "$pkgdir"
}
