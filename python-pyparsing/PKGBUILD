# Maintainer: atom2013 <atom.long@hotmail.com>

pkgbase=pyparsing
pkgname=("python3-${pkgbase}" "python2-${pkgbase}")
pkgver=2.2.0
pkgrel=2
pkgdesc='General parsing module for Python'
arch=('any')
url='https://pyparsing.wikispaces.com/'
makedepends=('python3-setuptools' 'python2-setuptools' 'subversion')
license=('MIT')
source=("svn://svn.code.sf.net/p/pyparsing/code/tags/pyparsing_$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a pyparsing_$pkgver{,-py2}

  # :/
  sed -i '1i#coding=utf-8' pyparsing_$pkgver-py2/src/unitTests.py
}

build() {
  # Build python 3 module
  cd "$srcdir"/pyparsing_$pkgver/src
  python3 setup.py build

  # Build python 2 module
  cd "$srcdir"/pyparsing_$pkgver-py2/src
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  # Check python3 module
  cd "$srcdir"/pyparsing_$pkgver/src
  python3 unitTests.py

  # Check python2 module
  cd "$srcdir"/pyparsing_$pkgver-py2/src
  python2 unitTests.py
}

package_python3-pyparsing() {
  depends=('python3')

  cd pyparsing_$pkgver/src

  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyparsing() {
  depends=('python2')

  cd pyparsing_$pkgver-py2/src

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
