# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=perl-YAML-Syck
pkgver=1.29
pkgrel=2
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/YAML-Syck/"
license=('BSD' 'custom')
depends=('perl')
groups=('perl-modules')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/T/TO/TODDR/YAML-Syck-$pkgver.tar.gz
        'msysize.patch')
sha256sums=('d3567b3653e610a2a0fd28286ddc7869ccdeba5e6663f9638c22bc17d6cfc939'
            '808289add8a10652ddf25d25e3053ff9b5619daa92dd923f02bf70b2366d17cd')

prepare() {
  cd YAML-Syck-$pkgver
  patch -p1 -i ${srcdir}/msysize.patch
}

build() {
  cd YAML-Syck-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-Syck-$pkgver
  make test
}

package() {
  cd YAML-Syck-$pkgver
  make install DESTDIR="${pkgdir}"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # fix permissons
  find ${pkgdir}/usr -type f \( -name *.dll -o -name *.exe \) | xargs chmod 0755
}
