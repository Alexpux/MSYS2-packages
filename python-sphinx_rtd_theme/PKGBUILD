# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=sphinx_rtd_theme
pkgbase=python-${_realname}
pkgname=("python2-${_realname}" "python3-${_realname}")
pkgver=0.1.9
pkgrel=1
pkgdesc="Python Sphinx Read The Docs Theme (mingw-w64)"
arch=('any')
license=('MIT')
url='https://github.com/snide/sphinx_rtd_theme'
makedepends=("python2-setuptools"
             "python3-setuptools")
options=('staticlibs')
source=("https://pypi.python.org/packages/source/s/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('273846f8aacac32bf9542365a593b495b68d8035c2e382c9ccedcac387c9a0a1')

prepare() {
  cd ${srcdir}
  cp -r ${_realname}-${pkgver} python2-build
  cp -r ${_realname}-${pkgver} python3-build
}

build() {
  cd "${srcdir}"
  for builddir in python{2,3}-build; do
    pushd ${builddir}
    /usr/bin/${builddir%-build} setup.py build
    popd
  done
}

check() {
  cd "${srcdir}"
  for builddir in python{2,3}-build; do
    pushd ${builddir}
    /usr/bin/${builddir%-build} setup.py test
    popd
  done
}

package_python2-sphinx_rtd_theme() {
  depends=("python2")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    /usr/bin/python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python2-${_realname}/COPYING"
}

package_python3-sphinx_rtd_theme() {
  depends=("python3")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    /usr/bin/python3 setup.py install --prefix=/usr --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python3-${_realname}/COPYING"
}
