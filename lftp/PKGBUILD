# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

pkgname=lftp
pkgver=4.6.5
pkgrel=1
pkgdesc="Sophisticated command line based FTP client (net-utils)"
arch=('i686' 'x86_64')
license=('GPL3')
groups=('net-utils')
depends=('gcc-libs'
         'ca-certificates'
         'expat'
         'gettext'
         'libexpat'
         'libgnutls'
         'libiconv'
         'libidn'
         'libintl'
         'libhogweed'
         'libnettle'
         'libp11-kit'
         'libreadline'
         'libtasn1'
         'openssh'
         'zlib')
makedepends=('gcc'
             'gettext-devel'
             'libexpat-devel'
             'libgnutls-devel'
             'libiconv-devel'
             'libidn-devel'
             'libnettle-devel'
             'libp11-kit-devel'
             'libreadline-devel'
             'libtasn1-devel'
             'zlib-devel')
optdepends=('perl: needed for convert-netscape-cookies and verify-file')
url="http://lftp.yar.ru/"
backup=('etc/lftp.conf')
source=(http://lftp.yar.ru/ftp/${pkgname}-${pkgver}.tar.xz{,.asc}
        lftp-4.4.15-msysize.patch
        lftp-4.4.15-no-undefined.patch)
options=('debug' '!strip')
sha256sums=('1fd0920a1791ce0e9e39ffce77ae6619e5dc665f16e9380bafbfc69411eeb71e'
            'SKIP'
            'c81742dad375326b1f595784976cb20f83591b4df4309abf87d664325e91cd10'
            '7abeaeebcfd2215841c5ce649f81b7f3098b5d0694059cf5f1defba86c12cdb9')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/lftp-4.4.15-msysize.patch
  patch -p1 -i ${srcdir}/lftp-4.4.15-no-undefined.patch

  #autoreconf -fi
}

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../${pkgname}-${pkgver}/configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-gnutls \
    --without-openssl \
    --without-included-regex \
    --disable-rpath \
    --disable-static \
    --with-debug
  make
}

check() {
  cd ${srcdir}/build-${CARCH}
  make -k check
}

package() {

  cd ${srcdir}/build-${CARCH}
  make DESTDIR=${pkgdir} install
  
  grep -q 'set ssl:ca-file' ${pkgdir}/etc/lftp.conf || echo 'set ssl:ca-file /etc/pki/ca-trust/extracted/pem/tls-ca-bundle.pem' >> ${pkgdir}/etc/lftp.conf

}
