# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=pkgfile
pkgver=17
pkgrel=1
pkgdesc="A pacman .files metadata explorer"
arch=('i686' 'x86_64')
url="https://github.com/falconindy/pkgfile"
groups=('base' 'base-devel')
license=('MIT')
makedepends=('pcre-devel' 'libarchive-devel' 'libcurl-devel')
depends=('libarchive' 'curl' 'pcre' 'pacman')
options=('emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/falconindy/$pkgname/archive/v$pkgver.tar.gz")

install=pkgfile.install
sha256sums=('de406dae1535296d4e71554cdaae4e1d279ef2c645fcff4405ed1b3d9e8ed0cb')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
}

build() {
  [[ -d "$srcdir/build-${CHOST}" ]] && rm -rf "$srcdir/build-${CHOST}"
  mkdir "$srcdir/build-${CHOST}" && cd "$srcdir/build-${CHOST}"
  #cd "$pkgname-$pkgver"

  ../$pkgname-$pkgver/configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make

  # generate a license file
  sed '/\*\//q' $srcdir/$pkgname-$pkgver/src/pkgfile.c >LICENSE
}

package() {
  cd "$srcdir/build-${CHOST}"

  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
