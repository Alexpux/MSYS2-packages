# Maintainer: James Ross-Gowan <rossymiles@gmail.com>

pkgname=mosh
pkgver=1.2.5
pkgrel=1
pkgdesc="Mobile shell, surviving disconnects with local echo and line editing"
arch=('i686' 'x86_64')
url='http://mosh.mit.edu/'
groups=('net-utils')
license=('GPL3')
depends=('protobuf' 'ncurses' 'zlib' 'libopenssl' 'openssh' 'perl')
makedepends=('protobuf-devel' 'ncurses-devel' 'zlib-devel' 'openssl-devel' 'bash-completion')
source=("http://${pkgname}.mit.edu/${pkgname}-${pkgver}.tar.gz")
sha256sums=('1af809e5d747c333a852fbf7acdbf4d354dc4bbc2839e3afe5cf798190074be3')
options=(!emptydirs)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --host=${CHOST} \
    --prefix=/usr \
    --enable-completion
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
