# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=dejagnu
pkgver=1.5.3
pkgrel=1
pkgdesc="Framework for testing other programs"
arch=('any')
url="https://www.gnu.org/software/dejagnu/"
license=('GPL')
depends=('expect')
#install=dejagnu.install
source=(https://ftp.gnu.org/gnu/dejagnu/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('099b8e364ca1d6248f8e1d32168c4b12677abff4253bbbb4a8ac8cdd321e3f19'
            'SKIP')
validpgpkeys=('CB0B31452BBE862933013D9176362888B55988D4')   # Ben Elliston

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
