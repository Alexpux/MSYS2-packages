# Maintainer: Johannes Schindelin <johannes.schindelin@gmx.de>

_realname=git-flow
pkgname=("${_realname}")
pkgver=0.4.1.108.g15aab26
pkgrel=1
pkgdesc="Git extensions to provide high-level repository operations for Vincent Driessen's branching model"
arch=('i686' 'x86_64')
license=('BSD')
depends=('git' 'util-linux')
url="http://nvie.com/posts/a-successful-git-branching-model/"
source=("${_realname}"::"git+https://github.com/nvie/gitflow.git#branch=develop")
sha1sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"

  # Need to create sane tags to allow `git describe` to work
  for t in $(git tag -l | grep '^[0-9]')
  do
    git rev-parse --quiet --verify v$t >/dev/null ||
    git tag v$t $t^2
  done

  git describe --tags |
  sed -e 's/^v//' -e 'y/-/./'
}

prepare () {
  cd "${srcdir}/${_realname}"

  git submodule update --init

  # Make sure that gitflow-shFlags is handled properly
  test true = "$(git config core.symlinks)" || {
    git config core.symlinks true
    git reset --hard
  }
}

package() {
  cd "${srcdir}/${_realname}"

  make prefix="${pkgdir}/usr/" install
}
