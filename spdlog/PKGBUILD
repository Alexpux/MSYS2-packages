# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

pkgname=spdlog
pkgver=1.8.5
pkgrel=1
pkgdesc='Very fast, header-only/compiled, C++ logging library'
arch=('i686' 'x86_64')
url='https://github.com/gabime/spdlog'
license=('MIT')
makedepends=(gcc cmake ninja)
depends=(fmt)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gabime/spdlog/archive/v${pkgver}.tar.gz")
sha256sums=('944d0bd7c763ac721398dca2bb0f3b5ed16f67cef36810ede5061f35a543b4b8')

build() {
  [[ -d "${srcdir}/build" ]] && rm -rf "${srcdir}/build"
  mkdir -p "${srcdir}/build" && cd "${srcdir}/build"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  /usr/bin/cmake \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DSPDLOG_BUILD_BENCH=OFF \
    -DSPDLOG_BUILD_EXAMPLE=OFF \
    -DSPDLOG_BUILD_TESTS=OFF \
    -DSPDLOG_FMT_EXTERNAL=ON \
    -DSPDLOG_BUILD_SHARED=ON \
    -DCMAKE_RUNTIME_OUTPUT_DIRECTORY="${srcdir}/build" \
    ../${pkgname}-${pkgver}

  ninja
}

package() {
  cd "${srcdir}/build"

  DESTDIR="${pkgdir}" ninja install

  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/"
}
